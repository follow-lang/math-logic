
# 量化等价 `a13` 

```follow
axiom a13.1(set s0, set s1, set s2) {
  |- imp(not(eq(c(s0),c(s1))),imp(eq(c(s1),c(s2)),forall(s0,eq(c(s1),c(s2)))))
  diff (s0, s2) (s1, s2)
}
```

## 超出`follow`语言的证明  

原因和公理 `a10`, `a11` 和 `a12` 类似， 这个公理在 `follow` 中不可证明，在一阶逻辑中可以通过前面的公理证明出来。

```follow
thm a13.weak.1(set s0, set s1, set s2) {
  |- imp(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s2)),forall(s0,eq(c(s1),c(s2)))))
  diff (s0,s1) (s0,s2)
} = {
  a1i(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s2)),forall(s0,eq(c(s1),c(s2)))))
  a5.forall.1(s0, eq(c(s1),c(s2)))
}
```

```follow
// s1 = s0
thm a13.weak.2(set s0, set s1) {
  |- imp(not(eq(c(s0),c(s0))), imp(eq(c(s0),c(s1)),forall(s0,eq(c(s0),c(s1)))))
} = {
  mp(imp(not(eq(c(s0),c(s0))),imp(eq(c(s0),c(s1)),forall(s0,eq(c(s0),c(s1))))), eq(c(s0),c(s0)))
  cont.1(eq(c(s0),c(s0)), imp(eq(c(s0),c(s1)),forall(s0,eq(c(s0),c(s1)))))
  eq.id(s0)
}
```

```follow
// s2 = s0
thm a13.weak.3(set s0, set s1) {
  |- imp(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s0)),forall(s0,eq(c(s1),c(s0)))))
} = {
  rw2(not(eq(c(s0),c(s1))), eq(c(s1),c(s0)), forall(s0,eq(c(s1),c(s0))), eq(c(s0),c(s1)))
  cont.1(eq(c(s0),c(s1)), forall(s0,eq(c(s1),c(s0))))
  eq.com(s1, s0)
}
```

```follow
// s2 = s1
thm a13.weak.4(set s0, set s1) {
  |- imp(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s1)),forall(s0,eq(c(s1),c(s1)))))
} = {
  a1i(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s1)),forall(s0,eq(c(s1),c(s1)))))
  a1i(eq(c(s1),c(s1)), forall(s0,eq(c(s1),c(s1))))
  gen.forall(s0, eq(c(s1),c(s1)))
  eq.id(s1)
}
```

```follow
// s1 = s0, s2 = s0
thm a13.weak.5(set s0) {
  |- imp(not(eq(c(s0),c(s0))), imp(eq(c(s0),c(s0)),forall(s0,eq(c(s0),c(s0)))))
} = {
  cont.1(eq(c(s0),c(s0)), forall(s0,eq(c(s0),c(s0))))
}
```

## 等价定义

```follow
thm a13.2(set s0, set s1, set s2) {
  |- imp(not(eq(c(s0),c(s1))), imp(eq(c(s2),c(s1)), forall(s0,eq(c(s2),c(s1)))))
  |- imp(and(not(eq(c(s0),c(s1))),eq(c(s2),c(s1))), forall(s0,eq(c(s2),c(s1))))
  |- imp(eq(c(s2),c(s1)), imp(not(eq(c(s0),c(s1))), forall(s0,eq(c(s2),c(s1)))))
  diff (s0, s2)
} = {
  com12i(eq(c(s2),c(s1)), not(eq(c(s0),c(s1))), forall(s0,eq(c(s2),c(s1))))
  and.joini(not(eq(c(s0),c(s1))), eq(c(s2),c(s1)), forall(s0,eq(c(s2),c(s1))))
  rw2(not(eq(c(s0),c(s1))), eq(c(s2),c(s1)), forall(s0,eq(c(s2),c(s1))), exist(hs0,and(eq(c(hs0),c(s2)),eq(c(hs0),c(s1)))))
  eq.exist.trans.1(s2, s1, hs0)
  syl(not(eq(c(s0),c(s1))), imp(exist(hs0,and(eq(c(hs0),c(s2)),eq(c(hs0),c(s1)))),forall(s0,eq(c(s2),c(s1)))), forall(hs0,imp(and(eq(c(hs0),c(s2)),eq(c(hs0),c(s1))),forall(s0,eq(c(s2),c(s1))))))
  a4.diff.aee(hs0, and(eq(c(hs0),c(s2)),eq(c(hs0),c(s1))), forall(s0,eq(c(s2),c(s1))))
  gend(hs0, imp(and(eq(c(hs0),c(s2)),eq(c(hs0),c(s1))),forall(s0,eq(c(s2),c(s1)))), not(eq(c(s0),c(s1))))
  and.joinid(not(eq(c(s0),c(s1))), eq(c(hs0),c(s2)), eq(c(hs0),c(s1)), forall(s0,eq(c(s2),c(s1))))
  com12id(not(eq(c(s0),c(s1))), eq(c(hs0),c(s2)), eq(c(hs0),c(s1)), forall(s0,eq(c(s2),c(s1))))
  rw23(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), imp(eq(c(hs0),c(s2)),forall(s0,eq(c(s2),c(s1)))), eq(c(s1),c(hs0)), forall(s0,eq(c(s1),c(hs0))))
  a13.1(s0, s1, hs0)
  eq.com(hs0, s1)
  com12i(forall(s0,eq(c(s1),c(hs0))), eq(c(hs0),c(s2)), forall(s0,eq(c(s2),c(s1))))
  syl(eq(c(hs0),c(s2)), imp(forall(s0,eq(c(s1),c(hs0))),forall(s0,eq(c(s2),c(s1)))), forall(s0,imp(eq(c(s1),c(hs0)),eq(c(s2),c(s1)))))
  a4.aaa(s0, eq(c(s1),c(hs0)), eq(c(s2),c(s1)))
  gend(s0, imp(eq(c(s1),c(hs0)),eq(c(s2),c(s1))), eq(c(hs0),c(s2)))
  eq.trans.1(hs0, s2, s1)
}
```

```follow
thm a13.3(set s0, set s1, set s2) {
  |- imp(not(eq(c(s0),c(s1))), imp(eq(c(s1),c(s2)), forall(s0,eq(c(s1),c(s2)))))
} = {
  rw2(not(eq(c(s0),c(s1))), eq(c(s1),c(s2)), forall(s0,eq(c(s1),c(s2))), exist(hs0,and(eq(c(hs0),c(s1)),eq(c(hs0),c(s2)))))
  eq.exist.trans.1(s1, s2, hs0)
  syl(not(eq(c(s0),c(s1))), imp(exist(hs0,and(eq(c(hs0),c(s1)),eq(c(hs0),c(s2)))),forall(s0,eq(c(s1),c(s2)))), forall(hs0,imp(and(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))),forall(s0,eq(c(s1),c(s2))))))
  a4.diff.aee(hs0, and(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))), forall(s0,eq(c(s1),c(s2))))
  gend(hs0, imp(and(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))),forall(s0,eq(c(s1),c(s2)))), not(eq(c(s0),c(s1))))
  and.joinid(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), eq(c(hs0),c(s2)), forall(s0,eq(c(s1),c(s2))))
  and.spliti(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), imp(eq(c(hs0),c(s2)),forall(s0,eq(c(s1),c(s2)))))
  mpd(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), imp(eq(c(hs0),c(s2)),forall(s0,eq(c(s1),c(s2)))), forall(s0,eq(c(hs0),c(s1))))
  a13.2(s0, s1, hs0)
  com12id(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), forall(s0,eq(c(hs0),c(s1))), eq(c(hs0),c(s2)), forall(s0,eq(c(s1),c(s2))))
  and.spliti(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), eq(c(hs0),c(s2)), imp(forall(s0,eq(c(hs0),c(s1))),forall(s0,eq(c(s1),c(s2)))))
  a4id.aaa(s0, eq(c(hs0),c(s1)), eq(c(s1),c(s2)), and(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))),eq(c(hs0),c(s2))))
  mpd(and(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))),eq(c(hs0),c(s2))), forall(s0,imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2)))), not(eq(c(s0),c(s2))))
  and.joini(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), eq(c(hs0),c(s2)), not(eq(c(s0),c(s2))))
  and.joini(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), imp(eq(c(hs0),c(s2)),not(eq(c(s0),c(s2)))))
  com12i(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), imp(eq(c(hs0),c(s2)),not(eq(c(s0),c(s2)))))
  com12id(eq(c(hs0),c(s1)), not(eq(c(s0),c(s1))), eq(c(hs0),c(s2)), not(eq(c(s0),c(s2))))
  rw3(eq(c(hs0),c(s1)), eq(c(hs0),c(s2)), imp(not(eq(c(s0),c(s1))),not(eq(c(s0),c(s2)))), imp(eq(c(s0),c(s2)),eq(c(s0),c(s1))))
  con.4(eq(c(s0),c(s2)), eq(c(s0),c(s1)))
  rw3(eq(c(hs0),c(s1)), eq(c(hs0),c(s2)), imp(eq(c(s0),c(s2)),eq(c(s0),c(s1))), eq(c(s1),c(s2)))
  eq.trans.1(hs0, s1, s2)
  eq.trans.1(s1, s2, s0)
  and.joini(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), eq(c(hs0),c(s2)), imp(not(eq(c(s0),c(s2))),forall(s0,imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2))))))
  a1i(and(not(eq(c(s0),c(s1))),eq(c(hs0),c(s1))), imp(eq(c(hs0),c(s2)),imp(not(eq(c(s0),c(s2))),forall(s0,imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2)))))))
  rw3(eq(c(hs0),c(s2)), not(eq(c(s0),c(s2))), forall(s0,imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2)))), forall(s0,eq(c(hs0),c(s2))))
  a13.2(s0, s2, hs0)
  a4igen.aaa(s0, eq(c(hs0),c(s2)), imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2))))
  eq.trans.1(hs0, s2, s1)
}
```

```follow
thm a13.4(set s0, set s1, set s2) {
  |- imp(not(eq(c(s0),c(s1))), imp(exist(s0,eq(c(s2),c(s1))),eq(c(s2),c(s1))))
  diff (s0,s2)
} = {
  rw3(not(eq(c(s0),c(s1))), exist(s0,eq(c(s2),c(s1))), eq(c(s2),c(s1)), eq(c(s1),c(s2)))
  eq.com(s1, s2)
  rw3(not(eq(c(s0),c(s1))), exist(s0,eq(c(s2),c(s1))), eq(c(s1),c(s2)), forall(hs0,imp(eq(c(hs0),c(s1)),eq(c(hs0),c(s2)))))
  eq.forall.2(eq(c(hs0),c(s2)), hs0, eq(c(s1),c(s2)), s1)
  eq.trans.1(hs0, s1, s2)
  syl(not(eq(c(s0),c(s1))), imp(exist(s0,eq(c(s2),c(s1))),forall(hs0,imp(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))))), forall(hs0,imp(exist(s0,eq(c(s2),c(s1))),imp(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))))))
  a4.diff.aaa(hs0, imp(eq(c(hs0),c(s1)),eq(c(hs0),c(s2))), exist(s0,eq(c(s2),c(s1))))
  gend(hs0, imp(exist(s0,eq(c(s2),c(s1))),imp(eq(c(hs0),c(s1)),eq(c(hs0),c(s2)))), not(eq(c(s0),c(s1))))
  com12id(not(eq(c(s0),c(s1))), exist(s0,eq(c(s2),c(s1))), eq(c(hs0),c(s1)), eq(c(hs0),c(s2)))
  rw3(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), imp(exist(s0,eq(c(s2),c(s1))),eq(c(hs0),c(s2))), forall(s0,imp(eq(c(s2),c(s1)),eq(c(hs0),c(s2)))))
  a4.diff.aee(s0, eq(c(s2),c(s1)), eq(c(hs0),c(s2)))
  rw3(not(eq(c(s0),c(s1))), eq(c(hs0),c(s1)), forall(s0,imp(eq(c(s2),c(s1)),eq(c(hs0),c(s2)))), forall(s0,eq(c(hs0),c(s1))))
  a13.2(s0, s1, hs0)
  a4igen.aaa(s0, eq(c(hs0),c(s1)), imp(eq(c(s2),c(s1)),eq(c(hs0),c(s2))))
  eq.trans.1(hs0, s1, s2)
}
```
