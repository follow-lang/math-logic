
# 逻辑量词的定义

这里我们定义集合论中用到的逻辑量词 `forall`、`exist`。

逻辑量词的引入让数理逻辑变得复杂起来，也给数理逻辑注入了血肉。

由于新的类型 **集合** 的出现，数理逻辑的连接符从逻辑连接符，这种一张真值表就能概括的符合，扩展到了非0-1范围，极大地扩展了数理逻辑语言的表达能力。

## 基本类型的声明 

集合论中有两个基本类型 `set` 和 `class`。
量词的第一个变量可以接受 `set` 但是不能接受 `class`。
`set` 可以转化成 `class`，但是 `class` 不能接受 `set`。

```follow
type set class
term prop forall(set s0, prop p0) { ∀(s0, p0 ) } 
term prop exist(set s0, prop p0) { ∃(s0, p0) }
term prop eq(class c0, class c1) { ( c0 = c1 )}
term class c(set s0) { s0 }

// 在逻辑量词的证明中，会需要用到隐变量
// 也就是没有出现在定理参数列表里的变量。
term set hs0
term set hs1
term set hs2
term set hs3
term set hs4
term set hs5
```

## `exist` 的定义

```follow
axiom exist.def.1(set s0, prop p1) {
  |- iff(exist(s0, p1), not(forall(s0, not(p1))))
}
```

```follow
thm exist.def.2(set s0, prop p1) {
  |- iff(not(forall(s0, not(p1))), exist(s0, p1))
  |- imp(exist(s0, p1), not(forall(s0, not(p1))))
  |- imp(not(forall(s0, not(p1))), exist(s0, p1))
} = {
  iff.lefti(exist(s0,p1), not(forall(s0,not(p1))))
  iff.righti(not(forall(s0,not(p1))), exist(s0,p1))
  iff.comi(not(forall(s0,not(p1))), exist(s0,p1))
  exist.def.1(s0, p1)
}
```

## `forall` 的四个基本公理

`forall` 由多个公理共同定义，这些公理都可以扩展出一系列与之紧密相关的定理。本教程计划将这些定理和对应的公理用相同名字开头。

```follow
// 泛化公理
axiom gen.forall(set s0, prop p1) {
  |- forall(s0, p1)
  -| p1
}
```

```follow
axiom a4.aaa(set s0, prop p1, prop p2) {
  |- imp(forall(s0, imp(p1, p2)), imp(forall(s0, p1), forall(s0, p2)))
}
```

```follow
// 首次使用 diff 关键词, 要求传入的变量 s0 和 p1 不能包含相同的变量。
// 既不能有相同的参数，也不能有相同的隐变量。
axiom a5.forall.1(set s0, prop p1) {
  |- imp(p1, forall(s0, p1))
  diff (s0, p1)
}
```

```follow
// 存在性定理，看似不起眼，威力非常巨大的定理。
axiom a6.forall(set s0, set s1) {
  |- not(forall(s0, not(eq(c(s0), c(s1)))))
  diff (s0, s1)
}
```

我们看到存在两个公理 `gen.1` 和 `a5.1`，说明 `(p1→∀(s0,p1))` 
只在有限条件下才成立。 在逻辑量词相关命题中，`Deduction`定理不一定成立。有些时候 `-| p1 |- ∀(s0,p1)` 是成立的，但是 `(p1→∀(s0,p1))` 不一定成立。

```follow
thm a6.exist(set s0, set s1) {
  |- exist(s0,  eq(c(s0), c(s1)))
  diff (s0, s1)
} = {
  mp(exist(s0,eq(c(s0),c(s1))), not(forall(s0,not(eq(c(s0),c(s1))))))
  exist.def.2(s0, eq(c(s0),c(s1)))
  a6.forall(s0, s1)
}
```

## 公理 `a4` 基本扩展  

```follow
thm a4i.aaa(set s0, prop p1, prop p2) {
  |- imp(forall(s0, p1), forall(s0, p2))
  -| forall(s0, imp(p1, p2))
} = {
  mp(imp(forall(s0,p1),forall(s0,p2)), forall(s0,imp(p1,p2)))
  a4.aaa(s0, p1, p2)
}
```

```follow
thm a4id.aaa(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, imp(forall(s0, p1), forall(s0, p2)))
  -| imp(p3, forall(s0, imp(p1, p2)))
} = {
  syl(p3, imp(forall(s0,p1),forall(s0,p2)), forall(s0,imp(p1,p2)))
  a4.aaa(s0, p1, p2)
}
```

```follow
thm a4igen.aaa(set s0, prop p1, prop p2) {
  |- imp(forall(s0, p1), forall(s0, p2))
  -| imp(p1, p2)
} = {
  a4i.aaa(s0, p1, p2)
  gen.forall(s0, imp(p1,p2))
}
```

## `a4` 的 `iff` 版本

```follow
thm a4.aaa.iff(set s0, prop p1, prop p2) {
  |- imp(forall(s0, iff(p1, p2)), iff(forall(s0, p1), forall(s0, p2)))
} = {
  iff.introiid.1(forall(s0,iff(p1,p2)), forall(s0,p1), forall(s0,p2))
  a4id.aaa(s0, p1, p2, forall(s0,iff(p1,p2)))
  a4id.aaa(s0, p2, p1, forall(s0,iff(p1,p2)))
  a4i.aaa(s0, iff(p1,p2), imp(p2,p1))
  a4i.aaa(s0, iff(p1,p2), imp(p1,p2))
  gen.forall(s0, imp(iff(p1,p2),imp(p1,p2)))
  gen.forall(s0, imp(iff(p1,p2),imp(p2,p1)))
  iff.left(p1, p2)
  iff.right(p1, p2)
}
```

```follow
thm a4i.aaa.iff(set s0, prop p1, prop p2) {
  |- iff(forall(s0, p1), forall(s0, p2))
  -| forall(s0, iff(p1, p2)) 
} = {
  mp(iff(forall(s0,p1),forall(s0,p2)), forall(s0,iff(p1,p2)))
  a4.aaa.iff(s0, p1, p2)
}
```

```follow
thm a4id.aaa.iff(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, iff(forall(s0, p1), forall(s0, p2)))
  -| imp(p3, forall(s0, iff(p1, p2)))
} = {
  syl(p3, iff(forall(s0,p1),forall(s0,p2)), forall(s0,iff(p1,p2)))
  a4.aaa.iff(s0, p1, p2)
}
```

```follow
thm a4igen.aaa.iff(set s0, prop p1, prop p2) {
  |- iff(forall(s0, p1), forall(s0, p2))
  -| iff(p1, p2)
} = {
  a4i.aaa.iff(s0, p1, p2)
  gen.forall(s0, iff(p1,p2))
}
```

## exist 扩展定义

```follow
thm exist.def.3(set s0, prop p0) {
  |- iff(forall(s0, not(p0)), not(exist(s0, p0)))
  |- iff(not(exist(s0, p0)), forall(s0, not(p0)))
  |- imp(forall(s0, not(p0)), not(exist(s0, p0)))
  |- imp(not(exist(s0, p0)), forall(s0, not(p0)))
} = {
  iff.comi(not(exist(s0,p0)), forall(s0,not(p0)))
  iff.lefti(forall(s0,not(p0)), not(exist(s0,p0)))
  iff.righti(not(exist(s0,p0)), forall(s0,not(p0)))
  mp(iff(forall(s0,not(p0)),not(exist(s0,p0))), iff(exist(s0,p0),not(forall(s0,not(p0)))))
  iff.con(exist(s0,p0), forall(s0,not(p0)))
  exist.def.1(s0, p0)
}
```

```follow
thm exist.def.4(set s0, prop p0) {
  |- iff(forall(s0, p0), not(exist(s0, not(p0))))
  |- iff(not(exist(s0, not(p0))), forall(s0, p0))
  |- imp(forall(s0, p0), not(exist(s0, not(p0))))
  |- imp(not(exist(s0, not(p0))), forall(s0, p0))
} = {
  iff.comi(not(exist(s0,not(p0))), forall(s0,p0))
  iff.lefti(forall(s0,p0), not(exist(s0,not(p0))))
  iff.righti(not(exist(s0,not(p0))), forall(s0,p0))
  iff.syl(forall(s0,p0), not(exist(s0,not(p0))), forall(s0,not(not(p0))))
  exist.def.3(s0, not(p0))
  a4igen.aaa.iff(s0, p0, not(not(p0)))
  iff.notnot(p0)
}
```

```follow
thm exist.def.5(set s0, prop p0) {
  |- iff(not(forall(s0, p0)), exist(s0, not(p0)))
  |- iff(exist(s0, not(p0)), not(forall(s0, p0)))
  |- imp(not(forall(s0, p0)), exist(s0, not(p0)))
  |- imp(exist(s0, not(p0)), not(forall(s0, p0)))
} = {
  iff.comi(exist(s0,not(p0)), not(forall(s0,p0)))
  iff.lefti(not(forall(s0,p0)), exist(s0,not(p0)))
  iff.righti(exist(s0,not(p0)), not(forall(s0,p0)))
  iff.syl(not(forall(s0,p0)), exist(s0,not(p0)), not(forall(s0,not(not(p0)))))
  exist.def.2(s0, not(p0))
  iff.coni.4(forall(s0,p0), forall(s0,not(not(p0))))
  a4igen.aaa.iff(s0, not(not(p0)), p0)
  iff.notnot(p0)
}
```

## `a4` 的 `exist` 版本

```follow
thm a4.aee(set s0, prop p1, prop p2) {
  |- imp(forall(s0, imp(p1, p2)), imp(exist(s0, p1), exist(s0, p2)))
} = {
  rw23(forall(s0,imp(p1,p2)), exist(s0,p1), exist(s0,p2), not(forall(s0,not(p1))), not(forall(s0,not(p2)))) 
  exist.def.2(s0, p2)
  exist.def.2(s0, p1)
  conid.4(forall(s0,imp(p1,p2)), forall(s0,not(p1)), forall(s0,not(p2)))
  syl(forall(s0,imp(p1,p2)), imp(forall(s0,not(p2)),forall(s0,not(p1))), forall(s0,imp(not(p2),not(p1))))
  a4.aaa(s0, not(p2), not(p1))
  a4igen.aaa(s0, imp(p1,p2), imp(not(p2),not(p1)))
  con.4(p1, p2)
}
```

```follow
thm a4i.aee(set s0, prop p1, prop p2) {
  |- imp(exist(s0, p1), exist(s0, p2))
  -| forall(s0, imp(p1, p2)), 
} = {
  mp(imp(exist(s0,p1),exist(s0,p2)), forall(s0,imp(p1,p2)))
  a4.aee(s0, p1, p2)
}
```

```follow
thm a4id.aee(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, imp(exist(s0, p1), exist(s0, p2)))
  -| imp(p3, forall(s0, imp(p1, p2)))
} = {
  syl(p3, imp(exist(s0,p1),exist(s0,p2)), forall(s0,imp(p1,p2)))
  a4.aee(s0, p1, p2)
}
```

```follow
thm a4igen.aee(set s0, prop p1, prop p2) {
  |- imp(exist(s0, p1), exist(s0, p2))
  -| imp(p1, p2)
} = {
  a4i.aee(s0, p1, p2)
  gen.forall(s0, imp(p1,p2))
}
```

```follow
thm a4.aee.iff(set s0, prop p1, prop p2) {
  |- imp(forall(s0, iff(p1, p2)), iff(exist(s0, p1), exist(s0, p2)))
} = {
  iff.rw23(forall(s0,iff(p1,p2)), exist(s0,p1), exist(s0,p2), not(forall(s0,not(p1))), not(forall(s0,not(p2))))
  exist.def.1(s0, p1)
  exist.def.1(s0, p2)
  iff.conid.4(forall(s0,iff(p1,p2)), forall(s0,not(p1)), forall(s0,not(p2)))
  syl(forall(s0,iff(p1,p2)), iff(forall(s0,not(p2)),forall(s0,not(p1))), forall(s0,iff(not(p2),not(p1))))
  a4.aaa.iff(s0, not(p2), not(p1))
  a4igen.aaa(s0, iff(p1,p2), iff(not(p2),not(p1)))
  iff.con(p1, p2)
}
```

```follow
thm a4i.aee.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, p1), exist(s0, p2))
  -| forall(s0, iff(p1, p2)) 
} = {
  mp(iff(exist(s0,p1),exist(s0,p2)), forall(s0,iff(p1,p2)))
  a4.aee.iff(s0, p1, p2)
}
```

```follow
thm a4id.aee.iff(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, iff(exist(s0, p1), exist(s0, p2)))
  -| imp(p3, forall(s0, iff(p1, p2)))
} = {
  syl(p3, iff(exist(s0,p1),exist(s0,p2)), forall(s0,iff(p1,p2)))
  a4.aee.iff(s0, p1, p2)
}
```

```follow
thm a4igen.aee.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, p1), exist(s0, p2))
  -| iff(p1, p2)
} = {
  a4i.aee.iff(s0, p1, p2)
  gen.forall(s0, iff(p1,p2))
}
```

## 泛化公理 `gen` 与 `exist 

```follow
thm gen.notexist(set s0, prop p1) {
  |- not(exist(s0, p1))
  -| not(p1)
} = {
  mp(not(exist(s0,p1)), forall(s0,not(p1)))
  gen.forall(s0, not(p1))
  exist.def.3(s0, p1)
}
```

```follow
thm gen.exist(set s0, prop p1) {
  |- exist(s0, p1)
  -| p1
} = {
  mp(exist(s0,p1), exist(s0,eq(c(s0),c(hs0))))
  a6.exist(s0, hs0)
  a4i.aee(s0, eq(c(s0),c(hs0)), p1)
  gen.forall(s0, imp(eq(c(s0),c(hs0)),p1))
  a1i(eq(c(s0),c(hs0)), p1)
}
```

## `a4.aea`

```follow
thm a4.aea(set s0, prop p1, prop p2) {
  |- imp(imp(exist(s0, p1), forall(s0, p2)), forall(s0, imp(p1, p2)))
} = {
  joinii(exist(s0,p1), forall(s0,p2), forall(s0,imp(p1,p2)))
  a4igen.aaa(s0, p2, imp(p1,p2))
  a1(p2, p1)
  syl(not(exist(s0,p1)), forall(s0,imp(p1,p2)), forall(s0,not(p1)))
  exist.def.3(s0, p1)
  a4igen.aaa(s0, not(p1), imp(p1,p2))
  cont.1(p1, p2)
}
```

## `a4.eae`

```follow
thm a4.eae.1(set s0, prop p1, prop p2) {
  |- imp(exist(s0, imp(p1, p2)), imp(forall(s0, p1), exist(s0, p2)))
} = {
  com12i(exist(s0,imp(p1,p2)), forall(s0,p1), exist(s0,p2))
  syl(forall(s0,p1), imp(exist(s0,imp(p1,p2)),exist(s0,p2)), forall(s0,imp(imp(p1,p2),p2)))
  a4.aee(s0, imp(p1,p2), p2)
  a4igen.aaa(s0, p1, imp(imp(p1,p2),p2))
  iidd(p1, p2)
}
```

```follow
thm a4i.eae.1(set s0, prop p1, prop p2) {
  |- imp(forall(s0, p1), exist(s0, p2))
  -| exist(s0, imp(p1, p2)) 
} = {
  mp(imp(forall(s0,p1),exist(s0,p2)), exist(s0,imp(p1,p2)))
  a4.eae.1(s0, p1, p2)
}
```

```follow
thm a4d.eae.1(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, imp(forall(s0, p1), exist(s0, p2)))
  -| imp(p3, exist(s0, imp(p1, p2)))
} = {
  syl(p3, imp(forall(s0,p1),exist(s0,p2)), exist(s0,imp(p1,p2)))
  a4.eae.1(s0, p1, p2)
}
```

```follow
thm a4igen.eae.1(set s0, prop p1, prop p2) {
  |- imp(forall(s0, p1), exist(s0, p2))
  -| imp(p1, p2)
} = {
  a4i.eae.1(s0, p1, p2)
  gen.exist(s0, imp(p1,p2))
}
```

```follow
thm a4.eae.2(set s0, prop p1, prop p2) {
  |- imp(imp(forall(s0, p1), exist(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  joinii(forall(s0,p1), exist(s0,p2), exist(s0,imp(p1,p2)))
  syl(not(forall(s0,p1)), exist(s0,imp(p1,p2)), not(forall(s0,not(imp(p1,p2)))))
  exist.def.2(s0, imp(p1,p2))
  coni.4(forall(s0,p1), forall(s0,not(imp(p1,p2))))
  a4igen.aaa(s0, not(imp(p1,p2)), p1)
  coni.1(imp(p1,p2), p1)
  cont.1(p1, p2)
  a4igen.aee(s0, p2, imp(p1,p2))
  a1(p2, p1)
}
```

```follow
thm a4.eae.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, imp(p1, p2)), imp(forall(s0, p1), exist(s0, p2)))
  |- iff(imp(forall(s0, p1), exist(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  iff.introii.1(exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  iff.introii.1(imp(forall(s0,p1),exist(s0,p2)), exist(s0,imp(p1,p2)))
  a4.eae.1(s0, p1, p2)
  a4.eae.2(s0, p1, p2)
}
```

## `a4` 与 `and` 

```follow
thm a4.and.1(set s0, prop p1, prop p2) {
  |- imp(forall(s0, and(p1, p2)), and(forall(s0, p1), forall(s0, p2)))
} = {
  and.introiid(forall(s0,and(p1,p2)), forall(s0,p1), forall(s0,p2))
  a4igen.aaa(s0, and(p1,p2), p1)
  a4igen.aaa(s0, and(p1,p2), p2)
  and.left(p1, p2)
  and.right(p1, p2)
}
```

```follow
thm a4i.and.1(set s0, prop p1, prop p2) {
  |- and(forall(s0, p1), forall(s0, p2))
  -| forall(s0, and(p1, p2)) 
} = {
  mp(and(forall(s0,p1),forall(s0,p2)), forall(s0,and(p1,p2)))
  a4.and.1(s0, p1, p2)
}
```

```follow
thm a4igen.and.1(set s0, prop p1, prop p2) {
  |- and(forall(s0, p1), forall(s0, p2))
  -| and(p1, p2)
} = {
  a4i.and.1(s0, p1, p2)
  gen.forall(s0, and(p1,p2))
}
```

```follow
thm a4id.and.1(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, and(forall(s0, p1), forall(s0, p2)))
  -| imp(p3, forall(s0, and(p1, p2)))
} = {
  syl(p3, and(forall(s0,p1),forall(s0,p2)), forall(s0,and(p1,p2)))
  a4.and.1(s0, p1, p2)
}
```

```follow
thm a4.and.2(set s0, prop p1, prop p2) {
  |- imp(exist(s0, and(p1, p2)), and(exist(s0, p1), exist(s0, p2)))
} = {
  and.introiid(exist(s0,and(p1,p2)), exist(s0,p1), exist(s0,p2))
  a4igen.aee(s0, and(p1,p2), p1)
  a4igen.aee(s0, and(p1,p2), p2)
  and.left(p1, p2)
  and.right(p1, p2)
}
```

```follow
thm a4i.and.2(set s0, prop p1, prop p2) {
  |- and(exist(s0, p1), exist(s0, p2))
  -| exist(s0, and(p1, p2)) 
} = {
  mp(and(exist(s0,p1),exist(s0,p2)), exist(s0,and(p1,p2)))
  a4.and.2(s0, p1, p2)
}
```

```follow
thm a4igen.and.2(set s0, prop p1, prop p2) {
  |- and(exist(s0, p1), exist(s0, p2))
  -| and(p1, p2)
} = {
  a4i.and.2(s0, p1, p2)
  gen.exist(s0, and(p1,p2))
}

```follow
thm a4id.and.2(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, and(exist(s0, p1), exist(s0, p2)))
  -| imp(p3, exist(s0, and(p1, p2)))
} = {
  syl(p3, and(exist(s0,p1),exist(s0,p2)), exist(s0,and(p1,p2)))
  a4.and.2(s0, p1, p2)
}
```

## `a4` 与 `or` 

```follow
thm a4.or.1(set s0, prop p1, prop p2) {
  |- imp(exist(s0, or(p1, p2)), or(exist(s0, p1), exist(s0, p2)))
} = {
  syl(exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)), imp(not(exist(s0,p1)),exist(s0,p2)))
  or.def.1(exist(s0,p1), exist(s0,p2))
  rw2(exist(s0,or(p1,p2)), not(exist(s0,p1)), exist(s0,p2), forall(s0,not(p1)))
  exist.def.3(s0, p1)
  syl(exist(s0,or(p1,p2)), imp(forall(s0,not(p1)),exist(s0,p2)), exist(s0,imp(not(p1),p2)))
  a4.eae.1(s0, not(p1), p2)
  a4igen.aee(s0, or(p1,p2), imp(not(p1),p2))
  or.def.1(p1, p2)
}
```

```follow
thm a4i.or.1(set s0, prop p1, prop p2) {
  |- or(exist(s0, p1), exist(s0, p2))
  -| exist(s0, or(p1, p2)) 
} = {
  mp(or(exist(s0,p1),exist(s0,p2)), exist(s0,or(p1,p2)))
  a4.or.1(s0, p1, p2)
}
```

```follow
thm a4igen.or.1(set s0, prop p1, prop p2) {
  |- or(exist(s0, p1), exist(s0, p2))
  -| or(p1, p2)
} = {
  a4i.or.1(s0, p1, p2)
  gen.exist(s0, or(p1,p2))
}
```

```follow
thm a4id.or.1(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, or(exist(s0, p1), exist(s0, p2)))
  -| imp(p3, exist(s0, or(p1, p2)))
} = {
  syl(p3, or(exist(s0,p1),exist(s0,p2)), exist(s0,or(p1,p2)))
  a4.or.1(s0, p1, p2)
}
```

```follow
thm a4.or.2(set s0, prop p1, prop p2) {
  |- imp(or(exist(s0, p1), exist(s0, p2)), exist(s0, or(p1, p2)))
} = {
  or.casesii(exist(s0,p1), exist(s0,p2), exist(s0,or(p1,p2)))
  a4igen.aee(s0, p1, or(p1,p2))
  a4igen.aee(s0, p2, or(p1,p2))
  or.left(p1, p2)
  or.right(p2, p1)
}
```

```follow
thm a4i.or.2(set s0, prop p1, prop p2) {
  |- exist(s0, or(p1, p2))
  -| or(exist(s0, p1), exist(s0, p2))
} = {
  mp(exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.2(s0, p1, p2)
}
```

```follow
thm a4id.or.2(set s0, prop p1, prop p2, prop p3) {
  |- imp(p3, exist(s0, or(p1, p2)))
  -| imp(p3, or(exist(s0, p1), exist(s0, p2)))
} = {
  syl(p3, exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.2(s0, p1, p2)
}
```

```follow
thm a4.or.iff(set s0, prop p1, prop p2) {
  |- iff(or(exist(s0, p1), exist(s0, p2)), exist(s0, or(p1, p2)))
  |- iff(exist(s0, or(p1, p2)), or(exist(s0, p1), exist(s0, p2)))
} = {
  iff.introii.1(or(exist(s0,p1),exist(s0,p2)), exist(s0,or(p1,p2)))
  iff.introii.1(exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.1(s0, p1, p2)
  a4.or.2(s0, p1, p2)
}
```
##  The Deduction of `gen`  

```follow
thm forall.2exist(set s0, prop p0) {
  |- imp(forall(s0, p0), exist(s0, p0))
} = {
  a4igen.eae.1(s0, p0, p0)
  id(p0)
}
```

```follow
thm gend(set s0, prop p0, prop p1) {
  |- imp(p1, forall(s0, p0))
  |- imp(p1, exist(s0, p0))
  -| imp(p1, p0)
  diff (p1, s0)
} = {
  syl(p1, exist(s0,p0), forall(s0,p0))
  forall.2exist(s0, p0)
  syl(p1, forall(s0,p0), forall(s0,p1))
  a5.forall.1(s0, p1)
  a4igen.aaa(s0, p1, p0)
}
```

## 公理 `a5` 相关的定理 

```follow
thm a5.exist.1(set s0, prop p1) {
  |- imp(exist(s0, p1), p1)
  diff (s0, p1)
} = {
  syl(exist(s0,p1), p1, not(forall(s0,not(p1))))
  exist.def.2(s0, p1)
  coni.1(forall(s0,not(p1)), p1)
  a5.forall.1(s0, not(p1))
}
```

```follow
thm a5.exist.2(set s0, prop p0) {
  |- imp(p0, exist(s0, p0))
  diff (s0, p0)
} = {
  syl(p0, exist(s0,p0), forall(s0,p0))
  forall.2exist(s0, p0)
  a5.forall.1(s0, p0)
}
```

```follow
thm a5.exist.iff(set s0, prop p0) {
  |- iff(p0, exist(s0, p0))
  |- iff(exist(s0, p0), p0)
  diff (s0, p0)
} = {
  iff.introii.1(p0, exist(s0,p0))
  iff.introii.1(exist(s0,p0), p0)
  a5.exist.1(s0, p0)
  a5.exist.2(s0, p0)
}
```

```follow
thm a5.exist.forall(set s0, prop p1) {
  |- iff(exist(s0, p1), forall(s0, p1))
  |- iff(forall(s0, p1), exist(s0, p1))
  |- imp(exist(s0, p1), forall(s0, p1))
  |- imp(forall(s0, p1), exist(s0, p1))
  diff (s0, p1)
} = {
  iff.introii.1(exist(s0,p1), forall(s0,p1))
  iff.introii.1(forall(s0,p1), exist(s0,p1))
  forall.2exist(s0, p1)
  syl(exist(s0,p1), forall(s0,p1), p1)
  a5.exist.1(s0, p1)
  a5.forall.1(s0, p1)
}
```

```follow
thm a5.forall.2(set s0, prop p1) {
  |- imp(forall(s0, p1), p1)
  diff (s0, p1)
} = {
  syl(forall(s0,p1), p1, exist(s0,p1))
  a5.exist.forall(s0, p1)
  a5.exist.1(s0, p1)
}
```

```follow
thm a5.forall.iff(set s0, prop p1) {
  |- iff(forall(s0, p1), p1)
  |- iff(p1, forall(s0, p1))
  diff (s0, p1)
} = {
  iff.introii.1(forall(s0,p1), p1)
  iff.introii.1(p1, forall(s0,p1))
  a5.forall.1(s0, p1)
  a5.forall.2(s0, p1)
}
```

```follow
thm a5.rw(set s0, prop p0, prop p1) {
  |- imp(p0, forall(s0, p0))
  -| imp(p1, forall(s0, p1))
  -| iff(p0, p1)
} = {
  syl(p0, forall(s0,p0), p1)
  syl(p1, forall(s0,p0), forall(s0,p1))
  a4igen.aaa(s0, p1, p0)
  iff.righti(p1, p0)
  iff.lefti(p0, p1)
}
```

## `a5` 汇总 

| 名称 | 目标命题 ｜
| :---: | :---: |
| `a5.forall.1` | `(p1→∀(s0,p1))` | 
| `a5.exist.1` | `(∃(s0,p1)→p1)` | 
| `a5.exist.2` | `(p0→∃(s0,p0))` |
| `a5.exist.iff` | `(p0↔∃(s0,p0))` | 
| `a5.exist.forall` | `(∃(s0,p1)↔∀(s0,p1))` | 
| `a5.forall.2` | `(∀(s0,p1)→p1)` | 
| `a5.forall.iff` | `(p1↔∀(s0,p1))` | 

## `a4.eaa`

```follow
thm a4.eaa(set s0, prop p1, prop p2) {
  |- imp(imp(forall(s0, p1), forall(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  syl(imp(forall(s0,p1),forall(s0,p2)), exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  a4.eae.2(s0, p1, p2)
  mp(imp(imp(forall(s0,p1),forall(s0,p2)),imp(forall(s0,p1),exist(s0,p2))), imp(forall(s0,p2),exist(s0,p2)))
  trans.1(forall(s0,p1), forall(s0,p2), exist(s0,p2))
  forall.2exist(s0, p2)
}
```

## `a4.eee`

```follow
thm a4.eee(set s0, prop p1, prop p2) {
  |- imp(imp(exist(s0, p1), exist(s0, p2)), exist(s0, imp(p1, p2)))
} = {
  syl(imp(exist(s0,p1),exist(s0,p2)), exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  a4.eae.2(s0, p1, p2)
  mp(imp(imp(exist(s0,p1),exist(s0,p2)),imp(forall(s0,p1),exist(s0,p2))), imp(forall(s0,p1),exist(s0,p1)))
  trans.2(forall(s0,p1), exist(s0,p1), exist(s0,p2))
  forall.2exist(s0, p1)
}
```

## `a4.or` (续)

```follow
thm a4.or.3(set s0, prop p1, prop p2) {
  |- imp(or(forall(s0, p1), exist(s0, p2)), exist(s0, or(p1, p2)))
} = {
  syl(or(forall(s0,p1),exist(s0,p2)), exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.2(s0, p1, p2)
  or.2orii(forall(s0,p1), exist(s0,p2), exist(s0,p1), exist(s0,p2))
  id(exist(s0,p2))
  forall.2exist(s0, p1)
}
```

```follow
thm a4.or.4(set s0, prop p1, prop p2) {
  |- imp(or(exist(s0, p1), forall(s0, p2)), exist(s0, or(p1, p2)))
} = {
  syl(or(exist(s0,p1),forall(s0,p2)), exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.2(s0, p1, p2)
  or.2orii(exist(s0,p1), forall(s0,p2), exist(s0,p1), exist(s0,p2))
  id(exist(s0,p1))
  forall.2exist(s0, p2)
}
```

```follow
thm a4.or.5(set s0, prop p1, prop p2) {
  |- imp(or(forall(s0, p1), forall(s0, p2)), exist(s0, or(p1, p2)))
} = {
  syl(or(forall(s0,p1),forall(s0,p2)), exist(s0,or(p1,p2)), or(exist(s0,p1),forall(s0,p2)))
  a4.or.4(s0, p1, p2)
  or.2orii(forall(s0,p1), forall(s0,p2), exist(s0,p1), forall(s0,p2))
  id(forall(s0,p2))
  forall.2exist(s0, p1)
}
```

## `a4` 汇总

| 名称 | 目标命题 ｜
| :---: | :---: |
| `a4.1` | `(∀(s0,(p1→p2))→(∀(s0,p1)→∀(s0,p2)))` | 
| `a4.iff` | `(∀(s0,(p1↔p2))→(∀(s0,p1)↔∀(s0,p2)))` | 
| `a4.aee` | `(∀(s0,(p1→p2))→(∃(s0,p1)→∃(s0,p2)))` |
| `a4.aee.iff` | `(∀(s0,(p1↔p2))→(∃(s0,p1)↔∃(s0,p2)))` |
| `a4.aea` | `((∃(s0,p1)→∀(s0,p2))→∀(s0,(p1→p2)))` |
| `a4.eae.1` | `(∃(s0,(p1→p2))→(∀(s0,p1)→∃(s0,p2)))` |
| `a4.eae.2` | `((∀(s0,p1)→∃(s0,p2))→∃(s0,(p1→p2)))` |
| `a4.eae.iff` | `(∃(s0,(p1→p2))↔(∀(s0,p1)→∃(s0,p2)))` | 
| `a4.eaa` | `((∀(s0,p1)→∀(s0,p2))→∃(s0,(p1→p2)))` |
| `a4.eee` | `((∃(s0,p1)→∃(s0,p2))→∃(s0,(p1→p2)))` |
| `a4.and.1` | `(∀(s0,(p1∧p2))→(∀(s0,p1)∧∀(s0,p2)))` |
| `a4.and.2` | `(∃(s0,(p1∧p2))→(∃(s0,p1)∧∃(s0,p2)))` | 
| `a4.or.1` | `(∃(s0,(p1∨p2))→(∃(s0,p1)∨∃(s0,p2)))` |
| `a4.or.2` | `((∃(s0,p1)∨∃(s0,p2))→∃(s0,(p1∨p2)))` |
| `a4.or.iff` | `(∃(s0,(p1∨p2))↔(∃(s0,p1)∨∃(s0,p2)))` | 
| `a4.or.3` | `((∀(s0,p1)∨∃(s0,p2))→∃(s0,(p1∨p2)))` |
| `a4.or.4` | `((∃(s0,p1)∨∀(s0,p2))→∃(s0,(p1∨p2)))` |
| `a4.or.5` | `((∀(s0,p1)∨∀(s0,p2))→∃(s0,(p1∨p2)))` |

## `a4` + `a5` 

### `a4.diff.aaa`

```follow
thm a4.diff.aaa(set s0, prop p1, prop p2) {
  |- imp(forall(s0, imp(p1, p2)), imp(forall(s0, p1), p2))
  |- imp(forall(s0, imp(p2, p1)), imp(p2, forall(s0, p1)))
  diff (s0, p2)
} = {
  rw3(forall(s0,imp(p1,p2)), forall(s0,p1), p2, forall(s0,p2))
  a4.aaa(s0, p1, p2)
  a5.forall.2(s0, p2)
  rw2(forall(s0,imp(p2,p1)), p2, forall(s0,p1), forall(s0,p2))
  a4.aaa(s0, p2, p1)
  a5.forall.1(s0, p2)
}
```

```follow
thm a4.diff.aaa.iff(set s0, prop p1, prop p2) {
  |- imp(forall(s0, iff(p1, p2)), iff(forall(s0, p1), p2))
  |- imp(forall(s0, iff(p2, p1)), iff(p2, forall(s0, p1)))
  diff (s0, p2)
} = {
  iff.rw3(forall(s0,iff(p1,p2)), forall(s0,p1), p2, forall(s0,p2))
  a5.forall.iff(s0, p2)
  iff.rw2(forall(s0,iff(p2,p1)), p2, forall(s0,p1), forall(s0,p2))
  a5.forall.iff(s0, p2)
  a4.aaa.iff(s0, p2, p1)
  a4.aaa.iff(s0, p1, p2)
}
```

### `a4.diff.aee`

```follow
thm a4.diff.aee(set s0, prop p1, prop p2) {
  |- imp(forall(s0, imp(p1, p2)), imp(exist(s0, p1), p2))
  |- imp(forall(s0, imp(p2, p1)), imp(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  rw3(forall(s0,imp(p1,p2)), exist(s0,p1), p2, exist(s0,p2))
  a4.aee(s0, p1, p2)
  a5.exist.1(s0, p2)
  rw2(forall(s0,imp(p2,p1)), p2, exist(s0,p1), exist(s0,p2))
  a4.aee(s0, p2, p1)
  a5.exist.2(s0, p2)
}
```

```follow
thm a4.diff.aee.iff(set s0, prop p1, prop p2) {
  |- imp(forall(s0, iff(p1, p2)), iff(exist(s0, p1), p2))
  |- imp(forall(s0, iff(p2, p1)), iff(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  iff.rw3(forall(s0,iff(p1,p2)), exist(s0,p1), p2, exist(s0,p2))
  a4.aee.iff(s0, p1, p2)
  a5.exist.iff(s0, p2)
  iff.rw2(forall(s0,iff(p2,p1)), p2, exist(s0,p1), exist(s0,p2))
  a4.aee.iff(s0, p2, p1)
  a5.exist.iff(s0, p2)
}
```

### `a4.diff.aea` 

```follow
thm a4.diff.aea.1(set s0, prop p1, prop p2) {
  |- imp(imp(exist(s0, p1), p2), forall(s0, imp(p1, p2)))
  diff (s0, p2)
} = {
  syl(imp(exist(s0,p1),p2), forall(s0,imp(p1,p2)), imp(exist(s0,p1),forall(s0,p2)))
  a4.aea(s0, p1, p2)
  mp(imp(imp(exist(s0,p1),p2),imp(exist(s0,p1),forall(s0,p2))), imp(p2,forall(s0,p2)))
  trans.1(exist(s0,p1), p2, forall(s0,p2))
  a5.forall.1(s0, p2)
}
```

```follow
thm a4.diff.aea.2(set s0, prop p1, prop p2) {
  |- imp(imp(p2, forall(s0, p1)), forall(s0, imp(p2, p1)))
  diff (s0, p2)
} = {
  syl(imp(p2,forall(s0,p1)), forall(s0,imp(p2,p1)), imp(exist(s0,p2),forall(s0,p1)))
  a4.aea(s0, p2, p1)
  mp(imp(imp(p2,forall(s0,p1)),imp(exist(s0,p2),forall(s0,p1))), imp(exist(s0,p2),p2))
  trans.2(exist(s0,p2), p2, forall(s0,p1))
  a5.exist.1(s0, p2)
}
```

```follow
thm a4.diff.aea.iff(set s0, prop p1, prop p2) {
  |- iff(imp(exist(s0, p1), p2), forall(s0, imp(p1, p2)))
  |- iff(forall(s0, imp(p1, p2)), imp(exist(s0, p1), p2))
  |- iff(imp(p2, forall(s0, p1)), forall(s0, imp(p2, p1)))
  |- iff(forall(s0, imp(p2, p1)), imp(p2, forall(s0, p1)))
  diff (s0, p2)
} = {
  iff.introii.1(imp(exist(s0,p1),p2), forall(s0,imp(p1,p2)))
  iff.introii.1(forall(s0,imp(p1,p2)), imp(exist(s0,p1),p2))
  iff.introii.1(imp(p2,forall(s0,p1)), forall(s0,imp(p2,p1)))
  iff.introii.1(forall(s0,imp(p2,p1)), imp(p2,forall(s0,p1)))
  a4.diff.aaa(s0, p1, p2)
  a4.diff.aee(s0, p1, p2)
  a4.diff.aea.1(s0, p1, p2)
  a4.diff.aea.2(s0, p1, p2)
}
```

### `a4.diff.eae` 

```follow
thm a4.diff.eae.1(set s0, prop p1, prop p2) {
  |- imp(exist(s0, imp(p1, p2)), imp(forall(s0, p1), p2))
  |- imp(exist(s0, imp(p2, p1)), imp(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  rw3(exist(s0,imp(p1,p2)), forall(s0,p1), p2, exist(s0,p2))
  a4.eae.1(s0, p1, p2)
  a5.exist.1(s0, p2)
  rw2(exist(s0,imp(p2,p1)), p2, exist(s0,p1), forall(s0,p2))
  a4.eae.1(s0, p2, p1)
  a5.forall.1(s0, p2)
}
```

```follow
thm a4.diff.eae.2(set s0, prop p1, prop p2) {
  |- imp(imp(forall(s0, p1), p2), exist(s0, imp(p1, p2)))
  |- imp(imp(p2, exist(s0, p1)), exist(s0, imp(p2, p1)))
  diff (s0, p2)
} = {
  syl(imp(forall(s0,p1),p2), exist(s0,imp(p1,p2)), imp(forall(s0,p1),exist(s0,p2)))
  a4.eae.2(s0, p1, p2)
  mp(imp(imp(forall(s0,p1),p2),imp(forall(s0,p1),exist(s0,p2))), imp(p2,exist(s0,p2)))
  trans.1(forall(s0,p1), p2, exist(s0,p2))
  a5.exist.2(s0, p2)
  syl(imp(p2,exist(s0,p1)), exist(s0,imp(p2,p1)), imp(forall(s0,p2),exist(s0,p1)))
  a4.eae.2(s0, p2, p1)
  mp(imp(imp(p2,exist(s0,p1)),imp(forall(s0,p2),exist(s0,p1))), imp(forall(s0,p2),p2))
  trans.2(forall(s0,p2), p2, exist(s0,p1))
  a5.forall.2(s0, p2)
}
```

```follow
thm a4.diff.eae.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, imp(p1, p2)), imp(forall(s0, p1), p2))
  |- iff(imp(forall(s0, p1), p2), exist(s0, imp(p1, p2)))
  |- iff(exist(s0, imp(p2, p1)), imp(p2, exist(s0, p1)))
  |- iff(imp(p2, exist(s0, p1)), exist(s0, imp(p2, p1)))
  diff (s0, p2)
} = {
  iff.introii.1(exist(s0,imp(p1,p2)), imp(forall(s0,p1),p2))
  iff.introii.1(imp(forall(s0,p1),p2), exist(s0,imp(p1,p2)))
  iff.introii.1(exist(s0,imp(p2,p1)), imp(p2,exist(s0,p1)))
  iff.introii.1(imp(p2,exist(s0,p1)), exist(s0,imp(p2,p1)))
  a4.diff.eae.1(s0, p1, p2)
  a4.diff.eae.2(s0, p1, p2)
}
```

### `a4.diff.eaa` 

```follow
thm a4.diff.eaa(set s0, prop p1, prop p2) {
  |- imp(imp(p2, forall(s0,p1)), exist(s0, imp(p2, p1)))
  diff (s0, p2)
} = {
  syl(imp(p2,forall(s0,p1)), exist(s0,imp(p2,p1)), imp(forall(s0,p2),forall(s0,p1)))
  a4.eaa(s0, p2, p1)
  mp(imp(imp(p2,forall(s0,p1)),imp(forall(s0,p2),forall(s0,p1))), imp(forall(s0,p2),p2))
  trans.2(forall(s0,p2), p2, forall(s0,p1))
  a5.forall.2(s0, p2)
}
```

### `a4.diff.eee` 

```follow
thm a4.diff.eee(set s0, prop p1, prop p2) {
  |- imp(imp(exist(s0,p1), p2), exist(s0, imp(p1, p2)))
  diff (s0, p2)
} = {
  syl(imp(exist(s0,p1),p2), exist(s0,imp(p1,p2)), imp(exist(s0,p1),exist(s0,p2)))
  a4.eee(s0, p1, p2)
  mp(imp(imp(exist(s0,p1),p2),imp(exist(s0,p1),exist(s0,p2))), imp(p2,exist(s0,p2)))
  trans.1(exist(s0,p1), p2, exist(s0,p2))
  a5.exist.2(s0, p2)
}
```

### `a4.diff.and`

```follow
thm a4.diff.and.1(set s0, prop p1, prop p2) {
  |- imp(forall(s0, and(p1, p2)), and(forall(s0, p1), p2))
  |- imp(forall(s0, and(p2, p1)), and(p2, forall(s0, p1)))
  diff (s0, p2)
} = {
  and.rw3(forall(s0,and(p1,p2)), forall(s0,p1), p2, forall(s0,p2))
  a4.and.1(s0, p1, p2)
  a5.forall.2(s0, p2)
  and.rw2(forall(s0,and(p2,p1)), p2, forall(s0,p1), forall(s0,p2))
  a4.and.1(s0, p2, p1)
  a5.forall.2(s0, p2)
}
```

```follow
thm a4.diff.and.2(set s0, prop p1, prop p2) {
  |- imp(exist(s0, and(p1, p2)), and(exist(s0, p1), p2))
  |- imp(exist(s0, and(p2, p1)), and(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  and.rw3(exist(s0,and(p1,p2)), exist(s0,p1), p2, exist(s0,p2))
  a4.and.2(s0, p1, p2)
  a5.exist.1(s0, p2)
  and.rw2(exist(s0,and(p2,p1)), p2, exist(s0,p1), exist(s0,p2))
  a4.and.2(s0, p2, p1)
  a5.exist.1(s0, p2)
}
```

```follow
thm a4.diff.and.3(set s0, prop p1, prop p2) {
  |- imp(and(exist(s0, p1), p2), exist(s0, and(p1, p2)))
  |- imp(and(p2, exist(s0, p1)), exist(s0, and(p2, p1)))
  diff (s0, p2)
} = {
  and.joini(exist(s0,p1), p2, exist(s0,and(p1,p2)))
  com12i(exist(s0,p1), p2, exist(s0,and(p1,p2)))
  a4id.aee(s0, p1, and(p1,p2), p2)
  gend(s0, imp(p1,and(p1,p2)), p2)
  and.intro(p1, p2)
  and.joini(p2, exist(s0,p1), exist(s0,and(p2,p1)))
  a4id.aee(s0, p1, and(p2,p1), p2)
  gend(s0, imp(p1,and(p2,p1)), p2)
  and.intro(p2, p1)
}
```

```follow
thm a4.diff.and.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, and(p1, p2)), and(exist(s0, p1), p2))
  |- iff(and(exist(s0, p1), p2), exist(s0, and(p1, p2)))
  |- iff(and(p2, exist(s0, p1)), exist(s0, and(p2, p1)))
  |- iff(exist(s0, and(p2, p1)), and(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  iff.introii.1(exist(s0,and(p1,p2)), and(exist(s0,p1),p2))
  iff.introii.1(and(exist(s0,p1),p2), exist(s0,and(p1,p2)))
  iff.introii.1(and(p2,exist(s0,p1)), exist(s0,and(p2,p1)))
  iff.introii.1(exist(s0,and(p2,p1)), and(p2,exist(s0,p1)))
  a4.diff.and.2(s0, p1, p2)
  a4.diff.and.3(s0, p1, p2)
}
```

### `a4.diff.or` 

```follow
thm a4.diff.or.1(set s0, prop p1, prop p2) {
  |- imp(or(exist(s0, p1), p2), exist(s0, or(p1, p2)))
  |- imp(or(p2, exist(s0, p1)), exist(s0, or(p2, p1)))
  diff (s0, p2)
} = {
  syl(or(exist(s0,p1),p2), exist(s0,or(p1,p2)), or(exist(s0,p1),exist(s0,p2)))
  a4.or.2(s0, p1, p2)
  or.2orii(exist(s0,p1), p2, exist(s0,p1), exist(s0,p2))
  id(exist(s0,p1))
  a5.exist.2(s0, p2)

  syl(or(p2,exist(s0,p1)), exist(s0,or(p2,p1)), or(exist(s0,p2),exist(s0,p1)))
  a4.or.2(s0, p2, p1)
  or.2orii(p2, exist(s0,p1), exist(s0,p2), exist(s0,p1))
  a5.exist.2(s0, p2)
  id(exist(s0,p1))
}
```

```follow
thm a4.diff.or.2(set s0, prop p1, prop p2) {
  |- imp(exist(s0, or(p1, p2)), or(exist(s0, p1), p2))
  |- imp(exist(s0, or(p2, p1)), or(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  or.rw3(exist(s0,or(p1,p2)), exist(s0,p1), p2, exist(s0,p2))
  a4.or.1(s0, p1, p2)
  a5.exist.1(s0, p2)
  or.rw2(exist(s0,or(p2,p1)), p2, exist(s0,p1), exist(s0,p2))
  a4.or.1(s0, p2, p1)
  a5.exist.1(s0, p2)
}
```

```follow
thm a4.diff.or.iff(set s0, prop p1, prop p2) {
  |- iff(exist(s0, or(p1, p2)), or(exist(s0, p1), p2))
  |- iff(or(exist(s0, p1), p2), exist(s0, or(p1, p2)))
  |- iff(or(p2, exist(s0, p1)), exist(s0, or(p2, p1)))
  |- iff(exist(s0, or(p2, p1)), or(p2, exist(s0, p1)))
  diff (s0, p2)
} = {
  iff.introii.1(exist(s0,or(p1,p2)), or(exist(s0,p1),p2))
  iff.introii.1(or(exist(s0,p1),p2), exist(s0,or(p1,p2)))
  iff.introii.1(or(p2,exist(s0,p1)), exist(s0,or(p2,p1)))
  iff.introii.1(exist(s0,or(p2,p1)), or(p2,exist(s0,p1)))
  a4.diff.or.1(s0, p1, p2)
  a4.diff.or.2(s0, p1, p2)
}
```

```follow
thm a4.diff.or.5(set s0, prop p1, prop p2) {
  |- imp(or(forall(s0, p1), p2), exist(s0, or(p1, p2)))
  |- imp(or(p2, forall(s0, p1)), exist(s0, or(p2, p1)))
  diff (s0, p2)
} = {
  syl(or(forall(s0,p1),p2), exist(s0,or(p1,p2)), or(forall(s0,p1),forall(s0,p2)))
  a4.or.5(s0, p1, p2)
  or.2orii(forall(s0,p1), p2, forall(s0,p1), forall(s0,p2))
  id(forall(s0,p1))
  a5.forall.1(s0, p2)

  syl(or(p2,forall(s0,p1)), exist(s0,or(p2,p1)), or(forall(s0,p2),forall(s0,p1)))
  a4.or.5(s0, p2, p1)
  or.2orii(p2, forall(s0,p1), forall(s0,p2), forall(s0,p1))
  id(forall(s0,p1))
  a5.forall.1(s0, p2)
}
```

### `a4.diff` 汇总 

| 名称 | 目标命题 ｜
| :---: | :---: |
| `a4.diff.aaa` | `(∀(s0,(p1→p2))→(∀(s0,p1)→p2))` <br> `(∀(s0,(p2→p1))→(p2→∀(s0,p1)))` | 
| `a4.diff.aaa.iff` | `(∀(s0,(p1↔p2))→(∀(s0,p1)↔p2))` <br> `(∀(s0,(p2↔p1))→(p2↔∀(s0,p1)))` | 
| `a4.diff.aee` | `(∀(s0,(p1→p2))→(∃(s0,p1)→p2))` <br> `(∀(s0,(p2→p1))→(p2→∃(s0,p1)))` |
| `a4.diff.aee.iff` | `(∀(s0,(p1↔p2))→(∃(s0,p1)↔p2))` <br> `(∀(s0,(p2↔p1))→(p2↔∃(s0,p1)))` |
| `a4.diff.aea.1` | `((∃(s0,p1)→p2)→∀(s0,(p1→p2)))` |
| `a4.diff.aea.2` | `((p2→∀(s0,p1))→∀(s0,(p2→p1)))` |
| `a4.diff.aea.iff` | `((∃(s0,p1)→p2)↔∀(s0,(p1→p2)))` <br> `(∀(s0,(p1→p2))↔(∃(s0,p1)→p2))` <br> `((p2→∀(s0,p1))↔∀(s0,(p2→p1)))` <br> `(∀(s0,(p2→p1))↔(p2→∀(s0,p1)))` | 
| `a4.diff.eae.1` | `(∃(s0,(p1→p2))→(∀(s0,p1)→p2))` <br> `(∃(s0,(p2→p1))→(p2→∃(s0,p1)))` | 
| `a4.diff.eae.2` | `((∀(s0,p1)→p2)→∃(s0,(p1→p2)))` <br> `((p2→∃(s0,p1))→∃(s0,(p2→p1)))` |
| `a4.diff.eae.iff` | `(∃(s0,(p1→p2))↔(∀(s0,p1)→p2))` <br> `((∀(s0,p1)→p2)↔∃(s0,(p1→p2)))` <br> `(∃(s0,(p2→p1))↔(p2→∃(s0,p1)))` <br> `((p2→∃(s0,p1))↔∃(s0,(p2→p1)))` |
| `a4.diff.and.1` | `(∀(s0,(p1∧p2))→(∀(s0,p1)∧p2))` <br> `(∀(s0,(p2∧p1))→(p2∧∀(s0,p1)))` | 
| `a4.diff.and.2` | `(∃(s0,(p1∧p2))→(∃(s0,p1)∧p2))` <br> `(∃(s0,(p2∧p1))→(p2∧∃(s0,p1)))` |
| `a4.diff.and.3` | `((∃(s0,p1)∧p2)→∃(s0,(p1∧p2)))` <br> `((p2∧∃(s0,p1))→∃(s0,(p2∧p1)))` | 
| `a4.diff.and.iff` | `(∃(s0,(p1∧p2))↔(∃(s0,p1)∧p2))` <br> `((∃(s0,p1)∧p2)↔∃(s0,(p1∧p2)))` <br> `((p2∧∃(s0,p1))↔∃(s0,(p2∧p1)))` <br> `(∃(s0,(p2∧p1))↔(p2∧∃(s0,p1)))` |
| `a4.diff.or.1` | `((∃(s0,p1)∨p2)→∃(s0,(p1∨p2)))` <br> `((p2∨∃(s0,p1))→∃(s0,(p2∨p1)))` |
| `a4.diff.or.2` | `(∃(s0,(p1∨p2))→(∃(s0,p1)∨p2))` <br> `(∃(s0,(p2∨p1))→(p2∨∃(s0,p1)))` |
| `a4.diff.or.iff` | `(∃(s0,(p1∨p2))↔(∃(s0,p1)∨p2))` <br> `((∃(s0,p1)∨p2)↔∃(s0,(p1∨p2)))` <br> `((p2∨∃(s0,p1))↔∃(s0,(p2∨p1)))` <br> `(∃(s0,(p2∨p1))↔(p2∨∃(s0,p1)))` |
| `a4.diff.or.5` | `((∀(s0,p1)∨p2)→∃(s0,(p1∨p2)))` <br> `((p2∨∀(s0,p1))→∃(s0,(p2∨p1)))` |

## 其他定理 

```follow
thm forall.not.and(set s0, prop p1, prop p2) {
  |- iff(forall(s0,not(and(p1,p2))), forall(s0,imp(p1,not(p2))))
  |- iff(forall(s0,imp(p1,not(p2))), forall(s0,not(and(p1,p2))))
  |- imp(forall(s0,not(and(p1,p2))), forall(s0,imp(p1,not(p2))))
  |- imp(forall(s0,imp(p1,not(p2))), forall(s0,not(and(p1,p2))))
} = {
  iff.comi(forall(s0,imp(p1,not(p2))), forall(s0,not(and(p1,p2))))
  iff.lefti(forall(s0,not(and(p1,p2))), forall(s0,imp(p1,not(p2))))
  iff.righti(forall(s0,imp(p1,not(p2))), forall(s0,not(and(p1,p2))))
  a4igen.aaa.iff(s0, not(and(p1,p2)), imp(p1,not(p2)))
  iff.syl(not(and(p1,p2)), imp(p1,not(p2)), or(not(p1),not(p2)))
  not.and.1(p1, p2)
  iff.syl(or(not(p1),not(p2)), imp(p1,not(p2)), imp(not(not(p1)),not(p2)))
  iff.or.def(not(p1), not(p2))
  imp.iffimpii.3(not(not(p1)), not(p2), p1, not(p2))
  iff.notnot(p1)
  iff.id(not(p2))
}
```

```follow
thm not.exist.or(set s0, prop p1, prop p2) {
  |- iff(not(exist(s0,and(p1,p2))), forall(s0,imp(p1,not(p2))))
  |- iff(forall(s0,imp(p1,not(p2))), not(exist(s0,and(p1,p2))))
  |- imp(not(exist(s0,and(p1,p2))), forall(s0,imp(p1,not(p2))))
  |- imp(forall(s0,imp(p1,not(p2))), not(exist(s0,and(p1,p2))))
} = {
  iff.comi(forall(s0,imp(p1,not(p2))), not(exist(s0,and(p1,p2))))
  iff.lefti(not(exist(s0,and(p1,p2))), forall(s0,imp(p1,not(p2))))
  iff.righti(forall(s0,imp(p1,not(p2))), not(exist(s0,and(p1,p2))))
  iff.syl(not(exist(s0,and(p1,p2))), forall(s0,imp(p1,not(p2))), forall(s0,not(and(p1,p2))))
  exist.def.3(s0, and(p1,p2))
  forall.not.and(s0, p1, p2)
}
```

```follow
thm exist.exist(set s0, set s1, prop p1) {
  |- iff(exist(s0, exist(s1, p1)), not(forall(s0, forall(s1, not(p1)))))
  |- iff(not(forall(s0, forall(s1, not(p1)))), exist(s0, exist(s1, p1)))
  |- imp(exist(s0, exist(s1, p1)), not(forall(s0, forall(s1, not(p1)))))
  |- imp(not(forall(s0, forall(s1, not(p1)))), exist(s0, exist(s1, p1)))
} = {
  iff.comi(not(forall(s0,forall(s1,not(p1)))), exist(s0,exist(s1,p1)))
  iff.lefti(exist(s0,exist(s1,p1)), not(forall(s0,forall(s1,not(p1)))))
  iff.righti(not(forall(s0,forall(s1,not(p1)))), exist(s0,exist(s1,p1)))
  iff.syl(exist(s0,exist(s1,p1)), not(forall(s0,forall(s1,not(p1)))), exist(s0,not(forall(s1,not(p1)))))
  exist.def.5(s0, forall(s1,not(p1)))
  a4igen.aee.iff(s0, exist(s1,p1), not(forall(s1,not(p1))))
  exist.def.1(s1, p1)
}
```

```follow
thm forall.forall(set s0, set s1, prop p1) {
  |- iff(forall(s0, forall(s1, p1)), not(exist(s0, exist(s1, not(p1)))))
  |- iff(not(exist(s0, exist(s1, not(p1)))), forall(s0, forall(s1, p1)))
  |- imp(forall(s0, forall(s1, p1)), not(exist(s0, exist(s1, not(p1)))))
  |- imp(not(exist(s0, exist(s1, not(p1)))), forall(s0, forall(s1, p1)))
} = {
  iff.comi(not(exist(s0,exist(s1,not(p1)))), forall(s0,forall(s1,p1)))
  iff.lefti(forall(s0,forall(s1,p1)), not(exist(s0,exist(s1,not(p1)))))
  iff.righti(not(exist(s0,exist(s1,not(p1)))), forall(s0,forall(s1,p1)))
  mp(iff(forall(s0,forall(s1,p1)),not(exist(s0,exist(s1,not(p1))))), iff(exist(s0,exist(s1,not(p1))),not(forall(s0,forall(s1,p1)))))
  iff.con(exist(s0,exist(s1,not(p1))), forall(s0,forall(s1,p1)))
  iff.syl(exist(s0,exist(s1,not(p1))), not(forall(s0,forall(s1,p1))), not(forall(s0,forall(s1,not(not(p1))))))
  exist.exist(s0, s1, not(p1))
  iff.coni.4(forall(s0,forall(s1,not(not(p1)))), forall(s0,forall(s1,p1)))
  a4igen.aaa.iff(s0, forall(s1,p1), forall(s1,not(not(p1))))
  a4igen.aaa.iff(s1, p1, not(not(p1)))
  iff.notnot(p1)
}
```
