
# 包含符号 `in` 


```follow
term prop in(class c0, class c1) { (c0 ∈ c1) }
```

## 左等于 `a8` 

```follow
axiom a8.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s0), c(s2)), in(c(s1), c(s2))))
  diff (s0, s1) (s0, s2)
}
```

```follow
axiom a8.2(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s0), c(s2)), in(c(s1), c(s2))))
  diff (s1, s0) (s1, s2)
}
```

### `a8` 去除 `diff`条件

```follow
thm a8.3(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s0), c(s2)), in(c(s1), c(s2))))
} = {
  syl(eq(c(s0),c(s1)), imp(in(c(s0),c(s2)),in(c(s1),c(s2))), exist(hs0,and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1)))))
  eq.exist.trans.1(s0, s1, hs0)
  a4igen.diff.aee.1(hs0, and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))), imp(in(c(s0),c(s2)),in(c(s1),c(s2))))

  and.syl(eq(c(hs0),c(s0)), eq(c(hs0),c(s1)), in(c(s0),c(s2)), in(c(s1),c(s2)), in(c(hs0),c(s2)))
  a8.1(hs0, s1, s2)

  syl(eq(c(hs0),c(s0)), imp(in(c(s0),c(s2)),in(c(hs0),c(s2))), eq(c(s0),c(hs0)))
  a8.2(s0, hs0, s2)
  eq.com(hs0, s0)
}
```

```follow
thm a8(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s0), c(s2)), in(c(s1), c(s2))))
  |- imp(eq(c(s0), c(s1)), imp(in(c(s1), c(s2)), in(c(s0), c(s2))))
  |- imp(eq(c(s0), c(s1)), iff(in(c(s0), c(s2)), in(c(s1), c(s2))))
  |- imp(eq(c(s0), c(s1)), iff(in(c(s1), c(s2)), in(c(s0), c(s2))))
} = {
  iff.introiid.1(eq(c(s0),c(s1)), in(c(s0),c(s2)), in(c(s1),c(s2)))
  iff.introiid.1(eq(c(s0),c(s1)), in(c(s1),c(s2)), in(c(s0),c(s2)))
  syl(eq(c(s0),c(s1)), imp(in(c(s1),c(s2)),in(c(s0),c(s2))), eq(c(s1),c(s0)))
  eq.com(s0, s1)
  a8.3(s1, s0, s2)
  a8.3(s0, s1, s2)
}
```

### `a8` 与 `exist` 

```follow
thm a8.exist(set s0, set s1, set s2) {
  |- iff(in(c(s0), c(s1)), exist(s2, and(eq(c(s2), c(s0)), in(c(s2), c(s1)))))
  diff (s2, s0) (s2, s1)
} = {
  eq.exist.1(s2, s0, in(c(s2),c(s1)), in(c(s0),c(s1)))
  a8(s2, s0, s1)
}
```

## 右等于 `a9` 

```follow
axiom a9.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s2), c(s0)), in(c(s2), c(s1))))
  diff (s0, s1) (s0, s2)
}
```

```follow
axiom a9.2(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s2), c(s0)), in(c(s2), c(s1))))
  diff (s0, s1) (s1, s2)
}
```

### `a9` 去除 `diff` 条件 

```follow
thm a9.3(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s2), c(s0)), in(c(s2), c(s1))))
} = {
  syl(eq(c(s0),c(s1)), imp(in(c(s2),c(s0)),in(c(s2),c(s1))), exist(hs0,and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1)))))
  eq.exist.trans.1(s0, s1, hs0)
  a4igen.diff.aee.1(hs0, and(eq(c(hs0),c(s0)),eq(c(hs0),c(s1))), imp(in(c(s2),c(s0)),in(c(s2),c(s1))))
  and.syl(eq(c(hs0),c(s0)), eq(c(hs0),c(s1)), in(c(s2),c(s0)), in(c(s2),c(s1)), in(c(s2),c(hs0)))
  a9.1(hs0, s1, s2)
  syl(eq(c(hs0),c(s0)), imp(in(c(s2),c(s0)),in(c(s2),c(hs0))), eq(c(s0),c(hs0)))
  a9.2(s0, hs0, s2)
  eq.com(hs0, s0)
}
```

```follow
thm a9(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(in(c(s2), c(s0)), in(c(s2), c(s1))))
  |- imp(eq(c(s0), c(s1)), imp(in(c(s2), c(s1)), in(c(s2), c(s0))))
  |- imp(eq(c(s0), c(s1)), iff(in(c(s2), c(s0)), in(c(s2), c(s1))))
  |- imp(eq(c(s0), c(s1)), iff(in(c(s2), c(s1)), in(c(s2), c(s0))))
} = {
  iff.introiid.1(eq(c(s0),c(s1)), in(c(s2),c(s0)), in(c(s2),c(s1)))
  iff.introiid.1(eq(c(s0),c(s1)), in(c(s2),c(s1)), in(c(s2),c(s0)))
  syl(eq(c(s0),c(s1)), imp(in(c(s2),c(s1)),in(c(s2),c(s0))), eq(c(s1),c(s0)))
  eq.com(s0, s1)
  a9.3(s1, s0, s2)
  a9.3(s0, s1, s2)
}
```
