
# 等价符号 `=` 

### Specialization 特例（从 `forall` 推导出特例）

下面这个定理 `p0` 和 `p1` 存在隐式的关系，`p1` 可以看成将 `p0` 里面的 `s0` 替换为 `s1` 后获得的命题。这个替换关系需要我们自己完成。

```follow
thm forall.special.1(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  -| imp(not(p1), forall(s0, not(p1))) // nf(s0, p1)
  diff (s0, s1)
} = {
  syl(forall(s0,p0), p1, not(forall(s0,not(p1))))
  coni.1(forall(s0,not(p1)), p1)
  syl(forall(s0,p0), not(forall(s0,not(p1))), exist(s0,p1))
  exist.def.2(s0, p1)
  a4i.eae.1(s0, p0, p1)
  mp(exist(s0,imp(p0,p1)), exist(s0,eq(c(s0),c(s1))))
  a4igen.aee(s0, eq(c(s0),c(s1)), imp(p0,p1))
  a6.exist(s0, s1)
}
```

```follow
thm forall.special.2(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0, p0), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, p1) (s0, s1)
} = {
  forall.special.1(s0, s1, p0, p1)
  a5.forall.1(s0, not(p1))
}
```

## `exist.mp`

```follow
thm exist.mp(set s0, prop p1, prop p2) {
  |- p1
  -| imp(p2, p1)
  -| exist(s0, p2)
  diff (s0, p1)
} = {
  mp(p1, exist(s0,p1))
  a5.exist.1(s0, p1)
  mp(exist(s0,p1), exist(s0,p2))
  a4igen.aee(s0, p2, p1)
}
```

## 公理 `a7` 

公理`a7`涉及等号的有限的传递性，等号两边需要是`set`，并且穿入参数需要有两个 `diff` 限制。它一共有两种形式。

```follow
axiom a7.diff.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
  diff (s0, s1) (s0, s2)
}
```

```follow
axiom a7.diff.2(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
  diff (s0, s1) (s1, s2)
}
```

```follow
thm eq.id(set s0) {
  |- eq(c(s0), c(s0))
} = {
  mp(eq(c(s0),c(s0)), exist(hs0,eq(c(s0),c(s0))))
  a5.exist.1(hs0, eq(c(s0),c(s0)))
  mp(exist(hs0,eq(c(s0),c(s0))), exist(hs0,eq(c(hs0),c(s0))))
  a6.exist(hs0, s0)
  a4igen.aee(hs0, eq(c(hs0),c(s0)), eq(c(s0),c(s0)))
  iid(eq(c(hs0),c(s0)), eq(c(s0),c(s0)))
  a7.diff.1(hs0, s0, s0)
}
```

```follow
thm eq.com.diff(set s0, set s1) {
  |- imp(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
  diff (s0, s1)
} = {
  mp(imp(eq(c(s0),c(s1)),eq(c(s1),c(s0))), eq(c(s0),c(s0)))
  eq.id(s0)
  com12i(eq(c(s0),c(s0)), eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  a7.diff.2(s0, s1, s0)
}
```

```follow
thm a6.2(set s0, set s1) {
  |- not(forall(s0, not(eq(c(s1), c(s0)))))
  |- exist(s0, eq(c(s1), c(s0)))
  diff (s0, s1)
} = {
  mp(not(forall(s0,not(eq(c(s1),c(s0))))), exist(s0,eq(c(s1),c(s0))))
  exist.def.2(s0, eq(c(s1),c(s0)))
  mp(exist(s0,eq(c(s1),c(s0))), exist(s0,eq(c(s0),c(s1))))
  a6.exist(s0, s1)
  a4igen.aee(s0, eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  eq.com.diff(s0, s1)
}
```

```follow
// 不起眼的公理a6，威力逐渐显现
// a6 + 有条件的a7 = 无条件的a7
thm a7(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), imp(eq(c(s0), c(s2)), eq(c(s1), c(s2))))
} = {
  mp(imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2)))), exist(hs0,eq(c(s0),c(hs0))))
  a6.2(hs0, s0)
  a4igen.diff.aee.1(hs0, eq(c(s0),c(hs0)), imp(eq(c(s0),c(s1)),imp(eq(c(s0),c(s2)),eq(c(s1),c(s2)))))
  syld(eq(c(s0),c(hs0)), eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)),eq(c(s1),c(s2))), eq(c(hs0),c(s1)))
  a7.diff.2(s0, hs0, s1)
  com12id(eq(c(s0),c(hs0)), eq(c(hs0),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  rw3(eq(c(s0),c(hs0)), eq(c(s0),c(s2)), imp(eq(c(hs0),c(s1)),eq(c(s1),c(s2))), eq(c(hs0),c(s2)))
  a7.diff.2(s0, hs0, s2)
  com12i(eq(c(hs0),c(s2)), eq(c(hs0),c(s1)), eq(c(s1),c(s2)))
  a7.diff.1(hs0, s1, s2)
}
```

```follow
thm eq.com(set s0, set s1) {
  |- iff(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
  |- imp(eq(c(s0), c(s1)), eq(c(s1), c(s0)))
} = {
  iff.introii.1(eq(c(s0),c(s1)), eq(c(s1),c(s0)))
  mpd(eq(c(s1),c(s0)), eq(c(s0),c(s1)), eq(c(s1),c(s1)))
  a7(s1, s0, s1)
  a1i(eq(c(s1),c(s0)), eq(c(s1),c(s1)))
  eq.id(s1)
  mpd(eq(c(s0),c(s1)), eq(c(s1),c(s0)), eq(c(s0),c(s0)))
  a7(s0, s1, s0)
  a1i(eq(c(s0),c(s1)), eq(c(s0),c(s0)))
  eq.id(s0)
}
```

## `eq.trans`

```follow
thm eq.trans.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s0),c(s2)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s0)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s0)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)), eq(c(s2),c(s0))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s1)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), imp(eq(c(s2),c(s1)), eq(c(s2),c(s0))))
} = {
  a7(s0, s1, s2)
  rw3(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s2),c(s0)), eq(c(s0),c(s2)))
  eq.com(s0, s2)
  rw2(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  eq.com(s2, s1)
  rw3(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s2),c(s0)), eq(c(s0),c(s2)))
  eq.com(s0, s2)
  syl(eq(c(s0),c(s1)), imp(eq(c(s1),c(s2)),eq(c(s0),c(s2))), eq(c(s1),c(s0)))
  eq.com(s0, s1)
  a7(s1, s0, s2)
  rw2(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s2),c(s1)), eq(c(s0),c(s2)))
  eq.com(s2, s0)
  rw2(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s1),c(s2)), eq(c(s0),c(s2)))
  eq.com(s2, s0)
  rw3(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s2),c(s1)), eq(c(s1),c(s2)))
  eq.com(s1, s2)
  a7(s0, s1, s2)
}
```

```follow
thm eq.trans.iff(set s0, set s1, set s2) {
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s0),c(s2)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s0),c(s2)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s0)), eq(c(s1),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s0)), eq(c(s2),c(s1))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s1),c(s2)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s1),c(s2)), eq(c(s2),c(s0))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s1)), eq(c(s0),c(s2))))
  |- imp(eq(c(s0),c(s1)), iff(eq(c(s2),c(s1)), eq(c(s2),c(s0))))
} = {
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s1),c(s2)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s0),c(s2)), eq(c(s2),c(s1)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s1),c(s2)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s2),c(s0)), eq(c(s2),c(s1)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s0),c(s2)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s1),c(s2)), eq(c(s2),c(s0)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s0),c(s2)))
  iff.introiid.1(eq(c(s0),c(s1)), eq(c(s2),c(s1)), eq(c(s2),c(s0)))
  eq.trans.1(s0, s1, s2)
}
```

## `eq.exist`

```follow
// p1 = p0[s0:s1]
thm eq.exist.1(set s0, set s1, prop p0, prop p1) {
  |- iff(exist(s0, and(eq(c(s0), c(s1)), p0)), p1)
  |- iff(p1, exist(s0, and(eq(c(s0), c(s1)), p0)))
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  iff.comi(p1, exist(s0,and(eq(c(s0),c(s1)),p0)))
  iff.syl(exist(s0,and(eq(c(s0),c(s1)),p0)), p1, exist(s0,and(eq(c(s0),c(s1)),p1)))
  a4igen.aee.iff(s0, and(eq(c(s0),c(s1)),p0), and(eq(c(s0),c(s1)),p1))
  and.iffandi.2(eq(c(s0),c(s1)), p0, p1)
  iff.syl(exist(s0,and(eq(c(s0),c(s1)),p1)), p1, and(exist(s0,eq(c(s0),c(s1))),p1))
  a4.diff.and.iff(s0, eq(c(s0),c(s1)), p1)
  and.iffi.1(exist(s0,eq(c(s0),c(s1))), p1)
  a6.exist(s0, s1)
}
```

```follow
thm eq.exist.2(set s0, set s1, prop p0, prop p1) {
  |- imp(exist(s0, and(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  a4igen.diff.aee.1(s0, and(eq(c(s0),c(s1)),p0), p1)
  and.joini(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm eq.exist.3(set s0, set s1, prop p0, prop p1) {
  |- imp(p1, exist(s0, and(eq(c(s0), c(s1)), p0)))
  -| imp(eq(c(s0), c(s1)), imp(p1, p0))
  diff (s0, s1) (s0, p1)
} = {
  syl(p1, exist(s0,and(eq(c(s0),c(s1)),p0)), exist(s0,and(eq(c(s0),c(s1)),p1)))
  a4igen.aee(s0, and(eq(c(s0),c(s1)),p1), and(eq(c(s0),c(s1)),p0))
  and.2andi.3(eq(c(s0),c(s1)), p1, p0)
  syl(p1, exist(s0,and(eq(c(s0),c(s1)),p1)), and(exist(s0,eq(c(s0),c(s1))),p1))
  a4.diff.and.3(s0, eq(c(s0),c(s1)), p1)
  mp(imp(p1,and(exist(s0,eq(c(s0),c(s1))),p1)), exist(s0,eq(c(s0),c(s1))))
  and.intro(exist(s0,eq(c(s0),c(s1))), p1)
  a6.exist(s0, s1)
}
```

## `eq.exist.trans` 

```follow
thm eq.exist.trans.1(set s0, set s1, set s2) {
  |- imp(eq(c(s0), c(s1)), exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))))
  diff (s0, s2) (s1, s2)
} = {
  eq.exist.3(s2, s0, eq(c(s2),c(s1)), eq(c(s0),c(s1)))
  eq.trans.1(s2, s0, s1)
}
```

```follow
thm eq.exist.trans.2(set s0, set s1, set s2) {
  |- imp(exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))), eq(c(s0), c(s1)))
  diff (s0, s2) (s1, s2)
} = {
  eq.exist.2(s2, s0, eq(c(s2),c(s1)), eq(c(s0),c(s1)))
  eq.trans.1(s2, s0, s1)
}
```

```follow
thm eq.exist.trans.iff(set s0, set s1, set s2) {
  |- iff(eq(c(s0), c(s1)), exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))))
  |- iff(exist(s2, and(eq(c(s2),c(s0)), eq(c(s2),c(s1)))), eq(c(s0), c(s1)))
  diff (s0, s2) (s1, s2)
} = {
  iff.introii.1(eq(c(s0),c(s1)), exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))))
  iff.introii.1(exist(s2,and(eq(c(s2),c(s0)),eq(c(s2),c(s1)))), eq(c(s0),c(s1)))
  eq.exist.trans.1(s0, s1, s2)
  eq.exist.trans.2(s0, s1, s2)
}
```

## subs

```follow
// 替换exist中使用的 s0 变量为 s1 变量
thm exist.rw(prop p0, set s0, set s1) {
  |- imp(exist(s1, forall(s0, imp(eq(c(s0), c(s1)), p0))), exist(s0, p0))
  diff (s1, p0) (s1, s0)
} = {
  a4igen.diff.aee.1(s1, forall(s0,imp(eq(c(s0),c(s1)),p0)), exist(s0,p0))
  mp(imp(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s0,p0)), exist(s0,eq(c(s0),c(s1))))
  a6.exist(s0, s1)
  com12i(exist(s0,eq(c(s0),c(s1))), forall(s0,imp(eq(c(s0),c(s1)),p0)), exist(s0,p0))
  a4.aee(s0, eq(c(s0),c(s1)), p0)
}
```

## `eq.forall` 

```follow
thm eq.forall.1(prop p0, set s0, set s1) {
  |- iff(forall(s0, imp(eq(c(s0), c(s1)), p0)), p0)
  |- imp(forall(s0, imp(eq(c(s0), c(s1)), p0)), p0)
  |- imp(p0, forall(s0, imp(eq(c(s0), c(s1)), p0)))
  diff (s0, s1) (s0, p0)
} = {
  iff.lefti(forall(s0,imp(eq(c(s0),c(s1)),p0)), p0)
  iff.righti(p0, forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.mp.1(forall(s0,imp(eq(c(s0),c(s1)),p0)), p0, exist(s0,eq(c(s0),c(s1))))
  a6.exist(s0, s1)
  a4.diff.aea.iff(s0, eq(c(s0),c(s1)), p0)
}
```

```follow
thm eq.forall.2(prop p0, set s0, prop p1, set s1) {
  |- imp(forall(s0, imp(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), imp(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  syl(forall(s0,imp(eq(c(s0),c(s1)),p0)), p1, forall(s0,imp(eq(c(s0),c(s1)),p1)))
  eq.forall.1(p1, s0, s1)
  a4igen.aaa(s0, imp(eq(c(s0),c(s1)),p0), imp(eq(c(s0),c(s1)),p1))
  a2i(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm eq.forall.3(prop p0, set s0, prop p1, set s1) {
  |- imp(p1, forall(s0, imp(eq(c(s0), c(s1)), p0)))
  -| imp(eq(c(s0), c(s1)), imp(p1, p0))
  diff (s0, s1) (s0, p1)
} = {
  a4igen.diff.aaa.2(s0, imp(eq(c(s0),c(s1)),p0), p1)
  com12i(p1, eq(c(s0),c(s1)), p0)
}
```

```follow
thm eq.forall.4(prop p0, set s0, prop p1, set s1) {
  |- iff(forall(s0, imp(eq(c(s0), c(s1)), p0)), p1)
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1)
} = {
  iff.introii.1(forall(s0,imp(eq(c(s0),c(s1)),p0)), p1)
  eq.forall.2(p0, s0, p1, s1)
  eq.forall.3(p0, s0, p1, s1)
  iff.rightid(eq(c(s0),c(s1)), p1, p0)
  iff.leftid(eq(c(s0),c(s1)), p0, p1)
}
```

```follow
thm eq.forall.5(prop p0, set s0, prop p1, set s1) {
  |- imp(forall(s0, p0), forall(s1, p1))
  -| imp(eq(c(s0), c(s1)), imp(p0, p1)))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  gend(s1, p1, forall(s0,p0))
  forall.special.2(s0, s1, p0, p1)
}
```
```follow
thm eq.forall.6(prop p0, set s0, prop p1, set s1) {
  |- iff(forall(s0, p0), forall(s1, p1))
  |- iff(forall(s1, p1), forall(s0, p0))
  |- imp(forall(s0, p0), forall(s1, p1))
  |- imp(forall(s1, p1), forall(s0, p0))
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  iff.lefti(forall(s0,p0), forall(s1,p1))
  iff.righti(forall(s1,p1), forall(s0,p0))
  iff.introii.1(forall(s0,p0), forall(s1,p1))
  iff.introii.1(forall(s1,p1), forall(s0,p0))
  eq.forall.5(p0, s0, p1, s1)
  eq.forall.5(p1, s1, p0, s0)
  iff.rightid(eq(c(s1),c(s0)), p1, p0)
  iff.leftid(eq(c(s0),c(s1)), p0, p1)
  syl(eq(c(s1),c(s0)), iff(p0,p1), eq(c(s0),c(s1)))
  eq.com(s1, s0)
}
```

```follow
thm forall.special.5(set s0, set s1, prop p0, prop p1) {
  |- imp(forall(s0,p0),p0)
  -| imp(eq(c(s0),c(s1)),iff(p0,p1))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  syl(forall(s0,p0), p0, forall(s1,p1))
  forall.special.2(s1, s0, p1, p0)
  eq.forall.5(p0, s0, p1, s1)
  iff.leftid(eq(c(s0),c(s1)), p0, p1)
  iff.rightid(eq(c(s1),c(s0)), p1, p0)
  syl(eq(c(s1),c(s0)), iff(p0,p1), eq(c(s0),c(s1)))
  eq.com(s1, s0)
}
```

```follow
thm forall.special.6(set s0, set s1) {
  |- imp(forall(s0,eq(c(s0),c(s1))), eq(c(s0),c(s1)))
  diff (s0, s1)
} = {
  forall.special.5(s0, hs0, eq(c(s0),c(s1)), eq(c(hs0),c(s1)))
  eq.trans.iff(s0, hs0, s1)
}
```

```follow
thm forall.special.7(set s0, prop p0, set s1, prop p1) {
  |- imp(forall(s0, p0), p0)
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  diff (s1, s0) (s1, p0) (s0, p1)
} = {
  syl(forall(s0,p0), p0, forall(s1,p1))
  eq.forall.6(p0, s0, p1, s1)
  forall.special.1(s1, s0, p1, p0)
  a5.forall.1(s1, not(p0))
  syl(eq(c(s1),c(s0)), imp(p1,p0), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  iff.rightid(eq(c(s0),c(s1)), p1, p0)
}
```

## `forall.eq.com`

```follow
thm forall.eq.com(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s1),c(s2))), forall(s0, eq(c(s2),c(s1))))
} = {
  a4igen.aaa(s0, eq(c(s1),c(s2)), eq(c(s2),c(s1)))
  eq.com(s1, s2)
}
```

## `eq.rw.left` 

```follow
thm eq.rw.left(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s1))))
  diff (s0, s1) (s1, s2)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s1))), forall(hs0,eq(c(hs0),c(s1))))
  eq.forall.5(eq(c(s0),c(s1)), s0, eq(c(hs0),c(s1)), hs0)
  eq.trans.1(s0, hs0, s1)
  eq.forall.5(eq(c(hs0),c(s1)), hs0, eq(c(s2),c(s1)), s2)
  eq.trans.1(hs0, s2, s1)
}
```

```follow
thm eq.rw.right(set s0, set s1, set s2) {
  |- imp(forall(s0, eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s0))))
  diff (s0, s1) (s1, s2) (s0, s2)
} = {
  mpd(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s0))), forall(s2,eq(c(s2),c(s1))))
  eq.rw.left(s0, s1, s2)
  syl(forall(s0,eq(c(s0),c(s1))), imp(forall(s2,eq(c(s2),c(s1))),forall(s2,eq(c(s2),c(s0)))), forall(s2,eq(c(s0),c(s1))))
  a4igen.diff.aaa.2(s2, eq(c(s0),c(s1)), forall(s0,eq(c(s0),c(s1))))
  forall.special.6(s0, s1)
  a4id.aaa(s2, eq(c(s2),c(s1)), eq(c(s2),c(s0)), forall(s2,eq(c(s0),c(s1))))
  a4igen.aaa(s2, eq(c(s0),c(s1)), imp(eq(c(s2),c(s1)),eq(c(s2),c(s0))))
  eq.trans.1(s0, s1, s2)
}
```

```follow
thm eq.rw.diff(set s0, set s1, set s2, set s3) {
  |- imp(forall(s0, eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s3))))
  diff (s0, s1) (s2, s3)
} = {
  syl(forall(s0,eq(c(s0),c(s1))), forall(s2,eq(c(s2),c(s3))), forall(hs0,eq(c(hs0),c(s1))))
  eq.rw.left(s0, s1, hs0)
  syl(forall(hs0,eq(c(hs0),c(s1))), forall(s2,eq(c(s2),c(s3))), forall(s0,eq(c(s0),c(hs0))))
  eq.rw.right(hs0, s1, s0)
  syl(forall(s0,eq(c(s0),c(hs0))), forall(s2,eq(c(s2),c(s3))), forall(s3,eq(c(s3),c(hs0))))
  eq.rw.left(s0, hs0, s3)
  eq.rw.right(s3, hs0, s2)
}
```