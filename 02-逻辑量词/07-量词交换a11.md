
# 量词交换 (Quantifier Commutation)

```follow
axiom a11.1(prop p0, set s0, set s1) {
  |- imp(forall(s0, forall(s1, p0)), forall(s1, forall(s0, p0)))
}
```

## 超出`follow`语言的证明  

原因和上一个公理 `a10`， 这个公理在 `follow` 中不可证明，在一阶逻辑中可以通过前面的公理证明出来。
这里做一个说明。引入一个新的变量 `s2=hw0` 和新的命题 `p2 = p0[s1:hw0]`，需要人工替换。根据不同的 `p0`，需要使用不同的过程证明 `(s1=s2)->(p0<->p2)`，繁琐并且没有必要，所以这里将 `a11` 提升到公理级别进行简化。

```follow
thm a11.weak.1(prop p0, set s0, set s1, prop p2, set s2) {
  |- imp(forall(s0, forall(s1, p0)), forall(s1, forall(s0, p0)))
  -| imp(eq(c(s1),c(s2)), iff(p0, p2))
  diff (s1, s0) (s1, p2) (s1, s2) (s2, p0)
} = {
  syl(forall(s0,forall(s1,p0)), forall(s1,forall(s0,p0)), forall(s0,forall(s2,p2)))
  a4igen.aaa(s0, forall(s1,p0), forall(s2,p2))
  eq.forall.5(p0, s1, p2, s2)
  iff.leftid(eq(c(s1),c(s2)), p0, p2)
  syl(forall(s0,forall(s2,p2)), forall(s1,forall(s0,p0)), forall(s1,forall(s0,forall(s2,p2))))
  a5.forall.1(s1, forall(s0,forall(s2,p2)))
  a4igen.aaa(s1, forall(s0,forall(s2,p2)), forall(s0,p0))
  a4igen.aaa(s0, forall(s2,p2), p0)
  forall.special.2(s2, s1, p2, p0)
  syl(eq(c(s2),c(s1)), imp(p2,p0), eq(c(s1),c(s2)))
  eq.com(s2, s1)
  iff.rightid(eq(c(s1),c(s2)), p2, p0)
}
```

```follow
thm a11.weak.2(prop p0, set s0, set s1, prop p2, set s2) {
  |- imp(forall(s0, forall(s0, p0)), forall(s0, forall(s0, p0)))
} = {
  id(forall(s0,forall(s0,p0)))
}
```

## `a11` 的等价形式

```follow
thm a11.1.iff(prop p0, set s0, set s1) {
  |- iff(forall(s0, forall(s1, p0)), forall(s1, forall(s0, p0)))
} = {
  iff.introii.1(forall(s0,forall(s1,p0)), forall(s1,forall(s0,p0)))
  a11.1(p0, s1, s0)
  a11.1(p0, s0, s1)
}
```

```follow
thm a11.2(prop p0, set s0, set s1) {
  |- imp(exist(s0, exist(s1, p0)), exist(s1, exist(s0, p0)))
  |- iff(exist(s0, exist(s1, p0)), exist(s1, exist(s0, p0)))
} = {
  iff.lefti(exist(s0,exist(s1,p0)), exist(s1,exist(s0,p0)))
  iff.syl(exist(s0,exist(s1,p0)), exist(s1,exist(s0,p0)), not(forall(s0,forall(s1,not(p0)))))
  exist.exist(s0, s1, p0)
  iff.syl(not(forall(s0,forall(s1,not(p0)))), exist(s1,exist(s0,p0)), not(forall(s1,forall(s0,not(p0)))))
  exist.exist(s1, s0, p0)
  mp(iff(not(forall(s0,forall(s1,not(p0)))),not(forall(s1,forall(s0,not(p0))))), iff(forall(s1,forall(s0,not(p0))),forall(s0,forall(s1,not(p0)))))
  iff.con(forall(s1,forall(s0,not(p0))), forall(s0,forall(s1,not(p0))))
  iff.introii.1(forall(s1,forall(s0,not(p0))), forall(s0,forall(s1,not(p0))))
  a11.1(not(p0), s1, s0)
  a11.1(not(p0), s0, s1)
}
```

## `notfree` 

一个不痛不痒的定理 

```follow
thm nf.forallforall(prop p0, set s0, set s1) {
  |- nf(s0, forall(s1, forall(s0, p0)))
} = {
  nf.rw(s0, forall(s1,forall(s0,p0)), forall(s0,forall(s1,p0)))
  a11.1.iff(p0, s0, s1)
  nf.forall.1(s0, forall(s1,p0))
}
```
