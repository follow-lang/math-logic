
# 条件量词

## `exist*`（At Most One, 存在至多一个）

定义逻辑描述 **最多存在一个** 的量词。

```follow
term prop exist*(set s0, prop p0) { ∃ * ( s0 , p0 ) }
```

```follow
axiom exist*.def(set s0, prop p0, set s1) {
  |- iff(exist*(s0,p0), exist(s1, forall(s0,imp(p0,eq(c(s0),c(s1))))))
  diff (s1, s0) (s1, p0)
}
```

```follow
thm exist*.def.ext(set s0, prop p0, set s1) {
  |- iff(exist(s1, forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  |- imp(exist*(s0,p0), exist(s1, forall(s0,imp(p0,eq(c(s0),c(s1))))))
  |- imp(exist(s1, forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  diff (s1, s0) (s1, p0)
} = {
  iff.comi(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  iff.lefti(exist*(s0,p0), exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))))
  iff.righti(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  exist*.def(s0, p0, s1)
}
```

```follow
thm exist*.just(prop p0, set s0, set s1, set s2) {
  |- iff(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2))))))
  diff (s1, s0) (s1, p0) (s2, s0) (s2, p0)
} = {
  iff.syl(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2))))), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  nf.eq.exist.iff.1(s1, hs0, forall(s0,imp(p0,eq(c(s0),c(s1)))), forall(s0,imp(p0,eq(c(s0),c(hs0)))))
  a4id.aaa.iff(s0, imp(p0,eq(c(s0),c(s1))), imp(p0,eq(c(s0),c(hs0))), eq(c(s1),c(hs0)))
  a4igen.diff.aaa.2(s0, iff(imp(p0,eq(c(s0),c(s1))),imp(p0,eq(c(s0),c(hs0)))), eq(c(s1),c(hs0)))
  imp.iffimpd.4(eq(c(s1),c(hs0)), p0, eq(c(s0),c(s1)), eq(c(s0),c(hs0)))
  eq.trans.iff(s1, hs0, s0)
  nf.diff(s1, forall(s0,imp(p0,eq(c(s0),c(hs0)))))
  nf.diff(hs0, forall(s0,imp(p0,eq(c(s0),c(s1)))))
  nf.eq.exist.iff.1(hs0, s2, forall(s0,imp(p0,eq(c(s0),c(hs0)))), forall(s0,imp(p0,eq(c(s0),c(s2)))))
  a4id.aaa.iff(s0, imp(p0,eq(c(s0),c(hs0))), imp(p0,eq(c(s0),c(s2))), eq(c(hs0),c(s2)))
  a4igen.diff.aaa.2(s0, iff(imp(p0,eq(c(s0),c(hs0))),imp(p0,eq(c(s0),c(s2)))), eq(c(hs0),c(s2)))
  imp.iffimpd.4(eq(c(hs0),c(s2)), p0, eq(c(s0),c(hs0)), eq(c(s0),c(s2)))
  eq.trans.iff(hs0, s2, s0)
  nf.diff(hs0, forall(s0,imp(p0,eq(c(s0),c(s2)))))
  nf.diff(s2, forall(s0,imp(p0,eq(c(s0),c(hs0)))))
}
```

```follow
thm exist*.from.notexist(set s0, prop p0) {
  |- imp(not(exist(s0,p0)), exist*(s0,p0))
  |- or(exist(s0,p0), exist*(s0,p0))
  |- or(exist*(s0,p0), exist(s0,p0))
} = {
  or.comi(exist*(s0,p0), exist(s0,p0))
  mp(or(exist(s0,p0),exist*(s0,p0)), imp(not(exist(s0,p0)),exist*(s0,p0)))
  or.def.1(exist(s0,p0), exist*(s0,p0))
  syl(not(exist(s0,p0)), exist*(s0,p0), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, p0, hs0)
  syl(not(exist(s0,p0)), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))), forall(s0,not(p0)))
  exist.def.3(s0, p0)
  a4igen.diff.aee.2(hs0, forall(s0,imp(p0,eq(c(s0),c(hs0)))), forall(s0,not(p0)))
  a4igen.aaa(s0, not(p0), imp(p0,eq(c(s0),c(hs0))))
  cont.1(p0, eq(c(s0),c(hs0)))
}
```

```follow
thm exist*.absorption(set s0, prop p0) {
  |-  iff(exist*(s0,p0), imp(exist(s0,p0),exist*(s0,p0)))
  |-  iff(imp(exist(s0,p0),exist*(s0,p0)), exist*(s0,p0))
  |-  imp(exist*(s0,p0), imp(exist(s0,p0),exist*(s0,p0)))
  |-  imp(imp(exist(s0,p0),exist*(s0,p0)), exist*(s0,p0))
} = {
  iff.introii.1(exist*(s0,p0), imp(exist(s0,p0),exist*(s0,p0)))
  iff.introii.1(imp(exist(s0,p0),exist*(s0,p0)), exist*(s0,p0))
  a1(exist*(s0,p0), exist(s0,p0))
  joinii(exist(s0,p0), exist*(s0,p0), exist*(s0,p0))
  id(exist*(s0,p0))
  exist*.from.notexist(s0, p0)
}
```

```follow
thm a6.exist*(set s0, set s1) {
  |- exist*(s0, eq(c(s0),c(s1)))
  diff (s0, s1)
} = {
  mp(exist*(s0,eq(c(s0),c(s1))), exist(hs0,forall(s0,imp(eq(c(s0),c(s1)),eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, eq(c(s0),c(s1)), hs0)
  mp(exist(hs0,forall(s0,imp(eq(c(s0),c(s1)),eq(c(s0),c(hs0))))), exist(hs0,eq(c(hs0),c(s1))))
  a6.exist(hs0, s1)
  a4igen.aee(hs0, eq(c(hs0),c(s1)), forall(s0,imp(eq(c(s0),c(s1)),eq(c(s0),c(hs0)))))
  a4igen.diff.aaa.2(s0, imp(eq(c(s0),c(s1)),eq(c(s0),c(hs0))), eq(c(hs0),c(s1)))
  eq.trans.1(hs0, s1, s0)
}
```

## `exist*.2exist*` 

```follow
thm exist*.2exist*(set s0, prop p0, prop p1) {
  |- imp(forall(s0,imp(p0,p1)), imp(exist*(s0,p1), exist*(s0,p0)))
} = {
  rw23(forall(s0,imp(p0,p1)), exist*(s0,p1), exist*(s0,p0), exist(hs0,forall(s0,imp(p1,eq(c(s0),c(hs0))))), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, p1, hs0)
  exist*.def.ext(s0, p0, hs0)
  a4id.aee(hs0, forall(s0,imp(p1,eq(c(s0),c(hs0)))), forall(s0,imp(p0,eq(c(s0),c(hs0)))), forall(s0,imp(p0,p1)))
  a4igen.diff.aaa.2(hs0, imp(forall(s0,imp(p1,eq(c(s0),c(hs0)))),forall(s0,imp(p0,eq(c(s0),c(hs0))))), forall(s0,imp(p0,p1)))
  a4id.aaa(s0, imp(p1,eq(c(s0),c(hs0))), imp(p0,eq(c(s0),c(hs0))), forall(s0,imp(p0,p1)))
  a4igen.aaa(s0, imp(p0,p1), imp(imp(p1,eq(c(s0),c(hs0))),imp(p0,eq(c(s0),c(hs0)))))
  trans.2(p0, p1, eq(c(s0),c(hs0)))
}
```

```follow
thm exist*.2exist*i(set s0, prop p0, prop p1) {
  |- imp(exist*(s0,p1), exist*(s0,p0))
  -| forall(s0,imp(p0,p1)) 
} = {
  mp(imp(exist*(s0,p1),exist*(s0,p0)), forall(s0,imp(p0,p1)))
  exist*.2exist*(s0, p0, p1)
}
```

```follow
thm exist*.2exist*id(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, imp(exist*(s0,p1), exist*(s0,p0)))
  -| imp(p2, forall(s0,imp(p0,p1)))
} = {
  syl(p2, imp(exist*(s0,p1),exist*(s0,p0)), forall(s0,imp(p0,p1)))
  exist*.2exist*(s0, p0, p1)
}
```

```follow
thm exist*.2exist*igen(set s0, prop p0, prop p1) {
  |- imp(exist*(s0,p1), exist*(s0,p0))
  -| imp(p0,p1)
} = {
  exist*.2exist*i(s0, p0, p1)
  gen.forall(s0, imp(p0,p1))
}
```

```follow
thm exist*.2exist*idgen(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, imp(exist*(s0,p1), exist*(s0,p0)))
  -| imp(p2, imp(p0,p1))
  -| nf(s0, p2)
} = {
  exist*.2exist*id(s0, p0, p1, p2)
  nf.a4igen.aea.1(s0, p2, imp(p0,p1))
}
```

## `exist*.iffexist*` 

```follow
thm exist*.iffexist*(set s0, prop p0, prop p1) {
  |- imp(forall(s0,iff(p0,p1)), iff(exist*(s0,p1), exist*(s0,p0)))
  |- imp(forall(s0,iff(p0,p1)), iff(exist*(s0,p1), exist*(s0,p0)))
  |- imp(forall(s0,iff(p0,p1)), imp(exist*(s0,p1), exist*(s0,p0)))
  |- imp(forall(s0,iff(p0,p1)), imp(exist*(s0,p1), exist*(s0,p0)))
} = {
  iff.splitd(exist*(s0,p0), exist*(s0,p1), forall(s0,iff(p0,p1)))
  iff.introiid.1(forall(s0,iff(p0,p1)), exist*(s0,p1), exist*(s0,p0))
  exist*.2exist*id(s0, p0, p1, forall(s0,iff(p0,p1)))
  exist*.2exist*id(s0, p1, p0, forall(s0,iff(p0,p1)))
  a4igen.aaa(s0, iff(p0,p1), imp(p1,p0))
  a4igen.aaa(s0, iff(p0,p1), imp(p0,p1))
  iff.left(p0, p1)
  iff.right(p0, p1)
}
```

```follow
thm exist*.iffexist*i(set s0, prop p0, prop p1) {
  |- iff(exist*(s0,p1), exist*(s0,p0))
  |- iff(exist*(s0,p0), exist*(s0,p1))
  |- imp(exist*(s0,p1), exist*(s0,p0))
  |- imp(exist*(s0,p0), exist*(s0,p1))
  -| forall(s0,iff(p0,p1)) 
} = {
  iff.split(exist*(s0,p0), exist*(s0,p1))
  mp(iff(exist*(s0,p1),exist*(s0,p0)), forall(s0,iff(p0,p1)))
  exist*.iffexist*(s0, p0, p1)
}
```

```follow
thm exist*.iffexist*id(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(exist*(s0,p1), exist*(s0,p0)))
  |- imp(p2, iff(exist*(s0,p0), exist*(s0,p1)))
  |- imp(p2, imp(exist*(s0,p0), exist*(s0,p1)))
  |- imp(p2, imp(exist*(s0,p1), exist*(s0,p0)))
  -| imp(p2, forall(s0,iff(p0,p1)))
} = {
  iff.splitd(exist*(s0,p0), exist*(s0,p1), p2)
  syl(p2, iff(exist*(s0,p1),exist*(s0,p0)), forall(s0,iff(p0,p1)))
  exist*.iffexist*(s0, p0, p1)
}
```

```follow
thm exist*.iffexist*igen(set s0, prop p0, prop p1) {
  |- iff(exist*(s0,p1), exist*(s0,p0))
  |- iff(exist*(s0,p0), exist*(s0,p1))
  |- imp(exist*(s0,p1), exist*(s0,p0))
  |- imp(exist*(s0,p0), exist*(s0,p1))
  -| iff(p0,p1) 
} = {
  iff.split(exist*(s0,p0), exist*(s0,p1))
  exist*.iffexist*i(s0, p0, p1)
  gen.forall(s0, iff(p0,p1))
}
```

```follow
thm exist*.iffexist*idgen(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(exist*(s0,p1), exist*(s0,p0)))
  |- imp(p2, iff(exist*(s0,p0), exist*(s0,p1)))
  |- imp(p2, imp(exist*(s0,p0), exist*(s0,p1)))
  |- imp(p2, imp(exist*(s0,p1), exist*(s0,p0)))
  -| imp(p2, iff(p0,p1))
  -| nf(s0,p2)
} = {
  iff.splitd(exist*(s0,p0), exist*(s0,p1), p2)
  exist*.iffexist*id(s0, p0, p1, p2)
  nf.a4igen.aea.1(s0, p2, iff(p0,p1))
}
```

## `exist*` 复合行为

```follow
thm exist*.imp(set s0, prop p0, prop p1) {
  |- imp(exist*(s0, imp(p0, p1)), exist*(s0, p1))
} = {
  exist*.2exist*igen(s0, p1, imp(p0,p1))
  a1(p1, p0)
}
```

```follow
thm exist*.and(set s0, prop p0, prop p1) {
  |- imp(exist*(s0, p0), exist*(s0, and(p0,p1)))
  |- imp(exist*(s0, p1), exist*(s0, and(p0,p1)))
  |- imp(or(exist*(s0, p0),exist*(s0,p1)), exist*(s0, and(p0,p1)))
} = {
  or.casesii(exist*(s0,p0), exist*(s0,p1), exist*(s0,and(p0,p1)))
  exist*.2exist*igen(s0, and(p0,p1), p0)
  and.left(p0, p1)
  exist*.2exist*igen(s0, and(p0,p1), p1)
  and.right(p0, p1)
}
```

```follow
thm exist*.or(set s0, prop p0, prop p1) {
  |- imp(exist*(s0, or(p0,p1)), exist*(s0, p0))
  |- imp(exist*(s0, or(p0,p1)), exist*(s0, p1))
  |- imp(exist*(s0, or(p0,p1)), and(exist*(s0,p0), exist*(s0, p1)))
} = {
  and.introiid(exist*(s0,or(p0,p1)), exist*(s0,p0), exist*(s0,p1))
  exist*.2exist*igen(s0, p0, or(p0,p1))
  exist*.2exist*igen(s0, p1, or(p0,p1))
  or.left(p0, p1)
  or.right(p1, p0)
}
```

## `exist*` 与 `notfree` 

```follow
thm nf.exist*.1(set s0, prop p0) {
  |- nf(s0, exist*(s0,p0))
} = {
  nf.rw(s0, exist*(s0,p0), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, p0, hs0)
  nf.exist.2(forall(s0,imp(p0,eq(c(s0),c(hs0)))), s0, hs0)
  nf.forall.1(s0, imp(p0,eq(c(s0),c(hs0))))
}
```

```follow
thm nf.exist*d.1(set s0, prop p0, set s1, prop p1) {
  |- imp(p0, nf(s0,exist*(s1,p1)))
  -| imp(and(p0,not(forall(s0,eq(c(s0),c(s1))))), nf(s0,p1))
  -| nf(s1, p0)
} = {
  nf.rwd(s0, exist*(s1,p1), exist(hs0,forall(s1,imp(p1,eq(c(s1),c(hs0))))), p0)
  exist*.def.ext(s1, p1, hs0)
  nf.exist.3(p0, forall(s1,imp(p1,eq(c(s1),c(hs0)))), s0, hs0)
  nf.diff(hs0, p0) 
  nf.forall.4(s0, s1, p0, imp(p1,eq(c(s1),c(hs0)))) 
  nf.impiid(s0, p1, eq(c(s1),c(hs0)), and(p0,not(forall(s0,eq(c(s0),c(s1))))))
  syl(and(p0,not(forall(s0,eq(c(s0),c(s1))))), nf(s0,eq(c(s1),c(hs0))), not(forall(s0,eq(c(s0),c(s1)))))
  nf.neq.2(s0, s1, hs0)
  and.right(p0, not(forall(s0,eq(c(s0),c(s1)))))
}
```

```follow
thm nf.exist*d.2(set s0, set s1, prop p0, prop p1) {
  |- imp(p0, nf(s0, exist*(s1,p1)))
  -| imp(p0, nf(s0, p1))
  -| nf(s1, p0)
} = {
  nf.exist*d.1(s0, p0, s1, p1)
  syl(and(p0,not(forall(s0,eq(c(s0),c(s1))))), nf(s0,p1), p0)
  and.left(p0, not(forall(s0,eq(c(s0),c(s1)))))
}
```

```follow
thm nf.exist*.2(set s0, set s1, prop p1) {
  |- nf(s0, exist*(s1,p1))
  -| nf(s0, p1)
} = {
  mp(nf(s0,exist*(s1,p1)), eq(c(hs0),c(hs0)))
  eq.id(hs0)
  nf.exist*d.2(s0, s1, eq(c(hs0),c(hs0)), p1)
  a1i(eq(c(hs0),c(hs0)), nf(s0,p1))
  nf.diff(s1, eq(c(hs0),c(hs0)))
}
```

## `exist*` 其他定义

```follow
thm exist*.def.2(set s0, set s1, prop p0) {
  |- iff(exist*(s0,p0),exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))))
  |- iff(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))),exist*(s0,p0))
  |- imp(exist*(s0,p0),exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))))
  |- imp(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))),exist*(s0,p0))
  -| nf(s1, p0)
  diff (s1, s0)
} = {
  iff.comi(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  iff.lefti(exist*(s0,p0), exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))))
  iff.righti(exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist*(s0,p0))
  iff.syl(exist*(s0,p0), exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def(s0, p0, hs0)
  nf.eq.exist.iff.1(hs0, s1, forall(s0,imp(p0,eq(c(s0),c(hs0)))), forall(s0,imp(p0,eq(c(s0),c(s1)))))
  nf.diff(hs0, forall(s0,imp(p0,eq(c(s0),c(s1)))))
  nf.forall.2(imp(p0,eq(c(s0),c(hs0))), s1, s0)
  nf.impii(s1, p0, eq(c(s0),c(hs0)))
  nf.diff(s1, eq(c(s0),c(hs0)))
  a4id.aaa.iff(s0, imp(p0,eq(c(s0),c(hs0))), imp(p0,eq(c(s0),c(s1))), eq(c(hs0),c(s1)))
  a4igen.diff.aaa.2(s0, iff(imp(p0,eq(c(s0),c(hs0))),imp(p0,eq(c(s0),c(s1)))), eq(c(hs0),c(s1)))
  imp.iffimpd.4(eq(c(hs0),c(s1)), p0, eq(c(s0),c(hs0)), eq(c(s0),c(s1)))
  eq.trans.iff(hs0, s1, s0)
}
```

```follow
thm exist*.def.3(set s0, set s1, prop p0) {
  |- iff(exist*(s0,p0),forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  |- iff(forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0))
  |- imp(exist*(s0,p0),forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  |- imp(forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0))
  -| nf(s1,p0)
  diff (s1,s0)
} = {
  iff.comi(forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0))
  iff.lefti(exist*(s0,p0), forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  iff.righti(forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0))
  iff.introii.1(exist*(s0,p0), forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  nf.a4igen.aea.1(s0, exist*(s0,p0), forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))))
  nf.exist*.1(s0, p0)
  nf.a4igen.aea.1(s1, exist*(s0,p0), imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))
  nf.exist*.2(s1, s0, p0)
  syl(exist*(s0,p0), imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, p0, hs0)
  a4igen.diff.aee.1(hs0, forall(s0,imp(p0,eq(c(s0),c(hs0)))), imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))
  rw3(forall(s0,imp(p0,eq(c(s0),c(hs0)))), and(p0,subs(p0,s0,s1)), eq(c(s0),c(s1)), and(eq(c(s0),c(hs0)),eq(c(s1),c(hs0))))
  and.joini(eq(c(s0),c(hs0)), eq(c(s1),c(hs0)), eq(c(s0),c(s1)))
  eq.trans.1(s0, hs0, s1)
  and.2andiid.1(forall(s0,imp(p0,eq(c(s0),c(hs0)))), p0, subs(p0,s0,s1), eq(c(s0),c(hs0)), eq(c(s1),c(hs0)))
  special.2(imp(p0,eq(c(s0),c(hs0))), s0)
  syl(forall(s0,imp(p0,eq(c(s0),c(hs0)))), imp(subs(p0,s0,s1),eq(c(s1),c(hs0))), subs(imp(p0,eq(c(s0),c(hs0))),s0,s1))
  subs.forall.special(imp(p0,eq(c(s0),c(hs0))), s0, s1)
  rw3(subs(imp(p0,eq(c(s0),c(hs0))),s0,s1), subs(p0,s0,s1), eq(c(s1),c(hs0)), subs(eq(c(s0),c(hs0)),s0,s1))
  subs.2subs.1(p0, eq(c(s0),c(hs0)), s0, s1)
  eq.subs.diff.left(s0, hs0, s1)
  syl(forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0), forall(s1,forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  a11.1(imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))), s0, s1)
  syl(forall(s1,forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist*(s0,p0), imp(exist(s0,p0),exist*(s0,p0)))
  exist*.absorption(s0, p0)
  rw23(forall(s1,forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))), exist(s0,p0), exist*(s0,p0), exist(s1,subs(p0,s0,s1)), exist(s1,forall(s0,imp(p0,eq(c(s0),c(s1))))))
  exist.subs.1(s0, s1, p0)
  exist*.def.2(s0, s1, p0)
  a4id.aee(s1, subs(p0,s0,s1), forall(s0,imp(p0,eq(c(s0),c(s1)))), forall(s1,forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  a4igen.aaa(s1, forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))), imp(subs(p0,s0,s1),forall(s0,imp(p0,eq(c(s0),c(s1))))))
  nf.a4id.aea.1(s0, subs(p0,s0,s1), imp(p0,eq(c(s0),c(s1))), forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))))
  a1i(forall(s0,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))), nf(s0,subs(p0,s0,s1)))
  nf.subs.1(p0, s0, s1)
  a4igen.aaa(s0, imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))), imp(subs(p0,s0,s1),imp(p0,eq(c(s0),c(s1)))))
  com12id(imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))), subs(p0,s0,s1), p0, eq(c(s0),c(s1)))
  and.split(p0, subs(p0,s0,s1), eq(c(s0),c(s1)))
}
```

```follow
// 添加隐式的subs
thm exist*.def.4(set s0, set s1, prop p0, prop p1) {
  |- iff(exist*(s0,p0),forall(s0,forall(s1,imp(and(p0,p1),eq(c(s0),c(s1))))))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  -| nf(s1,p0)
  diff (s1,s0) (s0,p1)
} = {
  iff.syl(exist*(s0,p0), forall(s0,forall(s1,imp(and(p0,p1),eq(c(s0),c(s1))))), forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  exist*.def.3(s0, s1, p0)
  a4igen.aaa.iff(s0, forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))), forall(s1,imp(and(p0,p1),eq(c(s0),c(s1)))))
  a4igen.aaa.iff(s1, imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))), imp(and(p0,p1),eq(c(s0),c(s1))))
  imp.iffimpi.5(eq(c(s0),c(s1)), and(p0,subs(p0,s0,s1)), and(p0,p1))
  and.iffandii.1(p0, p0, subs(p0,s0,s1), p1)
  iff.id(p0)
  subs.eq.7(p0, s0, p1, s1)
}
```

## `exist!` (Unique Existential Quantifier, 存在唯一一个)

```follow
term prop exist!(set s0, prop p0) { ∃!(s0, p0) } 
```

```follow
axiom exist!.def(set s0, prop p0) {
  |- iff(exist!(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
}
```

```follow
thm exist!.def.ext(set s0, prop p0) {
  |- iff(and(exist(s0,p0),exist*(s0,p0)), exist!(s0,p0))
  |- imp(exist!(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  |- imp(and(exist(s0,p0),exist*(s0,p0)), exist!(s0,p0))
} = {
  iff.split(and(exist(s0,p0),exist*(s0,p0)), exist!(s0,p0))
  exist!.def(s0, p0)
}
```

```follow
thm exist!.just(prop p0, set s0, set s1, set s2) {
  |- iff(exist(s1,forall(s0, iff(p0,eq(c(s0),c(s1))))), exist(s2, forall(s0,iff(p0,eq(c(s0),c(s2))))))
  diff (s1, s0) (s1, p0) (s2, s0) (s2, p0)
} = {
  iff.syl(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), exist(s2,forall(s0,iff(p0,eq(c(s0),c(s2))))), exist(hs0,forall(s0,iff(p0,eq(c(s0),c(hs0))))))
  nf.eq.exist.iff.1(s1, hs0, forall(s0,iff(p0,eq(c(s0),c(s1)))), forall(s0,iff(p0,eq(c(s0),c(hs0)))))
  a4id.aaa.iff(s0, iff(p0,eq(c(s0),c(s1))), iff(p0,eq(c(s0),c(hs0))), eq(c(s1),c(hs0)))
  a4igen.diff.aaa.2(s0, iff(iff(p0,eq(c(s0),c(s1))),iff(p0,eq(c(s0),c(hs0)))), eq(c(s1),c(hs0)))
  iff.transid.2(eq(c(s0),c(hs0)), eq(c(s0),c(s1)), p0, eq(c(s1),c(hs0)))
  eq.trans.iff(s1, hs0, s0)
  nf.diff(s1, forall(s0,iff(p0,eq(c(s0),c(hs0)))))
  nf.diff(hs0, forall(s0,iff(p0,eq(c(s0),c(s1)))))
  nf.eq.exist.iff.1(hs0, s2, forall(s0,iff(p0,eq(c(s0),c(hs0)))), forall(s0,iff(p0,eq(c(s0),c(s2)))))
  a4id.aaa.iff(s0, iff(p0,eq(c(s0),c(hs0))), iff(p0,eq(c(s0),c(s2))), eq(c(hs0),c(s2)))
  a4igen.diff.aaa.2(s0, iff(iff(p0,eq(c(s0),c(hs0))),iff(p0,eq(c(s0),c(s2)))), eq(c(hs0),c(s2)))
  iff.transid.2(eq(c(s0),c(s2)), eq(c(s0),c(hs0)), p0, eq(c(hs0),c(s2)))
  eq.trans.iff(hs0, s2, s0)
  nf.diff(hs0, forall(s0,iff(p0,eq(c(s0),c(s2)))))
  nf.diff(s2, forall(s0,iff(p0,eq(c(s0),c(hs0)))))
}
```

## exist! 的其他定义

```follow
thm exist!.def.2.lemma(prop p0, set s0, set s1, set s2) {
  |- iff(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  |- iff(and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))))
  |- imp(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  |- imp(and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))), exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))))
  diff (s0, s1, s2) (p0, s1, s2)
} = {
  iff.split(and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))))
  iff.syl(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), exist(s1,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2))))))))
  nf.a4.diff.exist.and(s1, exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2))))), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  nf.diff(s1, exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2))))))
  a4igen.aee.iff(s1, forall(s0,iff(p0,eq(c(s0),c(s1)))), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  iff.syl(forall(s0,iff(p0,eq(c(s0),c(s1)))), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  nf.a4.diff.exist.and(s2, forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,imp(p0,eq(c(s0),c(s2)))))
  nf.diff(s2, forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.comi(forall(s0,iff(p0,eq(c(s0),c(s1)))), exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  iff.mp.and(exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))), forall(s0,iff(p0,eq(c(s0),c(s1)))), exist(s2,eq(c(s2),c(s1))))
  a6.exist(s2, s1)
  iff.syl(exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))), and(exist(s2,eq(c(s2),c(s1))),forall(s0,iff(p0,eq(c(s0),c(s1))))), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  a4.diff.and.iff(s2, forall(s0,imp(p0,eq(c(s0),c(s2)))), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.syl(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),exist(s2,forall(s0,imp(p0,eq(c(s0),c(s2)))))), and(exist(s2,eq(c(s2),c(s1))),forall(s0,iff(p0,eq(c(s0),c(s1))))), exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  a4.diff.and.iff(s2, forall(s0,imp(p0,eq(c(s0),c(s2)))), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  iff.syl(exist(s2,and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))), and(exist(s2,eq(c(s2),c(s1))),forall(s0,iff(p0,eq(c(s0),c(s1))))), exist(s2,and(eq(c(s2),c(s1)),forall(s0,iff(p0,eq(c(s0),c(s1)))))))
  a4.diff.and.iff(s2, eq(c(s2),c(s1)), forall(s0,iff(p0,eq(c(s0),c(s1)))))
  a4igen.aee.iff(s2, and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), and(eq(c(s2),c(s1)),forall(s0,iff(p0,eq(c(s0),c(s1))))))
  iff.syl(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), and(eq(c(s2),c(s1)),forall(s0,iff(p0,eq(c(s0),c(s1))))), and(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))),eq(c(s2),c(s1))))
  and.iffi.3(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), eq(c(s2),c(s1)))
  syl(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), eq(c(s2),c(s1)), eq(c(s1),c(s2)))
  eq.com(s1, s2)
  syl(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), eq(c(s1),c(s2)), forall(s0,imp(eq(c(s0),c(s1)),eq(c(s0),c(s2)))))
  foralleq.and(s0, s1, s2)
  and.joini(forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,imp(p0,eq(c(s0),c(s2)))), forall(s0,imp(eq(c(s0),c(s1)),eq(c(s0),c(s2)))))
  a4id.aaa(s0, imp(p0,eq(c(s0),c(s2))), imp(eq(c(s0),c(s1)),eq(c(s0),c(s2))), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  a4igen.aaa(s0, imp(eq(c(s0),c(s1)),p0), imp(imp(p0,eq(c(s0),c(s2))),imp(eq(c(s0),c(s1)),eq(c(s0),c(s2)))))
  trans.2(eq(c(s0),c(s1)), p0, eq(c(s0),c(s2)))
  iff.syl(and(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))),eq(c(s2),c(s1))), and(eq(c(s2),c(s1)),forall(s0,iff(p0,eq(c(s0),c(s1))))), and(eq(c(s2),c(s1)),and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2)))))))
  iff.and.com(and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), eq(c(s2),c(s1)))
  and.iffandi.2(eq(c(s2),c(s1)), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), forall(s0,iff(p0,eq(c(s0),c(s1)))))
  iff.rw3(eq(c(s2),c(s1)), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), forall(s0,iff(p0,eq(c(s0),c(s1)))), forall(s0,and(imp(eq(c(s0),c(s1)),p0),imp(p0,eq(c(s0),c(s1))))))
  a4igen.aaa.iff(s0, iff(p0,eq(c(s0),c(s1))), and(imp(eq(c(s0),c(s1)),p0),imp(p0,eq(c(s0),c(s1)))))
  iff.syl(iff(p0,eq(c(s0),c(s1))), and(imp(eq(c(s0),c(s1)),p0),imp(p0,eq(c(s0),c(s1)))), and(imp(p0,eq(c(s0),c(s1))),imp(eq(c(s0),c(s1)),p0)))
  iff.and.com(imp(p0,eq(c(s0),c(s1))), imp(eq(c(s0),c(s1)),p0))
  iff.def.iff(p0, eq(c(s0),c(s1)))
  iff.rw3(eq(c(s2),c(s1)), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s2))))), forall(s0,and(imp(eq(c(s0),c(s1)),p0),imp(p0,eq(c(s0),c(s1))))), and(forall(s0,imp(eq(c(s0),c(s1)),p0)),forall(s0,imp(p0,eq(c(s0),c(s1))))))
  a4.and.3(s0, imp(eq(c(s0),c(s1)),p0), imp(p0,eq(c(s0),c(s1))))
  and.iffandiid.1(eq(c(s2),c(s1)), forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,imp(eq(c(s0),c(s1)),p0)), forall(s0,imp(p0,eq(c(s0),c(s2)))), forall(s0,imp(p0,eq(c(s0),c(s1)))))
  iff.idd(eq(c(s2),c(s1)), forall(s0,imp(eq(c(s0),c(s1)),p0)))
  a4id.aaa.iff(s0, imp(p0,eq(c(s0),c(s2))), imp(p0,eq(c(s0),c(s1))), eq(c(s2),c(s1)))
  a4igen.diff.aaa.2(s0, iff(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s0),c(s1)))), eq(c(s2),c(s1)))
  imp.iffimpd.4(eq(c(s2),c(s1)), p0, eq(c(s0),c(s2)), eq(c(s0),c(s1)))
  eq.trans.iff(s2, s1, s0)
}
```

```follow
thm exist!.def.2(set s0, prop p0, set s1) {
  |- iff(exist!(s0,p0), exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))))
  |- iff(exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  |- imp(exist!(s0,p0), exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))))
  |- imp(exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  diff (s1, s0) (s1, p0)
} = {
  iff.split(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  iff.syl(exist!(s0,p0), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def(s0, p0)
  iff.syl(and(exist(s0,p0),exist*(s0,p0)), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), and(exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))),exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0)))))))
  exist!.def.2.lemma(p0, s0, s1, hs0)
  and.iffandii.1(exist(s0,p0), exist(s1,forall(s0,imp(eq(c(s0),c(s1)),p0))), exist*(s0,p0), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def(s0, p0, hs0)
  exist.rw.iff(s0, p0, s1)
  nf.diff(s1, p0)
}
```

```follow
thm exist!.def.3(set s0, prop p0, set s1) {
  |- iff(exist!(s0,p0), exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))))
  |- iff(exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  |- imp(exist!(s0,p0), exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))))
  |- imp(exist(s1, forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  -| nf(s1, p0)
  diff (s1, s0)
} = {
  iff.split(exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), exist!(s0,p0))
  iff.syl(exist!(s0,p0), exist(s1,forall(s0,iff(p0,eq(c(s0),c(s1))))), exist(hs0,forall(s0,iff(p0,eq(c(s0),c(hs0))))))
  exist!.def.2(s0, p0, hs0)
  nf.eq.exist.iff.1(hs0, s1, forall(s0,iff(p0,eq(c(s0),c(hs0)))), forall(s0,iff(p0,eq(c(s0),c(s1)))))
  nf.diff(hs0, forall(s0,iff(p0,eq(c(s0),c(s1)))))
  nf.forall.2(iff(p0,eq(c(s0),c(hs0))), s1, s0)
  nf.iffii(s1, p0, eq(c(s0),c(hs0)))
  nf.diff(s1, eq(c(s0),c(hs0)))
  a4id.aaa.iff(s0, iff(p0,eq(c(s0),c(hs0))), iff(p0,eq(c(s0),c(s1))), eq(c(hs0),c(s1)))
  a4igen.diff.aaa.2(s0, iff(iff(p0,eq(c(s0),c(hs0))),iff(p0,eq(c(s0),c(s1)))), eq(c(hs0),c(s1)))
  iff.transid.2(eq(c(s0),c(s1)), eq(c(s0),c(hs0)), p0, eq(c(hs0),c(s1)))
  eq.trans.iff(hs0, s1, s0)
}
```

## exist! 与之前的exist的关系

```follow
thm exist!.left(set s0, prop p0) {
  |- imp(exist!(s0,p0), exist(s0,p0))
} = {
  syl(exist!(s0,p0), exist(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def.ext(s0, p0)
  and.left(exist(s0,p0), exist*(s0,p0))
}
```

```follow
thm exist!.right(set s0, prop p0) {
  |- imp(exist!(s0,p0), exist*(s0,p0))
} = {
  syl(exist!(s0,p0), exist*(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def.ext(s0, p0)
  and.right(exist(s0,p0), exist*(s0,p0))
}
````

```follow
thm exist!.lefti(set s0, prop p0) {
  |- exist(s0, p0)
  -| exist!(s0,p0)
} = {
  mp(exist(s0,p0), exist!(s0,p0))
  exist!.left(s0, p0)
}
```

```follow
thm exist!.leftid(set s0, prop p0, prop p1) {
  |- imp(p1, exist(s0, p0))
  -| imp(p1, exist!(s0,p0))
} = {
  syl(p1, exist(s0,p0), exist!(s0,p0))
  exist!.left(s0, p0)
}
```

```follow
thm exist!.righti(set s0, prop p0) {
  |- exist*(s0, p0)
  -| exist!(s0,p0)
} = {
  mp(exist*(s0,p0), exist!(s0,p0))
  exist!.right(s0, p0)
}
```

```follow
thm exist!.rightid(set s0, prop p0, prop p1) {
  |- imp(p1, exist*(s0, p0))
  -| imp(p1, exist!(s0,p0))
} = {
  syl(p1, exist*(s0,p0), exist!(s0,p0))
  exist!.right(s0, p0)
}
```

```follow
thm exist!.iffexist*(set s0, prop p0) {
  |- imp(exist(s0,p0), iff(exist*(s0,p0),exist!(s0,p0)))
  |- imp(exist(s0,p0), iff(exist!(s0,p0), exist*(s0,p0)))
  |- imp(exist(s0,p0), imp(exist*(s0,p0),exist!(s0,p0)))
  |- imp(exist(s0,p0), imp(exist!(s0,p0), exist*(s0,p0)))
} = {
  iff.splitd(exist!(s0,p0), exist*(s0,p0), exist(s0,p0))
  iff.rw3(exist(s0,p0), exist*(s0,p0), exist!(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def(s0, p0)
  and.iff.1(exist(s0,p0), exist*(s0,p0))
}
```

```follow
thm exist!.iffexist*i(set s0, prop p0) {
  |- iff(exist*(s0,p0),exist!(s0,p0))
  |- iff(exist!(s0,p0), exist*(s0,p0))
  |- imp(exist*(s0,p0),exist!(s0,p0))
  |- imp(exist!(s0,p0), exist*(s0,p0))
  -| exist(s0, p0)
} = {
  mp(iff(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  mp(iff(exist!(s0,p0),exist*(s0,p0)), exist(s0,p0))
  mp(imp(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  mp(imp(exist!(s0,p0),exist*(s0,p0)), exist(s0,p0))
  exist!.iffexist*(s0, p0)
}
```

```follow
thm exist!.iffexist*id(set s0, prop p0, prop p1) {
  |- imp(p1, iff(exist*(s0,p0),exist!(s0,p0)))
  |- imp(p1, iff(exist!(s0,p0), exist*(s0,p0)))
  |- imp(p1, imp(exist*(s0,p0),exist!(s0,p0)))
  |- imp(p1, imp(exist!(s0,p0), exist*(s0,p0)))
  -| imp(p1, exist(s0, p0))
} = {
  syl(p1, iff(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  syl(p1, iff(exist!(s0,p0),exist*(s0,p0)), exist(s0,p0))
  syl(p1, imp(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  syl(p1, imp(exist!(s0,p0),exist*(s0,p0)), exist(s0,p0))
  exist!.iffexist*(s0, p0)
}
```

```follow
thm exist.exist*.exist!(set s0, prop p0) {
  |- iff(exist(s0,p0), imp(exist*(s0,p0),exist!(s0,p0)))
  |- iff(imp(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  |- imp(exist(s0,p0), imp(exist*(s0,p0),exist!(s0,p0)))
  |- imp(imp(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
} = {
  iff.split(imp(exist*(s0,p0),exist!(s0,p0)), exist(s0,p0))
  iff.introii.1(exist(s0,p0), imp(exist*(s0,p0),exist!(s0,p0)))
  exist!.iffexist*(s0, p0)
  joinii(exist*(s0,p0), exist!(s0,p0), exist(s0,p0))
  exist!.left(s0, p0)
  coni.1(exist*(s0,p0), exist(s0,p0))
  exist*.from.notexist(s0, p0)
}
```

```follow
thm exist!.iffexist(set s0, prop p0) {
  |- imp(exist*(s0,p0), iff(exist!(s0,p0),exist(s0,p0)))
  |- imp(exist*(s0,p0), iff(exist(s0,p0),exist!(s0,p0)))
  |- imp(exist*(s0,p0), imp(exist!(s0,p0),exist(s0,p0)))
  |- imp(exist*(s0,p0), imp(exist(s0,p0),exist!(s0,p0)))
} = {
  iff.splitd(exist(s0,p0), exist!(s0,p0), exist*(s0,p0))
  iff.rw2(exist*(s0,p0), exist!(s0,p0), exist(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def(s0, p0)
  and.iff.2(exist*(s0,p0), exist(s0,p0))
}
```

```follow
thm exist!.iffexisti(set s0, prop p0) {
  |- iff(exist!(s0,p0),exist(s0,p0))
  |- iff(exist(s0,p0),exist!(s0,p0))
  |- imp(exist!(s0,p0),exist(s0,p0))
  |- imp(exist(s0,p0),exist!(s0,p0))
  -| exist*(s0,p0)
} = {
  mp(iff(exist!(s0,p0),exist(s0,p0)), exist*(s0,p0))
  mp(iff(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  mp(imp(exist!(s0,p0),exist(s0,p0)), exist*(s0,p0))
  mp(imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  exist!.iffexist(s0, p0)
}
```

```follow
thm exist!.iffexistid(set s0, prop p0, prop p1) {
  |- imp(p1, iff(exist!(s0,p0),exist(s0,p0)))
  |- imp(p1, iff(exist(s0,p0),exist!(s0,p0)))
  |- imp(p1, imp(exist!(s0,p0),exist(s0,p0)))
  |- imp(p1, imp(exist(s0,p0),exist!(s0,p0)))
  -| imp(p1, exist*(s0,p0))
} = {
  syl(p1, iff(exist!(s0,p0),exist(s0,p0)), exist*(s0,p0))
  syl(p1, iff(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  syl(p1, imp(exist!(s0,p0),exist(s0,p0)), exist*(s0,p0))
  syl(p1, imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  exist!.iffexist(s0, p0)
}
```

```follow
thm exist*.exist.exist!(set s0, prop p0) {
  |- iff(exist*(s0,p0), imp(exist(s0,p0),exist!(s0,p0)))
  |- iff(imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  |- imp(exist*(s0,p0), imp(exist(s0,p0),exist!(s0,p0)))
  |- imp(imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
} = {
  iff.split(imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  iff.syl(exist*(s0,p0), imp(exist(s0,p0),exist!(s0,p0)), imp(exist(s0,p0),exist*(s0,p0)))
  exist*.absorption(s0, p0)
  imp.iffimpi.2(exist(s0,p0), exist*(s0,p0), exist!(s0,p0))
  exist!.iffexist*(s0, p0)
}
```

## `exist!.iffexist!` 

```follow
thm exist!.iffexist!(set s0, prop p0, prop p1) {
  |- imp(forall(s0,iff(p0,p1)), iff(exist!(s0,p0),exist!(s0,p1)))
} = {
  iff.rw23(forall(s0,iff(p0,p1)), exist!(s0,p0), exist!(s0,p1), and(exist(s0,p0),exist*(s0,p0)), and(exist(s0,p1),exist*(s0,p1)))
  exist!.def(s0, p0)
  exist!.def(s0, p1)
  and.iffandiid.1(forall(s0,iff(p0,p1)), exist(s0,p0), exist(s0,p1), exist*(s0,p0), exist*(s0,p1))
  exist*.iffexist*id(s0, p1, p0, forall(s0,iff(p0,p1)))
  a4.aee.iff(s0, p0, p1)
  a4igen.aaa(s0, iff(p0,p1), iff(p1,p0))
  iff.com(p0, p1)
}
```

```follow
thm exist!.iffexist!i(set s0, prop p0, prop p1) {
  |- iff(exist!(s0,p0),exist!(s0,p1))
  |- iff(exist!(s0,p1),exist!(s0,p0))
  |- imp(exist!(s0,p0),exist!(s0,p1))
  |- imp(exist!(s0,p1),exist!(s0,p0))
  -| forall(s0,iff(p0,p1)) 
} = {
  iff.split(exist!(s0,p1), exist!(s0,p0))
  mp(iff(exist!(s0,p0),exist!(s0,p1)), forall(s0,iff(p0,p1)))
  exist!.iffexist!(s0, p0, p1)
}
```

```follow
thm exist!.iffexist!igen(set s0, prop p0, prop p1) {
  |- iff(exist!(s0,p0),exist!(s0,p1))
  |- iff(exist!(s0,p1),exist!(s0,p0))
  |- imp(exist!(s0,p0),exist!(s0,p1))
  |- imp(exist!(s0,p1),exist!(s0,p0))
  -| iff(p0,p1) 
} = {
  iff.split(exist!(s0,p1), exist!(s0,p0))
  exist!.iffexist!i(s0, p0, p1)
  gen.forall(s0, iff(p0,p1))
}
```

```follow
thm exist!.iffexist!id(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(exist!(s0,p0),exist!(s0,p1)))
  |- imp(p2, iff(exist!(s0,p1), exist!(s0,p0)))
  |- imp(p2, imp(exist!(s0,p0),exist!(s0,p1)))
  |- imp(p2, imp(exist!(s0,p1), exist!(s0,p0)))
  -| imp(p2, forall(s0,iff(p0,p1)))
} = {
  iff.splitd(exist!(s0,p1), exist!(s0,p0), p2)
  syl(p2, iff(exist!(s0,p0),exist!(s0,p1)), forall(s0,iff(p0,p1)))
  exist!.iffexist!(s0, p0, p1)
}
```

```follow
thm exist!.iffexist!idgen(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(exist!(s0,p0),exist!(s0,p1)))
  |- imp(p2, iff(exist!(s0,p1),exist!(s0,p0)))
  |- imp(p2, imp(exist!(s0,p0),exist!(s0,p1)))
  |- imp(p2, imp(exist!(s0,p1),exist!(s0,p0)))
  -| imp(p2, iff(p0,p1))
  -| nf(s0, p2)
} = {
  iff.splitd(exist!(s0,p1), exist!(s0,p0), p2)
  exist!.iffexist!id(s0, p0, p1, p2)
  nf.gend(s0, iff(p0,p1), p2)
}
```

## exist! 与 notfree

```follow
thm nf.exist!.1(set s0, prop p0) {
  |- nf(s0, exist!(s0, p0))
} = {
  nf.rw(s0, exist!(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def.ext(s0, p0)
  nf.andii(s0, exist(s0,p0), exist*(s0,p0))
  nf.exist*.1(s0, p0)
  nf.exist.1(s0, p0)
}
```

```follow
thm nf.exist!d.1(set s0, set s1, prop p0, prop p1) {
  |- imp(p0, nf(s0,exist!(s1,p1)))
  -| imp(and(p0,not(forall(s0,eq(c(s0),c(s1))))), nf(s0,p1))
  -| nf(s1, p0)
} = {
  nf.rwd(s0, exist!(s1,p1), and(exist(s1,p1),exist*(s1,p1)), p0)  
  exist!.def.ext(s1, p1)
  nf.andiid(s0, exist(s1,p1), exist*(s1,p1), p0)
  nf.exist*d.1(s0, p0, s1, p1)
  nf.exist.4(s0, s1, p0, p1)
}
```

```follow
thm nf.exist!d.2(set s0, set s1, prop p0, prop p1) {
  |- imp(p0, nf(s0,exist!(s1,p1)))
  -| imp(p0, nf(s0,p1))
  -| nf(s1, p0)
} = {
  nf.exist!d.1(s0, s1, p0, p1)
  syl(and(p0,not(forall(s0,eq(c(s0),c(s1))))), nf(s0,p1), p0)
  and.left(p0, not(forall(s0,eq(c(s0),c(s1)))))
}
```

```follow
thm nf.exist!.2(set s0, set s1, prop p1) {
  |- nf(s0, exist!(s1,p1))
  -| nf(s0, p1)
} = {
  mp(nf(s0,exist!(s1,p1)), eq(c(hs0),c(hs0)))
  nf.exist!d.2(s0, s1, eq(c(hs0),c(hs0)), p1)
  a1i(eq(c(hs0),c(hs0)), nf(s0,p1))
  nf.diff(s1, eq(c(hs0),c(hs0)))
  eq.id(hs0)
}
```

```follow
thm a6.exist!(set s0, set s1) {
  |- exist!(s0, eq(c(s0),c(s1)))
  diff (s0, s1)
} = {
  mp(exist!(s0,eq(c(s0),c(s1))), and(exist(s0,eq(c(s0),c(s1))),exist*(s0,eq(c(s0),c(s1)))))
  exist!.def.ext(s0, eq(c(s0),c(s1)))
  and.introii(exist(s0,eq(c(s0),c(s1))), exist*(s0,eq(c(s0),c(s1))))
  a6.exist(s0, s1)
  a6.exist*(s0, s1)
}
```

## exist! 与 subs

```follow
thm exist!.subs.lemma1(prop p0, set s0, set s1, set s2) {
  |- iff(exist!(s0,p0), exist!(s1, subs(p0,s0,s1)))
  -| imp(eq(c(s2),c(s1)), iff(subs(p0,s0,s2), subs(p0,s0,s1)))
  -| nf(s1, subs(p0,s0,s2))
  diff (s2, p0) (s2, s0) (s2, s1)
} = {
  iff.syl(exist!(s0,p0), exist!(s1,subs(p0,s0,s1)), exist(hs0,forall(s0,iff(p0,eq(c(s0),c(hs0))))))
  exist!.def.2(s0, p0, hs0)
  iff.syl(exist(hs0,forall(s0,iff(p0,eq(c(s0),c(hs0))))), exist!(s1,subs(p0,s0,s1)), exist(hs0,forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(hs0))))))
  exist!.def.2(s1, subs(p0,s0,s1), hs0)
  a4igen.aee.iff(hs0, forall(s0,iff(p0,eq(c(s0),c(hs0)))), forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(hs0)))))
  iff.syl(forall(s0,iff(p0,eq(c(s0),c(hs0)))), forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(hs0)))), forall(s2,subs(iff(p0,eq(c(s0),c(hs0))),s0,s2)))
  forall.subs.1(s0, s2, iff(p0,eq(c(s0),c(hs0))))
  nf.diff(s2, iff(p0,eq(c(s0),c(hs0))))
  nf.eq.forall.iff.1(s2, s1, subs(iff(p0,eq(c(s0),c(hs0))),s0,s2), iff(subs(p0,s0,s1),eq(c(s1),c(hs0))))
  nf.diff(s2, iff(subs(p0,s0,s1),eq(c(s1),c(hs0))))
  nf.rw(s1, subs(iff(p0,eq(c(s0),c(hs0))),s0,s2), iff(subs(p0,s0,s2),subs(eq(c(s0),c(hs0)),s0,s2)))
  subs.iffsubs.4(p0, eq(c(s0),c(hs0)), s0, s2)
  nf.iffii(s1, subs(p0,s0,s2), subs(eq(c(s0),c(hs0)),s0,s2))
  nf.rw(s1, subs(eq(c(s0),c(hs0)),s0,s2), eq(c(s2),c(hs0)))
  eq.subs.diff.left(s0, hs0, s2)
  nf.diff(s1, eq(c(s2),c(hs0)))
  iff.rw2(eq(c(s2),c(s1)), subs(iff(p0,eq(c(s0),c(hs0))),s0,s2), iff(subs(p0,s0,s1),eq(c(s1),c(hs0))), iff(subs(p0,s0,s2),subs(eq(c(s0),c(hs0)),s0,s2)))
  subs.iffsubs.4(p0, eq(c(s0),c(hs0)), s0, s2)
  iff.iffiffiid(subs(p0,s0,s2), subs(eq(c(s0),c(hs0)),s0,s2), subs(p0,s0,s1), eq(c(s1),c(hs0)), eq(c(s2),c(s1)))
  iff.rw2(eq(c(s2),c(s1)), subs(eq(c(s0),c(hs0)),s0,s2), eq(c(s1),c(hs0)), eq(c(s2),c(hs0)))
  eq.subs.diff.left(s0, hs0, s2)
  eq.trans.iff(s2, s1, hs0)
}
```

```follow
thm exist!.subs(prop p0, set s0, set s1) {
  |- iff(exist!(s0,p0), exist!(s1, subs(p0,s0,s1)))
  -| nf(s1, p0)
} = {
  exist!.subs.lemma1(p0, s0, s1, hs0)
  nf.subs.2(p0, s0, hs0, s1)
  subs.eq.2(p0, s0, hs0, s1)
}
```

```follow
thm exist*.subs.1(prop p0, set s0, set s1) {
  |- iff(exist*(s0,p0), exist*(s1, subs(p0,s0,s1)))
  |- iff(exist*(s1, subs(p0,s0,s1)), exist*(s0,p0))
  |- imp(exist*(s0,p0), exist*(s1, subs(p0,s0,s1)))
  |- imp(exist*(s1, subs(p0,s0,s1)), exist*(s0,p0))
  -| nf(s1, p0)
} = {
  iff.split(exist*(s1,subs(p0,s0,s1)), exist*(s0,p0))
  iff.syl(exist*(s0,p0), exist*(s1,subs(p0,s0,s1)), imp(exist(s0,p0),exist!(s0,p0)))
  exist*.exist.exist!(s0, p0)
  iff.syl(imp(exist(s0,p0),exist!(s0,p0)), exist*(s1,subs(p0,s0,s1)), imp(exist(s1,subs(p0,s0,s1)),exist!(s1,subs(p0,s0,s1))))
  exist*.exist.exist!(s1, subs(p0,s0,s1))
  imp.iffimpii.3(exist(s0,p0), exist!(s0,p0), exist(s1,subs(p0,s0,s1)), exist!(s1,subs(p0,s0,s1)))
  exist!.subs(p0, s0, s1)
  exist.subs.1(s0, s1, p0)
}
```

```follow
thm exist*.rw(set s0, set s1, prop p0, prop p1) {
  |- iff(exist*(s0,p0), exist*(s1,p1))
  |- iff(exist*(s1,p1), exist*(s0,p0))
  |- imp(exist*(s0,p0), exist*(s1,p1))
  |- imp(exist*(s1,p1), exist*(s0,p0))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  -| nf(s1, p0) 
  -| nf(s0, p1)
} = {
  iff.split(exist*(s1,p1), exist*(s0,p0))
  iff.syl(exist*(s0,p0), exist*(s1,p1), exist*(s1,subs(p0,s0,s1)))
  exist*.subs.1(p0, s0, s1)
  exist*.iffexist*igen(s1, p1, subs(p0,s0,s1))
  nf.subs.a5(s0, s1, p0, p1)
}
```

```follow
thm exist!.rw(set s0, set s1, prop p0, prop p1) {
  |- iff(exist!(s0,p0), exist!(s1,p1))
  |- iff(exist!(s1,p1), exist!(s0,p0))
  |- imp(exist!(s0,p0), exist!(s1,p1))
  |- imp(exist!(s1,p1), exist!(s0,p0))
  -| imp(eq(c(s0),c(s1)), iff(p0,p1))
  -| nf(s1, p0) 
  -| nf(s0, p1)
} = {
  iff.split(exist!(s1,p1), exist!(s0,p0))
  iff.syl(exist!(s0,p0), exist!(s1,p1), exist!(s1,subs(p0,s0,s1)))
  exist!.subs(p0, s0, s1)
  exist!.iffexist!igen(s1, subs(p0,s0,s1), p1)
  nf.subs.a5(s0, s1, p0, p1)
}
```

## exist! 的其他定义

```follow
thm exist!.def.4(prop p0, set s0, set s1) {
  |- iff(exist!(s0,p0), and(exist(s0,p0), forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)), eq(c(s0),c(s1)))))))
  -| nf(s1, p0)
  diff (s1, s0)
} = {
  iff.syl(exist!(s0,p0), and(exist(s0,p0),forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1)))))), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def(s0, p0)
  and.iffandii.1(exist(s0,p0), exist(s0,p0), exist*(s0,p0), forall(s0,forall(s1,imp(and(p0,subs(p0,s0,s1)),eq(c(s0),c(s1))))))
  iff.id(exist(s0,p0))
  exist*.def.3(s0, s1, p0)
}
```

```follow
thm exist!.def.5(prop p0, set s0, set s1) {
  |- iff(exist!(s0,p0), exist(s0, and(p0,forall(s1,imp(subs(p0,s0,s1), eq(c(s0),c(s1)))))))
  -| nf(s1, p0)
  diff (s1, s0)
} = {
  iff.syl(exist!(s0,p0), exist(s0,and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1)))))), exist!(s1,subs(p0,s0,s1)))
  exist!.subs(p0, s0, s1)
  iff.syl(exist!(s1,subs(p0,s0,s1)), exist(s0,and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1)))))), exist(s0,forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(s0))))))
  exist!.def.3(s1, subs(p0,s0,s1), s0)
  nf.subs.1(p0, s0, s1)
  a4igen.aee.iff(s0, forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(s0)))), and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1))))))
  iff.syl(forall(s1,iff(subs(p0,s0,s1),eq(c(s1),c(s0)))), and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1))))), forall(s1,and(imp(subs(p0,s0,s1),eq(c(s1),c(s0))),imp(eq(c(s1),c(s0)),subs(p0,s0,s1)))))
  a4igen.aaa.iff(s1, iff(subs(p0,s0,s1),eq(c(s1),c(s0))), and(imp(subs(p0,s0,s1),eq(c(s1),c(s0))),imp(eq(c(s1),c(s0)),subs(p0,s0,s1))))
  iff.def.iff(subs(p0,s0,s1), eq(c(s1),c(s0)))
  iff.syl(forall(s1,and(imp(subs(p0,s0,s1),eq(c(s1),c(s0))),imp(eq(c(s1),c(s0)),subs(p0,s0,s1)))), and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1))))), and(forall(s1,imp(subs(p0,s0,s1),eq(c(s1),c(s0)))),forall(s1,imp(eq(c(s1),c(s0)),subs(p0,s0,s1)))))
  a4.and.3(s1, imp(subs(p0,s0,s1),eq(c(s1),c(s0))), imp(eq(c(s1),c(s0)),subs(p0,s0,s1)))
  iff.syl(and(forall(s1,imp(subs(p0,s0,s1),eq(c(s1),c(s0)))),forall(s1,imp(eq(c(s1),c(s0)),subs(p0,s0,s1)))), and(p0,forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1))))), and(forall(s1,imp(eq(c(s1),c(s0)),subs(p0,s0,s1))),forall(s1,imp(subs(p0,s0,s1),eq(c(s1),c(s0))))))
  iff.and.com(forall(s1,imp(subs(p0,s0,s1),eq(c(s1),c(s0)))), forall(s1,imp(eq(c(s1),c(s0)),subs(p0,s0,s1))))
  and.iffandii.1(forall(s1,imp(eq(c(s1),c(s0)),subs(p0,s0,s1))), p0, forall(s1,imp(subs(p0,s0,s1),eq(c(s1),c(s0)))), forall(s1,imp(subs(p0,s0,s1),eq(c(s0),c(s1)))))
  a4igen.aaa.iff(s1, imp(subs(p0,s0,s1),eq(c(s1),c(s0))), imp(subs(p0,s0,s1),eq(c(s0),c(s1))))
  imp.iffimpi.4(subs(p0,s0,s1), eq(c(s1),c(s0)), eq(c(s0),c(s1)))
  eq.com(s1, s0)
  nf.eq.forall.iff.2(s1, s0, subs(p0,s0,s1), p0)
  subs.eq.5(p0, s1, s0)
}
```

```follow
thm subs!.def.6(prop p0, prop p1, set s0, set s1) {
  |- iff(exist!(s0,p0), and(exist(s0,p0), forall(s0,forall(s1,imp(and(p0,p1), eq(c(s0),c(s1)))))))
  -| imp(eq(c(s0),c(s1)), iff(p0, p1))
  -| nf(s1, p0)
  diff (s0, s1) (s0, p1)
} = {
  iff.syl(exist!(s0,p0), and(exist(s0,p0),forall(s0,forall(s1,imp(and(p0,p1),eq(c(s0),c(s1)))))), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def(s0, p0)
  and.iffandii.1(exist(s0,p0), exist(s0,p0), exist*(s0,p0), forall(s0,forall(s1,imp(and(p0,p1),eq(c(s0),c(s1))))))
  iff.id(exist(s0,p0))
  exist*.def.4(s0, s1, p0, p1)
}
```

## exist! 与 or

```follow
thm exist!.or.1(set s0, prop p0, prop p1) {
  |- imp(and(not(p1),exist!(s0,p0)), exist!(s0,or(p1, p0)))
  -| nf(s0, p1)
} = {
  and.joini(not(p1), exist!(s0,p0), exist!(s0,or(p1,p0)))
  exist!.iffexist!idgen(s0, p0, or(p1,p0), not(p1))
  nf.noti(s0, p1)
  iff.rw3(not(p1), p0, or(p1,p0), imp(not(p1),p0))
  iff.or.def(p1, p0)
  iff.iidd(not(p1), p0)
}
```

```follow
thm exist!.or.2(set s0, prop p0, prop p1) {
  |- imp(and(not(p1),exist!(s0,p0)), exist!(s0,or(p1, p0)))
  diff (s0, p1)
} = {
  exist!.or.1(s0, p0, p1)
  nf.diff(s0, p1)
}
```

```follow
thm exist!.ori.1(set s0, prop p0, prop p1) {
  |- exist!(s0,or(p1, p0))
  -| and(not(p1),exist!(s0,p0)) 
  -| nf(s0, p1)
} = {
  mp(exist!(s0,or(p1,p0)), and(not(p1),exist!(s0,p0)))
  exist!.or.1(s0, p0, p1)
}
```

```follow
thm exist!.ori.2(set s0, prop p0, prop p1) {
  |- exist!(s0,or(p1, p0))
  -| and(not(p1),exist!(s0,p0)) 
  diff (s0, p1)
} = {
  mp(exist!(s0,or(p1,p0)), and(not(p1),exist!(s0,p0)))
  exist!.or.2(s0, p0, p1)
}
```

```follow
thm exist!.orid.1(set s0, prop p0, prop p1, prop p2) {
  |- imp(p0, exist!(s0,or(p1, p0)))
  -| imp(p0, and(not(p1),exist!(s0,p0)))
  -| nf(s0, p1)
} = {
  syl(p0, exist!(s0,or(p1,p0)), and(not(p1),exist!(s0,p0)))
  exist!.or.1(s0, p0, p1)
}
```

```follow
thm exist!.orid.2(set s0, prop p0, prop p1, prop p2) {
  |- imp(p2, exist!(s0,or(p1, p0)))
  -| imp(p2, and(not(p1),exist!(s0,p0)))
  diff (s0, p1)
} = {
  syl(p2, exist!(s0,or(p1,p0)), and(not(p1),exist!(s0,p0)))
  exist!.or.2(s0, p0, p1)
}
```

```follow
thm notexist.exist!.or(set s0, prop p0, prop p1) {
  |- imp(not(exist(s0,p0)), iff(exist!(s0,or(p0,p1)), exist!(s0,p1)))
} = {
  exist!.iffexist!idgen(s0, or(p0,p1), p1, not(exist(s0,p0)))
  nf.noti(s0, exist(s0,p0))
  nf.exist.1(s0, p0)
  syl(not(exist(s0,p0)), iff(or(p0,p1),p1), not(p0))
  coni.4(exist(s0,p0), p0)
  special.1(p0, s0)
  iff.rw2(not(p0), or(p0,p1), p1, imp(not(p0),p1))
  iff.or.def(p0, p1)
  iff.iidd(not(p0), p1)
}
```

## exist* 与 subs

```follow
thm exist*.subs.2(prop p0, set s0, set s1, set s2) {
  |- iff(subs(exist*(s0, p0), s1, s2), exist*(s0, subs(p0, s1, s2)))
  diff (s0, s1) (s0, s2)
} = {
  iff.syl(subs(exist*(s0,p0),s1,s2), exist*(s0,subs(p0,s1,s2)), exist(hs0,forall(s0,imp(subs(p0,s1,s2),eq(c(s0),c(hs0))))))
  exist*.def.2(s0, hs0, subs(p0,s1,s2))
  nf.diff(hs0, subs(p0,s1,s2))
  iff.syl(subs(exist*(s0,p0),s1,s2), exist(hs0,forall(s0,imp(subs(p0,s1,s2),eq(c(s0),c(hs0))))), subs(exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))),s1,s2))
  subs.iffsubsigen(exist*(s0,p0), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))), s1, s2)
  exist*.def(s0, p0, hs0)
  iff.syl(subs(exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))),s1,s2), exist(hs0,forall(s0,imp(subs(p0,s1,s2),eq(c(s0),c(hs0))))), exist(hs0,subs(forall(s0,imp(p0,eq(c(s0),c(hs0)))),s1,s2)))
  exist.subs.2(forall(s0,imp(p0,eq(c(s0),c(hs0)))), hs0, s1, s2)
  a4igen.aee.iff(hs0, subs(forall(s0,imp(p0,eq(c(s0),c(hs0)))),s1,s2), forall(s0,imp(subs(p0,s1,s2),eq(c(s0),c(hs0)))))
  iff.syl(subs(forall(s0,imp(p0,eq(c(s0),c(hs0)))),s1,s2), forall(s0,imp(subs(p0,s1,s2),eq(c(s0),c(hs0)))), forall(s0,subs(imp(p0,eq(c(s0),c(hs0))),s1,s2)))
  forall.subs.2(imp(p0,eq(c(s0),c(hs0))), s0, s1, s2)
  a4igen.aaa.iff(s0, subs(imp(p0,eq(c(s0),c(hs0))),s1,s2), imp(subs(p0,s1,s2),eq(c(s0),c(hs0))))
  iff.syl(subs(imp(p0,eq(c(s0),c(hs0))),s1,s2), imp(subs(p0,s1,s2),eq(c(s0),c(hs0))), imp(subs(p0,s1,s2),subs(eq(c(s0),c(hs0)),s1,s2)))
  subs.2subs.4(p0, eq(c(s0),c(hs0)), s1, s2)
  imp.iffimpi.4(subs(p0,s1,s2), subs(eq(c(s0),c(hs0)),s1,s2), eq(c(s0),c(hs0)))
  subs.id.diff.3(eq(c(s0),c(hs0)), s1, s2)
}
```

## exist! 复合关系

```follow
// 函数的逆 1
thm exist!.2exist*(set s0, prop p0, prop p1) {
  |- imp(forall(s0,imp(p0,p1)), imp(exist!(s0,p1), exist*(s0,p0)))
} = {
  rw2(forall(s0,imp(p0,p1)), exist!(s0,p1), exist*(s0,p0), exist*(s0,p1))
  exist*.2exist*(s0, p0, p1)
  exist!.right(s0, p1)
}
```

```follow
// 函数的逆 2
thm exist!.2exist!(set s0, prop p0, prop p1) {
  |- imp(
    and(exist(s0,p0), forall(s0,imp(p0,p1))),
    imp(exist!(s0,p1), exist!(s0,p0))
  )
} = {
  rw3(and(exist(s0,p0),forall(s0,imp(p0,p1))), exist!(s0,p1), exist!(s0,p0), and(exist(s0,p0),exist*(s0,p0)))
  exist!.def.ext(s0, p0)
  com12i(and(exist(s0,p0),forall(s0,imp(p0,p1))), exist!(s0,p1), and(exist(s0,p0),exist*(s0,p0)))
  and.2andid.2(forall(s0,imp(p0,p1)), exist*(s0,p0), exist(s0,p0), exist!(s0,p1))
  com12i(exist!(s0,p1), forall(s0,imp(p0,p1)), exist*(s0,p0))
  exist!.2exist*(s0, p0, p1)
}
```

```follow
thm exist*.and.lemma(set s0, prop p0, prop p1) {
  |- iff(exist*(s0,and(p0,p1)),imp(p0,exist*(s0,p1)))
  -| imp(p0,iff(exist*(s0,p1),exist*(s0,and(p0,p1))))
  -| imp(exist(s0,and(p0,p1)),p0)
} = {
  iff.introii.1(exist*(s0,and(p0,p1)), imp(p0,exist*(s0,p1)))
  com12i(exist*(s0,and(p0,p1)), p0, exist*(s0,p1))
  iff.rightid(p0, exist*(s0,and(p0,p1)), exist*(s0,p1))
  joinii(p0, exist*(s0,p1), exist*(s0,and(p0,p1)))
  exist*.and(s0, p0, p1)
  coni.1(p0, exist*(s0,and(p0,p1)))
  syl(not(exist*(s0,and(p0,p1))), p0, exist(s0,and(p0,p1)))
  coni.1(exist*(s0,and(p0,p1)), exist(s0,and(p0,p1)))
  exist*.from.notexist(s0, and(p0,p1))
}
```

```follow
thm exist*.and.1(set s0, prop p0, prop p1) {
  |- iff(exist*(s0,and(p0,p1)),imp(p0,exist*(s0,p1)))
  |- iff(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  |- imp(exist*(s0,and(p0,p1)),imp(p0,exist*(s0,p1)))
  |- imp(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  -| nf(s0, p0)
} = {
  iff.split(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  exist*.and.lemma(s0, p0, p1)
  exist*.iffexist*id(s0, and(p0,p1), p1, p0)
  nf.a4igen.aea.1(s0, p0, iff(and(p0,p1),p1))
  and.iff.1(p0, p1)
  nf.a4igen.aea.3(s0, p0, and(p0,p1))
  and.left(p0, p1)
}
```

```follow
thm exist*.and.2(set s0, prop p0, prop p1) {
  |- iff(exist*(s0,and(p0,p1)),imp(p0,exist*(s0,p1)))
  |- iff(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  |- imp(exist*(s0,and(p0,p1)),imp(p0,exist*(s0,p1)))
  |- imp(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  diff (s0, p0)
} = {
  iff.split(imp(p0,exist*(s0,p1)), exist*(s0,and(p0,p1)))
  exist*.and.1(s0, p0, p1)
  nf.diff(s0, p0)
}
```

```follow
thm exist!.and.1(set s0, prop p0, prop p1) {
  |- iff(exist!(s0,and(p0,p1)),and(p0,exist!(s0,p1)))
  |- iff(and(p0,exist!(s0,p1)),exist!(s0,and(p0,p1)))
  |- imp(exist!(s0,and(p0,p1)),and(p0,exist!(s0,p1)))
  |- imp(and(p0,exist!(s0,p1)),exist!(s0,and(p0,p1)))
  -| nf(s0, p0)
} = {
  iff.split(and(p0,exist!(s0,p1)), exist!(s0,and(p0,p1)))
  iff.introii.1(exist!(s0,and(p0,p1)), and(p0,exist!(s0,p1)))
  mpd(exist!(s0,and(p0,p1)), and(p0,exist!(s0,p1)), p0)
  syl(exist!(s0,and(p0,p1)), p0, exist(s0,and(p0,p1)))
  exist!.left(s0, and(p0,p1))
  nf.a4igen.aea.3(s0, p0, and(p0,p1))
  and.left(p0, p1)
  com12i(exist!(s0,and(p0,p1)), p0, and(p0,exist!(s0,p1)))
  syld(p0, exist!(s0,and(p0,p1)), and(p0,exist!(s0,p1)), and(p0,exist!(s0,and(p0,p1))))
  syld(p0, exist!(s0,and(p0,p1)), and(p0,exist!(s0,and(p0,p1))), exist!(s0,and(p0,p1)))
  and.intro(p0, exist!(s0,and(p0,p1)))
  idd(p0, exist!(s0,and(p0,p1)))
  and.2andid.2(exist!(s0,and(p0,p1)), exist!(s0,p1), p0, p0)
  exist!.iffexist!idgen(s0, and(p0,p1), p1, p0)
  and.iff.1(p0, p1)
  and.joini(p0, exist!(s0,p1), exist!(s0,and(p0,p1)))
  exist!.iffexist!idgen(s0, p1, and(p0,p1), p0)
  and.iff.1(p0, p1)
}
```

```follow
thm exist*.nest.1(set s0, prop p0) {
  |- exist*(s0, and(p0,exist*(s0,p0)))
} = {
  mp(exist*(s0,and(p0,exist*(s0,p0))), exist*(s0,and(exist*(s0,p0),p0)))
  exist*.iffexist*igen(s0, and(p0,exist*(s0,p0)), and(exist*(s0,p0),p0))
  iff.and.com(p0, exist*(s0,p0))
  mp(exist*(s0,and(exist*(s0,p0),p0)), imp(exist*(s0,p0),exist*(s0,p0)))
  exist*.and.1(s0, exist*(s0,p0), p0)
  nf.exist*.1(s0, p0)
  id(exist*(s0,p0))
}
```

```follow
thm exist*.nest.2(set s0, prop p0) {
  |- exist*(s0, and(p0,exist!(s0,p0)))
} = {
  mp(exist*(s0,and(p0,exist!(s0,p0))), exist*(s0,and(p0,exist*(s0,p0))))
  exist*.nest.1(s0, p0)
  exist*.2exist*igen(s0, and(p0,exist!(s0,p0)), and(p0,exist*(s0,p0)))
  and.2andi.2(exist!(s0,p0), exist*(s0,p0), p0)
  exist!.right(s0, p0)
}
```

```follow
thm exist*.pick(set s0, prop p0, prop p1) {
  |- imp(and(exist*(s0,p0), exist(s0,and(p0,p1))),imp(p0,p1))
  |- imp(exist*(s0,p0), imp(exist(s0,and(p0,p1)),imp(p0,p1)))
  |- imp(and(exist(s0,and(p0,p1)),exist*(s0,p0)),imp(p0,p1))
  |- imp(exist(s0,and(p0,p1)), imp(exist*(s0,p0), imp(p0,p1)))
} = {
  and.joini(exist(s0,and(p0,p1)), exist*(s0,p0), imp(p0,p1))
  com12i(exist(s0,and(p0,p1)), exist*(s0,p0), imp(p0,p1))
  and.joini(exist*(s0,p0), exist(s0,and(p0,p1)), imp(p0,p1))
  syl(exist*(s0,p0), imp(exist(s0,and(p0,p1)),imp(p0,p1)), exist(hs0,forall(s0,imp(p0,eq(c(s0),c(hs0))))))
  exist*.def.ext(s0, p0, hs0)
  a4igen.diff.aee.1(hs0, forall(s0,imp(p0,eq(c(s0),c(hs0)))), imp(exist(s0,and(p0,p1)),imp(p0,p1)))
  com12id(forall(s0,imp(p0,eq(c(s0),c(hs0)))), exist(s0,and(p0,p1)), p0, p1)
  syld(forall(s0,imp(p0,eq(c(s0),c(hs0)))), p0, imp(exist(s0,and(p0,p1)),p1), eq(c(s0),c(hs0)))
  special.2(imp(p0,eq(c(s0),c(hs0))), s0)
  com12id(forall(s0,imp(p0,eq(c(s0),c(hs0)))), eq(c(s0),c(hs0)), exist(s0,and(p0,p1)), p1)
  rw3(forall(s0,imp(p0,eq(c(s0),c(hs0)))), exist(s0,and(p0,p1)), imp(eq(c(s0),c(hs0)),p1), forall(s0,imp(eq(c(s0),c(hs0)),p1)))
  special.2(imp(eq(c(s0),c(hs0)),p1), s0)
  rw3(forall(s0,imp(p0,eq(c(s0),c(hs0)))), exist(s0,and(p0,p1)), forall(s0,imp(eq(c(s0),c(hs0)),p1)), exist(s0,and(eq(c(s0),c(hs0)),p1)))
  subs.def.4.lemma(p1, s0, hs0)
  a4id.aee(s0, and(p0,p1), and(eq(c(s0),c(hs0)),p1), forall(s0,imp(p0,eq(c(s0),c(hs0)))))
  a4igen.aaa(s0, imp(p0,eq(c(s0),c(hs0))), imp(and(p0,p1),and(eq(c(s0),c(hs0)),p1)))
  and.2andid.2(p0, eq(c(s0),c(hs0)), p1, imp(p0,eq(c(s0),c(hs0))))
  id(imp(p0,eq(c(s0),c(hs0))))
}
```

```follow
thm exist!.pick(set s0, prop p0, prop p1) {
  |- imp(and(exist!(s0,p0),exist(s0,and(p0,p1))),imp(p0,p1))
  |- imp(exist!(s0,p0), imp(exist(s0,and(p0,p1)),imp(p0,p1)))
  |- imp(and(exist(s0,and(p0,p1)),exist!(s0,p0)),imp(p0,p1))
  |- imp(exist(s0,and(p0,p1)), imp(exist!(s0,p0), imp(p0,p1)))
} = {
  and.joini(exist(s0,and(p0,p1)), exist!(s0,p0), imp(p0,p1))
  com12i(exist(s0,and(p0,p1)), exist!(s0,p0), imp(p0,p1))
  and.joini(exist!(s0,p0), exist(s0,and(p0,p1)), imp(p0,p1))
  syl(exist!(s0,p0), imp(exist(s0,and(p0,p1)),imp(p0,p1)), exist*(s0,p0))
  exist*.pick(s0, p0, p1)
  exist!.right(s0, p0)
}
```

```follow
thm exist!.pick.forall(set s0, prop p0, prop p1) {
  |- imp(and(exist!(s0,p0),exist(s0,and(p0,p1))),forall(s0,imp(p0,p1)))
  |- imp(exist!(s0,p0), imp(exist(s0,and(p0,p1)),forall(s0,imp(p0,p1))))
  |- imp(and(exist(s0,and(p0,p1)),exist!(s0,p0)),forall(s0,imp(p0,p1)))
  |- imp(exist(s0,and(p0,p1)), imp(exist!(s0,p0), forall(s0,imp(p0,p1))))
} = {
  and.joini(exist(s0,and(p0,p1)), exist!(s0,p0), forall(s0,imp(p0,p1)))
  com12i(exist(s0,and(p0,p1)), exist!(s0,p0), forall(s0,imp(p0,p1)))
  and.spliti(exist!(s0,p0), exist(s0,and(p0,p1)), forall(s0,imp(p0,p1)))
  nf.a4igen.aea.1(s0, and(exist!(s0,p0),exist(s0,and(p0,p1))), imp(p0,p1))
  exist!.pick(s0, p0, p1)
  nf.andii(s0, exist!(s0,p0), exist(s0,and(p0,p1)))
  nf.exist!.1(s0, p0)
  nf.exist.1(s0, and(p0,p1))
}
```

```follow
thm exist!.pick.iff(set s0, prop p0, prop p1) {
  |- imp(and(exist!(s0,p0),and(exist!(s0,p1),exist(s0,and(p0,p1)))),iff(p0,p1))
} = {
  and.joini(exist!(s0,p0), and(exist!(s0,p1),exist(s0,and(p0,p1))), iff(p0,p1))
  and.joinid(exist!(s0,p0), exist!(s0,p1), exist(s0,and(p0,p1)), iff(p0,p1))
  and.spliti(exist!(s0,p0), exist!(s0,p1), imp(exist(s0,and(p0,p1)),iff(p0,p1)))
  rw3(and(exist!(s0,p0),exist!(s0,p1)), exist(s0,and(p0,p1)), iff(p0,p1), and(imp(p0,p1),imp(p1,p0)))
  iff.def(p0, p1)
  com12i(and(exist!(s0,p0),exist!(s0,p1)), exist(s0,and(p0,p1)), and(imp(p0,p1),imp(p1,p0)))
  and.2andiid.1(exist(s0,and(p0,p1)), exist!(s0,p0), exist!(s0,p1), imp(p0,p1), imp(p1,p0))
  exist!.pick(s0, p0, p1)
  syl(exist(s0,and(p0,p1)), imp(exist!(s0,p1),imp(p1,p0)), exist(s0,and(p1,p0)))
  exist!.pick(s0, p1, p0)
  a4igen.aee(s0, and(p0,p1), and(p1,p0))
  and.com(p0, p1)
}
```

```follow
thm exist!.pick.iff.forall(set s0, prop p0, prop p1) {
  |- imp(exist!(s0,p0),iff(exist(s0,and(p0,p1)),forall(s0,imp(p0,p1))))
} = {
  iff.introiid.1(exist!(s0,p0), exist(s0,and(p0,p1)), forall(s0,imp(p0,p1)))
  exist!.pick.forall(s0, p0, p1)
  com12i(exist!(s0,p0), forall(s0,imp(p0,p1)), exist(s0,and(p0,p1)))
  rw2(forall(s0,imp(p0,p1)), exist!(s0,p0), exist(s0,and(p0,p1)), exist(s0,p0))
  exist!.left(s0, p0)
  a4id.aee(s0, p0, and(p0,p1), forall(s0,imp(p0,p1)))
  a4igen.aaa(s0, imp(p0,p1), imp(p0,and(p0,p1)))
  a2i(p0, p1, and(p0,p1))
  and.intro(p0, p1)
}
```

```follow
thm exist*.exist.and(set s0, set s1, prop p0, prop p1) {
  |- imp(and(exist*(s0,p0),forall(s0,exist*(s1,p1))),exist*(s1,exist(s0,and(p0,p1))))
  |- imp(exist*(s0,p0), imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1)))))
  |- imp(and(forall(s0,exist*(s1,p1)),exist*(s0,p0)),exist*(s1,exist(s0,and(p0,p1))))
  |- imp(forall(s0,exist*(s1,p1)), imp(exist*(s0,p0),exist*(s1,exist(s0,and(p0,p1)))))
  -| nf(s1, p0)
} = {
  and.joini(forall(s0,exist*(s1,p1)), exist*(s0,p0), exist*(s1,exist(s0,and(p0,p1))))
  com12i(forall(s0,exist*(s1,p1)), exist*(s0,p0), exist*(s1,exist(s0,and(p0,p1))))
  and.joini(exist*(s0,p0), forall(s0,exist*(s1,p1)), exist*(s1,exist(s0,and(p0,p1))))
  contii.4(imp(exist*(s0,p0),imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1))))), exist(s0,p0))
  nf.a4igen.aea.3(s0, imp(exist*(s0,p0),imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1))))), p0)
  nf.impii(s0, exist*(s0,p0), imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1)))))
  nf.exist*.1(s0, p0)
  nf.impii(s0, forall(s0,exist*(s1,p1)), exist*(s1,exist(s0,and(p0,p1))))
  nf.forall.1(s0, exist*(s1,p1))
  nf.exist*.2(s0, s1, exist(s0,and(p0,p1)))
  nf.exist.1(s0, and(p0,p1))
  rw3(p0, exist*(s0,p0), imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1)))), forall(s1,imp(exist(s0,and(p0,p1)),p1)))
  rw2(forall(s1,imp(exist(s0,and(p0,p1)),p1)), forall(s0,exist*(s1,p1)), exist*(s1,exist(s0,and(p0,p1))), exist*(s1,p1))
  special.2(exist*(s1,p1), s0)
  exist*.2exist*(s1, exist(s0,and(p0,p1)), p1)
  nf.a4id.aea.1(s1, exist*(s0,p0), imp(exist(s0,and(p0,p1)),p1), p0)
  a1i(p0, nf(s1,exist*(s0,p0)))
  nf.exist*.2(s1, s0, p0)
  nf.a4igen.aea.1(s1, p0, imp(exist*(s0,p0),imp(exist(s0,and(p0,p1)),p1)))
  com12i(p0, exist*(s0,p0), imp(exist(s0,and(p0,p1)),p1))
  com12id(exist*(s0,p0), p0, exist(s0,and(p0,p1)), p1)
  exist*.pick(s0, p0, p1)
  a1id(not(exist(s0,p0)), exist*(s0,p0), imp(forall(s0,exist*(s1,p1)),exist*(s1,exist(s0,and(p0,p1)))))
  a1id(not(exist(s0,p0)), forall(s0,exist*(s1,p1)), exist*(s1,exist(s0,and(p0,p1))))
  syl(not(exist(s0,p0)), exist*(s1,exist(s0,and(p0,p1))), not(exist(s1,exist(s0,and(p0,p1)))))
  exist*.from.notexist(s1, exist(s0,and(p0,p1)))
  coni.4(exist(s0,p0), exist(s1,exist(s0,and(p0,p1))))
  nf.a4igen.aea.3(s1, exist(s0,p0), exist(s0,and(p0,p1)))
  nf.exist.2(p0, s1, s0)
  a4igen.aee(s0, and(p0,p1), p0)
  and.left(p0, p1)
}
```

```follow
thm exist*.exist.forall(set s0, set s1, prop p0) {
  |- imp(exist*(s0,exist(s1,p0)),forall(s1,exist*(s0,p0)))
} = {
  nf.a4igen.aea.1(s1, exist*(s0,exist(s1,p0)), exist*(s0,p0))
  nf.exist*.2(s1, s0, exist(s1,p0))
  nf.exist.1(s1, p0)
  exist*.2exist*igen(s0, p0, exist(s1,p0))
  special.1(p0, s1)
}
```

```follow
thm exist!.exist.com(set s0, set s1, prop p0) {
  |- imp(exist!(s0,exist(s1,p0)),exist(s1,exist!(s0,p0)))
} = {
  syl(exist!(s0,exist(s1,p0)), exist(s1,exist!(s0,p0)), and(exist(s0,exist(s1,p0)),exist*(s0,exist(s1,p0))))
  exist!.def.ext(s0, exist(s1,p0))
  and.joini(exist(s0,exist(s1,p0)), exist*(s0,exist(s1,p0)), exist(s1,exist!(s0,p0)))
  com12i(exist(s0,exist(s1,p0)), exist*(s0,exist(s1,p0)), exist(s1,exist!(s0,p0)))
  rw2(exist*(s0,exist(s1,p0)), exist(s0,exist(s1,p0)), exist(s1,exist!(s0,p0)), exist(s1,exist(s0,p0)))
  a11.2(p0, s0, s1)
  a4id.aee(s1, exist(s0,p0), exist!(s0,p0), exist*(s0,exist(s1,p0)))
  nf.a4igen.aea.1(s1, exist*(s0,exist(s1,p0)), imp(exist(s0,p0),exist!(s0,p0)))
  nf.exist*.2(s1, s0, exist(s1,p0))
  nf.exist.1(s1, p0)
  syl(exist*(s0,exist(s1,p0)), imp(exist(s0,p0),exist!(s0,p0)), exist*(s0,p0))
  exist*.exist.exist!(s0, p0)
  exist*.2exist*igen(s0, p0, exist(s1,p0))
  special.1(p0, s1)
}
```

```follow
thm exist!.exist*.com(set s0, set s1, prop p0) {
  |- imp(exist!(s0,exist*(s1,p0)),exist*(s0,exist!(s1,p0)))
} = {
  syl(exist!(s0,exist*(s1,p0)), exist*(s0,exist!(s1,p0)), exist*(s0,exist*(s1,p0)))
  exist!.right(s0, exist*(s1,p0))
  exist*.2exist*igen(s0, exist!(s1,p0), exist*(s1,p0))
  exist!.right(s1, p0)
}
```

```follow
thm exist!.exist!.exist(set s0, set s1, prop p0) {
  |- imp(exist!(s0,exist!(s1,p0)),exist(s0,exist(s1,p0)))
} = {
  syl(exist!(s0,exist!(s1,p0)), exist(s0,exist(s1,p0)), exist(s0,exist!(s1,p0)))
  exist!.left(s0, exist!(s1,p0))
  a4igen.aee(s0, exist!(s1,p0), exist(s1,p0))
  exist!.left(s1, p0)
}
```

```follow
thm exist*.exist.swap(set s0, set s1, prop p0) {
  |- imp(forall(s0,exist*(s1,p0)), imp(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
} = {
  rw3(forall(s0,exist*(s1,p0)), exist*(s0,exist(s1,p0)), exist*(s1,exist(s0,p0)), exist*(s1,exist(s0,and(exist(s1,p0),p0))))
  exist*.exist.and(s0, s1, exist(s1,p0), p0)
  nf.exist.1(s1, p0)
  exist*.2exist*igen(s1, exist(s0,p0), exist(s0,and(exist(s1,p0),p0)))
  a4igen.aee(s0, p0, and(exist(s1,p0),p0))
  and.introiid(p0, exist(s1,p0), p0)
  special.1(p0, s1)
  id(p0)
}
```

```follow
thm exist!.exist.swap(set s0, set s1, prop p0) {
  |- imp(forall(s0,exist*(s1,p0)), imp(exist!(s0,exist(s1,p0)),exist!(s1,exist(s0,p0))))
} = {
  rw23(forall(s0,exist*(s1,p0)), exist!(s0,exist(s1,p0)), exist!(s1,exist(s0,p0)), and(exist(s0,exist(s1,p0)),exist*(s0,exist(s1,p0))), and(exist(s1,exist(s0,p0)),exist*(s1,exist(s0,p0))))
  exist!.def.ext(s0, exist(s1,p0))
  exist!.def.ext(s1, exist(s0,p0))
  and.2andiid.1(forall(s0,exist*(s1,p0)), exist(s0,exist(s1,p0)), exist*(s0,exist(s1,p0)), exist(s1,exist(s0,p0)), exist*(s1,exist(s0,p0)))
  a1i(forall(s0,exist*(s1,p0)), imp(exist(s0,exist(s1,p0)),exist(s1,exist(s0,p0))))
  a11.2(p0, s0, s1)
  exist*.exist.swap(s0, s1, p0)
}
```

```follow
thm exist!.exist!.intro(set s0, set s1, prop p0) {
  |- imp(and(exist!(s0,exist(s1,p0)),exist!(s1,exist(s0,p0))),exist!(s0,exist!(s1,p0)))
  |- imp(exist!(s0,exist(s1,p0)), imp(exist!(s1,exist(s0,p0)),exist!(s0,exist!(s1,p0))))
  |- imp(and(exist!(s1,exist(s0,p0)),exist!(s0,exist(s1,p0))),exist!(s0,exist!(s1,p0)))
  |- imp(exist!(s1,exist(s0,p0)), imp(exist!(s0,exist(s1,p0)),exist!(s0,exist!(s1,p0))))
} = {
  and.joini(exist!(s1,exist(s0,p0)), exist!(s0,exist(s1,p0)), exist!(s0,exist!(s1,p0)))
  com12i(exist!(s1,exist(s0,p0)), exist!(s0,exist(s1,p0)), exist!(s0,exist!(s1,p0)))
  and.spliti(exist!(s0,exist(s1,p0)), exist!(s1,exist(s0,p0)), exist!(s0,exist!(s1,p0)))
  syl(and(exist!(s0,exist(s1,p0)),exist!(s1,exist(s0,p0))), exist!(s0,exist!(s1,p0)), and(exist(s0,exist!(s1,p0)),exist*(s0,exist!(s1,p0))))
  exist!.def.ext(s0, exist!(s1,p0))
  syl(and(exist!(s0,exist(s1,p0)),exist!(s1,exist(s0,p0))), and(exist(s0,exist!(s1,p0)),exist*(s0,exist!(s1,p0))), and(exist!(s1,exist(s0,p0)),exist!(s0,exist(s1,p0))))
  and.com(exist!(s0,exist(s1,p0)), exist!(s1,exist(s0,p0)))
  and.2andii.1(exist!(s1,exist(s0,p0)), exist!(s0,exist(s1,p0)), exist(s0,exist!(s1,p0)), exist*(s0,exist!(s1,p0)))
  exist!.exist.com(s1, s0, p0)
  syl(exist!(s0,exist(s1,p0)), exist*(s0,exist!(s1,p0)), exist*(s0,exist(s1,p0)))
  exist!.right(s0, exist(s1,p0))
  exist*.2exist*igen(s0, exist!(s1,p0), exist(s1,p0))
  exist!.left(s1, p0)
}
```

```follow
// at most one of order of pair
thm exist*.andexist*.iff(prop p0, set s0, set s1, set s2, set s3) {
  |- iff(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))),exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  |- iff(exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  |- imp(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))),exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  |- imp(exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  diff (s2, s3, s0, s1) (s2, s3, p0) 
} = {
  iff.split(exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  iff.syl(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist(s2,forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2))))),exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))))
  and.iffandii.1(exist*(s0,exist(s1,p0)), exist(s2,forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2))))), exist*(s1,exist(s0,p0)), exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))))
  exist*.def(s0, exist(s1,p0), s2)
  exist*.def(s1, exist(s0,p0), s3)
  iff.syl(and(exist(s2,forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2))))),exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), exist(s2,and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))))))
  nf.a4.diff.exist.and(s2, exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))), forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))))
  nf.diff(s2, exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))))
  a4igen.aee.iff(s2, and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))), exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))
  iff.syl(and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),exist(s3,forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))), exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))), exist(s3,and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))))
  nf.a4.diff.exist.and(s3, forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))), forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))))
  nf.diff(s3, forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))))
  a4igen.aee.iff(s3, and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))), forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))
  iff.syl(and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))), forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))), forall(s0,forall(s1,and(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s1),c(s3)))))))
  a4igen.aaa.iff(s0, forall(s1,and(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s1),c(s3))))), forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))
  a4igen.aaa.iff(s1, and(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s1),c(s3)))), imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))
  iff.imp.and(p0, eq(c(s0),c(s2)), eq(c(s1),c(s3)))
  iff.syl(and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))), forall(s0,forall(s1,and(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s1),c(s3)))))), forall(s0,and(forall(s1,imp(p0,eq(c(s0),c(s2)))),forall(s1,imp(p0,eq(c(s1),c(s3)))))))
  a4igen.aaa.iff(s0, and(forall(s1,imp(p0,eq(c(s0),c(s2)))),forall(s1,imp(p0,eq(c(s1),c(s3))))), forall(s1,and(imp(p0,eq(c(s0),c(s2))),imp(p0,eq(c(s1),c(s3))))))
  a4.and.3(s1, imp(p0,eq(c(s0),c(s2))), imp(p0,eq(c(s1),c(s3))))
  iff.syl(and(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))),forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3))))), forall(s0,and(forall(s1,imp(p0,eq(c(s0),c(s2)))),forall(s1,imp(p0,eq(c(s1),c(s3)))))), and(forall(s0,forall(s1,imp(p0,eq(c(s0),c(s2))))),forall(s0,forall(s1,imp(p0,eq(c(s1),c(s3)))))))
  a4.and.3(s0, forall(s1,imp(p0,eq(c(s0),c(s2)))), forall(s1,imp(p0,eq(c(s1),c(s3)))))
  and.iffandii.1(forall(s0,imp(exist(s1,p0),eq(c(s0),c(s2)))), forall(s0,forall(s1,imp(p0,eq(c(s0),c(s2))))), forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))), forall(s0,forall(s1,imp(p0,eq(c(s1),c(s3))))))
  a4igen.aaa.iff(s0, imp(exist(s1,p0),eq(c(s0),c(s2))), forall(s1,imp(p0,eq(c(s0),c(s2)))))
  a4.diff.aea.iff(s1, p0, eq(c(s0),c(s2)))
  iff.syl(forall(s1,imp(exist(s0,p0),eq(c(s1),c(s3)))), forall(s0,forall(s1,imp(p0,eq(c(s1),c(s3))))), forall(s1,forall(s0,imp(p0,eq(c(s1),c(s3))))))
  a11.1.iff(imp(p0,eq(c(s1),c(s3))), s1, s0)
  a4igen.aaa.iff(s1, imp(exist(s0,p0),eq(c(s1),c(s3))), forall(s0,imp(p0,eq(c(s1),c(s3)))))
  a4.diff.aea.iff(s0, p0, eq(c(s1),c(s3)))
}
```

```follow
thm exist*.andexist*.subs.1(prop p0, set s0, set s1, set s2, set s3) {
  |- imp(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))),
  forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0, subs(subs(p0, s1, s3), s0, s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  diff (s2, s3, s0, s1) (s2, s3, p0) 
} = {
  nf.a4igen.aea.1(s0, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), forall(s1,forall(s2,forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))
  nf.andii(s0, exist*(s0,exist(s1,p0)), exist*(s1,exist(s0,p0)))
  nf.exist*.1(s0, exist(s1,p0))
  nf.exist*.2(s0, s1, exist(s0,p0))
  nf.exist.1(s0, p0)
  nf.a4igen.aea.1(s1, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), forall(s2,forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))
  nf.andii(s1, exist*(s0,exist(s1,p0)), exist*(s1,exist(s0,p0)))
  nf.exist*.1(s1, exist(s0,p0))
  nf.exist*.2(s1, s0, exist(s1,p0))
  nf.exist.1(s1, p0)
  nf.a4igen.aea.1(s2, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))
  nf.diff(s2, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  nf.a4igen.aea.1(s3, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))
  nf.diff(s3, and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  com12i(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), and(p0,subs(subs(p0,s1,s3),s0,s2)), and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))
  and.2andiid.1(and(p0,subs(subs(p0,s1,s3),s0,s2)), exist*(s0,exist(s1,p0)), exist*(s1,exist(s0,p0)), eq(c(s0),c(s2)), eq(c(s1),c(s3)))
  com12i(and(p0,subs(subs(p0,s1,s3),s0,s2)), exist*(s0,exist(s1,p0)), eq(c(s0),c(s2)))
  syl(exist*(s0,exist(s1,p0)), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s0),c(s2))), forall(s0,forall(s2,imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2))))))
  exist*.def.3(s0, s2, exist(s1,p0))
  nf.diff(s2, exist(s1,p0))
  syl(forall(s0,forall(s2,imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2))))), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s0),c(s2))), forall(s2,imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2)))))
  special.2(forall(s2,imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2)))), s0)
  syl(forall(s2,imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2)))), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s0),c(s2))), imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2))))
  special.2(imp(and(exist(s1,p0),subs(exist(s1,p0),s0,s2)),eq(c(s0),c(s2))), s2)
  transi.2(and(p0,subs(subs(p0,s1,s3),s0,s2)), and(exist(s1,p0),subs(exist(s1,p0),s0,s2)), eq(c(s0),c(s2)))
  and.2andii.1(p0, subs(subs(p0,s1,s3),s0,s2), exist(s1,p0), subs(exist(s1,p0),s0,s2))
  special.1(p0, s1)
  subs.2subsigen(subs(p0,s1,s3), exist(s1,p0), s0, s2)
  subs.exist(p0, s1, s3)
  com12i(and(p0,subs(subs(p0,s1,s3),s0,s2)), exist*(s1,exist(s0,p0)), eq(c(s1),c(s3)))
  syl(exist*(s1,exist(s0,p0)), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s1),c(s3))), forall(s1,forall(s3,imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3))))))
  exist*.def.3(s1, s3, exist(s0,p0))
  nf.diff(s3, exist(s0,p0))
  syl(forall(s1,forall(s3,imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3))))), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s1),c(s3))), forall(s3,imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3)))))
  special.2(forall(s3,imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3)))), s1)
  syl(forall(s3,imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3)))), imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),eq(c(s1),c(s3))), imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3))))
  special.2(imp(and(exist(s0,p0),subs(exist(s0,p0),s1,s3)),eq(c(s1),c(s3))), s3)
  transi.2(and(p0,subs(subs(p0,s1,s3),s0,s2)), and(exist(s0,p0),subs(exist(s0,p0),s1,s3)), eq(c(s1),c(s3)))
  and.2andii.1(p0, subs(subs(p0,s1,s3),s0,s2), exist(s0,p0), subs(exist(s0,p0),s1,s3))
  special.1(p0, s0)
  syl(subs(subs(p0,s1,s3),s0,s2), subs(exist(s0,p0),s1,s3), subs(subs(p0,s0,s2),s1,s3))
  subs.com(p0, s1, s3, s0, s2)
  subs.2subsigen(subs(p0,s0,s2), exist(s0,p0), s1, s3)
  subs.exist(p0, s0, s2)
}
```

```follow
thm forall.com4(prop p0, set s0, set s1, set s2, set s3) {
  |- imp(forall(s0,forall(s1,forall(s2,forall(s3,p0)))), forall(s2,forall(s3,forall(s0,forall(s1,p0)))))
} = {
  nf.a4igen.aea.1(s2, forall(s0,forall(s1,forall(s2,forall(s3,p0)))), forall(s3,forall(s0,forall(s1,p0))))
  nf.forall.2(forall(s1,forall(s2,forall(s3,p0))), s2, s0)
  nf.forall.2(forall(s2,forall(s3,p0)), s2, s1)
  nf.forall.1(s2, forall(s3,p0))
  nf.a4igen.aea.1(s3, forall(s0,forall(s1,forall(s2,forall(s3,p0)))), forall(s0,forall(s1,p0)))
  nf.forall.2(forall(s1,forall(s2,forall(s3,p0))), s3, s0)
  nf.forall.2(forall(s2,forall(s3,p0)), s3, s1)
  nf.forall.2(forall(s3,p0), s3, s2)
  nf.forall.1(s3, p0)
  a4igen.aaa(s0, forall(s1,forall(s2,forall(s3,p0))), forall(s1,p0))
  a4igen.aaa(s1, forall(s2,forall(s3,p0)), p0)
  syl(forall(s2,forall(s3,p0)), p0, forall(s3,p0))
  special.2(forall(s3,p0), s2)
  special.2(p0, s3)
}
```

```follow
thm exist.exist.subs(prop p0, set s0, set s1, set s2, set s3) {
  |- iff(exist(s0, exist(s1, p0)),exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))))
  |- iff(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s0, exist(s1, p0)))
  |- imp(exist(s0, exist(s1, p0)),exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))))
  |- imp(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s0, exist(s1, p0)))
  -| nf(s2, p0)
  -| nf(s3, p0)
  diff (s0, s2, s3) (s1, s2, s3) 
} = {
  iff.split(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s0,exist(s1,p0)))
  iff.syl(exist(s0,exist(s1,p0)), exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s0,exist(s3,subs(p0,s1,s3))))
  a4igen.aee.iff(s0, exist(s1,p0), exist(s3,subs(p0,s1,s3)))
  exist.subs.1(s1, s3, p0)
  iff.syl(exist(s0,exist(s3,subs(p0,s1,s3))), exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s3,exist(s0,subs(p0,s1,s3))))
  a11.2(subs(p0,s1,s3), s0, s3)
  iff.syl(exist(s3,exist(s0,subs(p0,s1,s3))), exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s3,exist(s2,subs(subs(p0,s1,s3),s0,s2))))
  a11.2(subs(subs(p0,s1,s3),s0,s2), s3, s2)
  a4igen.aee.iff(s3, exist(s0,subs(p0,s1,s3)), exist(s2,subs(subs(p0,s1,s3),s0,s2)))
  exist.subs.1(s0, s2, subs(p0,s1,s3))
  nf.subs.2(p0, s1, s3, s2)
}
```

```follow
thm not.exist.exist.subs(prop p0, set s0, set s1, set s2, set s3) {
  |- iff(not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))), forall(s0, forall(s1, not(p0))))
  |- iff(forall(s0, forall(s1, not(p0))), not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))))
  |- imp(not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))), forall(s0, forall(s1, not(p0))))
  |- imp(forall(s0, forall(s1, not(p0))), not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))))
  -| nf(s2, p0)
  -| nf(s3, p0)
  diff (s0, s2, s3) (s1, s2, s3) 
} = {
  iff.split(forall(s0,forall(s1,not(p0))), not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))))
  iff.syl(not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))), forall(s0,forall(s1,not(p0))), not(exist(s0,exist(s1,p0))))
  iff.coni.4(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), exist(s0,exist(s1,p0)))
  exist.exist.subs(p0, s0, s1, s2, s3)
  iff.syl(not(exist(s0,exist(s1,p0))), forall(s0,forall(s1,not(p0))), forall(s0,not(exist(s1,p0))))
  exist.def.3(s0, exist(s1,p0))
  a4igen.aaa.iff(s0, not(exist(s1,p0)), forall(s1,not(p0)))
  exist.def.3(s1, p0)
}
```

```follow
thm exist*.andexist*.subs.2(prop p0, set s0, set s1, set s2, set s3) {
  |- imp(forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0, subs(subs(p0, s1, s3), s0, s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))))
  diff (s2, s3, s0, s1) (s2, s3, p0) 
} = {
  syl(forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  exist*.andexist*.iff(p0, s0, s1, s2, s3)
  syl(forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  forall.com4(imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))), s0, s1, s2, s3)
  contii.4(imp(forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))),exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))), exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))))
  com12i(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2))), forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  a4id.aee(s2, exist(s3,subs(subs(p0,s1,s3),s0,s2)), exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))), forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  a4igen.aaa(s2, forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))), imp(exist(s3,subs(subs(p0,s1,s3),s0,s2)),exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  a4id.aee(s3, subs(subs(p0,s1,s3),s0,s2), forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))), forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))
  a4igen.aaa(s3, forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))), imp(subs(subs(p0,s1,s3),s0,s2),forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))
  com12i(forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))), subs(subs(p0,s1,s3),s0,s2), forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))
  a4id.aaa(s0, forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))), forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))), subs(subs(p0,s1,s3),s0,s2))
  nf.a4igen.aea.1(s0, subs(subs(p0,s1,s3),s0,s2), imp(forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))),forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))
  a4id.aaa(s1, imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))), imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))), subs(subs(p0,s1,s3),s0,s2))
  nf.a4igen.aea.1(s1, subs(subs(p0,s1,s3),s0,s2), imp(imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))),imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))
  transid.2(p0, and(p0,subs(subs(p0,s1,s3),s0,s2)), and(eq(c(s0),c(s2)),eq(c(s1),c(s3))), subs(subs(p0,s1,s3),s0,s2))
  and.intro(p0, subs(subs(p0,s1,s3),s0,s2))
  nf.subs.1(subs(p0,s1,s3), s0, s2)
  nf.subs.2(subs(p0,s1,s3), s0, s2, s1)
  nf.subs.1(p0, s1, s3)
  syl(not(exist(s2,exist(s3,subs(subs(p0,s1,s3),s0,s2)))), imp(forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))),exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))))), forall(s0,forall(s1,not(p0))))
  not.exist.exist.subs(p0, s0, s1, s2, s3)
  nf.diff(s2, p0)
  nf.diff(s3, p0)
  a1id(forall(s0,forall(s1,not(p0))), forall(s2,forall(s3,forall(s0,forall(s1,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))), exist(s2,exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  a4igen.diff.aee.2(s2, exist(s3,forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))), forall(s0,forall(s1,not(p0))))
  a4igen.diff.aee.2(s3, forall(s0,forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))), forall(s0,forall(s1,not(p0))))
  a4igen.aaa(s0, forall(s1,not(p0)), forall(s1,imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))))
  a4igen.aaa(s1, not(p0), imp(p0,and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))
  cont.1(p0, and(eq(c(s0),c(s2)),eq(c(s1),c(s3))))
}
```

```follow
thm exist*.andexist*.subs(prop p0, set s0, set s1, set s2, set s3) {
  |- iff(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))),
  forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0, subs(subs(p0, s1, s3), s0, s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  diff (s2, s3, s0, s1) (s2, s3, p0) 
} = {
  iff.introii.1(and(exist*(s0,exist(s1,p0)),exist*(s1,exist(s0,p0))), forall(s0,forall(s1,forall(s2,forall(s3,imp(and(p0,subs(subs(p0,s1,s3),s0,s2)),and(eq(c(s0),c(s2)),eq(c(s1),c(s3)))))))))
  exist*.andexist*.subs.1(p0, s0, s1, s2, s3)
  exist*.andexist*.subs.2(p0, s0, s1, s2, s3)
}
```

```follow
// 2eu1
```