
# 量化否定 `a10` (Quantified Negation)

```follow
axiom a10.1(set s0, prop p0) {
  |- imp(not(forall(s0,p0)), forall(s0, not(forall(s0, p0))))
}
```

## 超出`follow`语言的证明  

这个公理在 `follow` 中不可证明，在一阶逻辑中可以通过前面的公理证明出来。
这里做一个说明。

首先我们证明一个弱化的版本。这个版本增加了一个假设和三个条件。
实际上是凭空引入了一个新的变量 `s1` 和 `p1`。

这里我想需要跳出`follow`语言本身，从自然语言角度继续证明。这里我们可以带入 `s1 = hs0` 以及 `p1 = p0[s0:hs0]`（语法层面上的替换）。 从结果上看，`s1 = hs0 => p0 <-> p0[s0:hs0]` 和 `diff (s0, hs0) (s0, p0[s0:hs0]) (hs0, p0)` 总是可以成立的。

因为 `p0[s0:hs0]` 在 `follow` 语言内部以语义逻辑的形式出现，而没有在语法层面进行实现，所以在 `follow` 语言中不能直接从 `a10.weak` 推导到 `a10`，只能在 `p0` 确定下来之后，根据 `p0` 的具体形式才能证明。但是，`a10.weak` 会使证明过分麻烦，并且没有必要。我们还是要避免洁癖，以方便为主。所以将 `a10` 独立为公理。

```follow
thm a10.weak(set s0, prop p0, set s1, prop p1) {
  |- imp(not(forall(s0,p0)), forall(s0, not(forall(s0, p0))))
  -| imp(eq(c(s0), c(s1)), iff(p0, p1))
  diff (s0, s1) (s0, p1) (s1, p0)
} = {
  a5.rw(s0, not(forall(s0,p0)), not(forall(s1,p1)))
  mp(iff(not(forall(s0,p0)),not(forall(s1,p1))), iff(forall(s0,p0),forall(s1,p1)))
  iff.con(forall(s0,p0), forall(s1,p1))
  eq.forall.6(p0, s0, p1, s1)
  a5.forall.1(s0, not(forall(s1,p1)))
}
```

## `a10` 的其他形式

```follow
thm a10.2(set s0, prop p0) {
  |- imp(exist(s0,p0), forall(s0, exist(s0, p0)))
} = {
  syl(exist(s0,p0), forall(s0,exist(s0,p0)), not(forall(s0,not(p0))))
  exist.def.2(s0, p0)
  syl(not(forall(s0,not(p0))), forall(s0,exist(s0,p0)), forall(s0,not(forall(s0,not(p0)))))
  a10.1(s0, not(p0))
  a4igen.aaa(s0, not(forall(s0,not(p0))), exist(s0,p0))
  exist.def.2(s0, p0)
}
```


```follow
thm a10.3(set s0, prop p0) {
  |- imp(exist(s0,forall(s0,p0)), forall(s0, p0))
} = {
  syl(exist(s0,forall(s0,p0)), forall(s0,p0), not(forall(s0,not(forall(s0,p0)))))
  exist.def.2(s0, forall(s0,p0))
  coni.1(forall(s0,not(forall(s0,p0))), forall(s0,p0))
  a10.1(s0, p0)
}
```

## `notfree` 

```follow
thm nf.intro.2(set s0, prop p0) {
  |- imp(forall(s0, imp(p0, forall(s0, p0))), nf(s0,p0))
} = {
  syl(forall(s0,imp(p0,forall(s0,p0))), nf(s0,p0), imp(exist(s0,p0),forall(s0,p0)))
  nf.def.1.ext(s0, p0)
  rw3(forall(s0,imp(p0,forall(s0,p0))), exist(s0,p0), forall(s0,p0), exist(s0,forall(s0,p0)))
  a4.aee(s0, p0, forall(s0,p0))
  a10.3(s0, p0)
}
```

```follow
thm nf.exist.1(set s0, prop p0) {
  |- nf(s0, exist(s0, p0))
} = {
  mp(nf(s0,exist(s0,p0)), forall(s0,imp(exist(s0,p0),forall(s0,exist(s0,p0)))))
  nf.intro.2(s0, exist(s0,p0))
  gen.forall(s0, imp(exist(s0,p0),forall(s0,exist(s0,p0))))
  a10.2(s0, p0)
}
```

```follow
thm nf.forall.1(set s0, prop p0) {
  |- nf(s0, forall(s0, p0))
} = {
  nf.rw(s0, forall(s0,p0), not(exist(s0,not(p0))))
  exist.def.4(s0, p0)
  nf.noti(s0, exist(s0,not(p0)))
  nf.exist.1(s0, not(p0))
}
```

```follow
thm nf.nf.1(set s0, prop p0) {
  |- nf(s0, nf(s0, p0))
} = {
  nf.rw(s0, nf(s0,p0), imp(exist(s0,p0),forall(s0,p0)))
  nf.def.1.ext(s0, p0)
  nf.impii(s0, exist(s0,p0), forall(s0,p0))
  nf.exist.1(s0, p0)
  nf.forall.1(s0, p0)
}
```
