
# 条件量词 

条件量词是在数学命题中经常使用的复合命题，它们的目的是简化叙事。

```follow
// condition forall
term prop cforall(set s0, class c0, prop p0) { ∀(s0 ∈ c0, p0) }
// condition exist
term prop cexist(set s0, class c0, prop p0) { ∃(s0 ∈ c0, p0) }
// condition exist!
term prop cexist!(set s0, class c0, prop p0) { ∃!(s0 ∈ c0, p0) }
// condition exist*
term prop cexist*(set s0, class c0, prop p0) { ∃*(s0 ∈ c0, p0) }
// condition cab
term class ccab(set s0, class c0, prop p0) { { s0 ∈ c0 | p0 } }
```

```follow
axiom cforall.def(set s0, class c0, prop p0) {
  |- iff(cforall(s0,c0,p0), forall(s0, imp(in(c(s0),c0), p0)))
}
axiom cexist.def(set s0, class c0, prop p0) {
  |- iff(cexist(s0,c0,p0), exist(s0, and(in(c(s0),c0), p0)))
}
axiom cexist!.def(set s0, class c0, prop p0) {
  |- iff(cexist!(s0,c0,p0), exist!(s0, and(in(c(s0),c0), p0)))
}
axiom cexist*.def(set s0, class c0, prop p0) {
  |- iff(cexist*(s0,c0,p0), exist*(s0, and(in(c(s0),c0), p0)))
}
axiom ccab.def(set s0, class c0, prop p0) {
  |- eq(ccab(s0,c0,p0), cab(s0, and(in(c(s0),c0), p0)))
}
```

```follow
thm cforall.def.ext(set s0, class c0, prop p0) {
  |- iff(forall(s0, imp(in(c(s0),c0), p0)), cforall(s0,c0,p0))
  |- imp(cforall(s0,c0,p0), forall(s0, imp(in(c(s0),c0), p0)))
  |- imp(forall(s0, imp(in(c(s0),c0), p0)), cforall(s0,c0,p0))
} = {
  iff.split(forall(s0, imp(in(c(s0),c0), p0)), cforall(s0,c0,p0))
  cforall.def(s0,c0,p0)
}
thm cexist.def.ext(set s0, class c0, prop p0) {
  |- iff(exist(s0, and(in(c(s0),c0), p0)), cexist(s0,c0,p0))
  |- imp(cexist(s0,c0,p0), exist(s0, and(in(c(s0),c0), p0)))
  |- imp(exist(s0, and(in(c(s0),c0), p0)), cexist(s0,c0,p0))
} = {
  iff.split(exist(s0,and(in(c(s0),c0),p0)), cexist(s0,c0,p0))
  cexist.def(s0,c0,p0)
}
thm cexist!.def.ext(set s0, class c0, prop p0) {
  |- iff(exist!(s0, and(in(c(s0),c0), p0)), cexist!(s0,c0,p0))
  |- imp(cexist!(s0,c0,p0), exist!(s0, and(in(c(s0),c0), p0)))
  |- imp(exist!(s0, and(in(c(s0),c0), p0)), cexist!(s0,c0,p0))
} = {
  iff.split(exist!(s0,and(in(c(s0),c0),p0)), cexist!(s0,c0,p0))
  cexist!.def(s0,c0,p0)
}
thm cexist*.def.ext(set s0, class c0, prop p0) {
  |- iff(exist*(s0, and(in(c(s0),c0), p0)), cexist*(s0,c0,p0))
  |- imp(cexist*(s0,c0,p0), exist*(s0, and(in(c(s0),c0), p0)))
  |- imp(exist*(s0, and(in(c(s0),c0), p0)), cexist*(s0,c0,p0))
} = {
  iff.split(exist*(s0,and(in(c(s0),c0),p0)), cexist*(s0,c0,p0))
  cexist*.def(s0,c0,p0)
}
```

## 条件量词的性质-cforall

```follow
thm cforall.gen(set s0, class c0, prop p0) {
  |- cforall(s0, c0, p0)
  -| imp(in(c(s0),c0), p0)
} = {
  mp(cforall(s0,c0,p0), forall(s0,imp(in(c(s0),c0),p0)))
  cforall.def.ext(s0, c0, p0)
  gen.forall(s0, imp(in(c(s0),c0),p0))
}
```

```follow
thm cforall.id(set s0, class c0, prop p0) {
  |- cforall(s0, c0, in(c(s0),c0))
} = {
  cforall.gen(s0, c0, in(c(s0),c0))
  id(in(c(s0),c0))
}
```

```follow
thm cforall.gen.a1i(set s0, class c0, prop p0) {
  |- cforall(s0, c0, p0)
  -| p0
} = {
  cforall.gen(s0, c0, p0)
  a1i(in(c(s0),c0), p0)
}
```

```follow
thm cforall.gend(set s0, class c0, prop p0, prop p1) {
  |- imp(p1, cforall(s0, c0, p0))
  -| imp(p1, imp(in(c(s0),c0),p0))
  -| nf(s0, p1)
} = {
  syl(p1, cforall(s0,c0,p0), forall(s0,imp(in(c(s0),c0),p0)))
  cforall.def.ext(s0, c0, p0)
  nf.gend(s0, imp(in(c(s0),c0),p0), p1)
}
```

```follow
thm cforall.special.rw(set s0, class c0, prop p0, prop p1) {
  |- p0
  -| imp(cforall(s0, c0, p1), p0)
  -| imp(in(c(s0),c0), p1)
} = {
  mp(p0, cforall(s0,c0,p1))
  cforall.gen(s0, c0, p1)
}
```

```follow 
thm cforall.from.forall(set s0, class c0, prop p0, prop p1) {
  |- imp(forall(s0, p0), cforall(s0, c0, p0))
} = {
  syl(forall(s0,p0), cforall(s0,c0,p0), forall(s0,imp(in(c(s0),c0),p0)))
  cforall.def.ext(s0, c0, p0)
  a4igen.aaa(s0, p0, imp(in(c(s0),c0),p0))
  a1(p0, in(c(s0),c0))
}
```

```follow
thm cforall.not(set s0, class c0, prop p0) {
  |- iff(cforall(s0, c0, not(p0)), forall(s0, not(and(in(c(s0),c0), p0))))
} = {
  iff.syl(cforall(s0,c0,not(p0)), forall(s0,not(and(in(c(s0),c0),p0))), forall(s0,imp(in(c(s0),c0),not(p0))))
  cforall.def(s0, c0, not(p0))
  a4igen.aaa.iff(s0, imp(in(c(s0),c0),not(p0)), not(and(in(c(s0),c0),p0)))
  iff.coni.2(imp(in(c(s0),c0),not(p0)), and(in(c(s0),c0),p0))
  iff.and.def(in(c(s0),c0), p0)
}
```

```follow
thm cforall.2cforall(set s0, class c0, prop p0, prop p1) {
  |- imp(cforall(s0,c0,imp(p0, p1)), imp(cforall(s0,c0,p0), cforall(s0,c0,p1)))
} = {
  rw23(cforall(s0,c0,imp(p0,p1)), cforall(s0,c0,p0), cforall(s0,c0,p1), forall(s0,imp(in(c(s0),c0),p0)), forall(s0,imp(in(c(s0),c0),p1)))
  cforall.def.ext(s0, c0, p0)
  cforall.def.ext(s0, c0, p1)
  a4id.aaa(s0, imp(in(c(s0),c0),p0), imp(in(c(s0),c0),p1), cforall(s0,c0,imp(p0,p1)))
  syl(cforall(s0,c0,imp(p0,p1)), forall(s0,imp(imp(in(c(s0),c0),p0),imp(in(c(s0),c0),p1))), forall(s0,imp(in(c(s0),c0),imp(p0,p1))))
  cforall.def.ext(s0, c0, imp(p0,p1))
  a4igen.aaa(s0, imp(in(c(s0),c0),imp(p0,p1)), imp(imp(in(c(s0),c0),p0),imp(in(c(s0),c0),p1)))
  a2(in(c(s0),c0), p0, p1)
}
```

```follow
thm cforall.2cforalli(set s0, class c0, prop p0, prop p1) {
  |- imp(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| cforall(s0,c0,imp(p0, p1)) 
} = {
  mp(imp(cforall(s0,c0,p0),cforall(s0,c0,p1)), cforall(s0,c0,imp(p0,p1)))
  cforall.2cforall(s0, c0, p0, p1)
}
```

```follow
thm cforall.2cforalligen(set s0, class c0, prop p0, prop p1) {
  |- imp(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| imp(in(c(s0),c0),imp(p0, p1))
} = {
  cforall.2cforalli(s0, c0, p0, p1)
  cforall.gen(s0, c0, imp(p0,p1))
}
```

```follow
thm cforall.2cforalligen.a1i(set s0, class c0, prop p0, prop p1) {
  |- imp(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| imp(p0, p1)
} = {
  cforall.2cforalligen(s0, c0, p0, p1)
  a1i(in(c(s0),c0), imp(p0,p1))
}
```


```follow
thm cforall.2cforallid(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, imp(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, cforall(s0,c0,imp(p0, p1)))
} = {
  syl(p2, imp(cforall(s0,c0,p0),cforall(s0,c0,p1)), cforall(s0,c0,imp(p0,p1)))
  cforall.2cforall(s0, c0, p0, p1)
}
```

```follow
thm cforall.2cforallidgen(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, imp(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, imp(in(c(s0),c0),imp(p0, p1)))
  -| nf(s0, p2)
} = {
  cforall.2cforallid(s0, c0, p0, p1, p2)
  cforall.gend(s0, c0, imp(p0,p1), p2)
}
```

```follow
thm cforall.2cforallidgen.a1i(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, imp(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, imp(p0, p1))
  -| nf(s0, p2)
} = {
  cforall.2cforallidgen(s0, c0, p0, p1, p2)
  a1id(p2, in(c(s0),c0), imp(p0,p1))
}
```


```follow
thm cforall.iffcforall(set s0, class c0, prop p0, prop p1) {
  |- imp(cforall(s0,c0,iff(p0, p1)), iff(cforall(s0,c0,p0), cforall(s0,c0,p1)))
} = {
  iff.introiid.1(cforall(s0,c0,iff(p0,p1)), cforall(s0,c0,p0), cforall(s0,c0,p1))
  cforall.2cforallid(s0, c0, p0, p1, cforall(s0,c0,iff(p0,p1)))
  cforall.2cforalligen.a1i(s0, c0, iff(p0,p1), imp(p0,p1))
  iff.left(p0, p1)
  cforall.2cforallid(s0, c0, p1, p0, cforall(s0,c0,iff(p0,p1)))
  cforall.2cforalligen.a1i(s0, c0, iff(p0,p1), imp(p1,p0))
  iff.right(p0, p1)
}
```

```follow
thm cforall.iffcforalli(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| cforall(s0,c0,iff(p0, p1)) 
} = {
  mp(iff(cforall(s0,c0,p0),cforall(s0,c0,p1)), cforall(s0,c0,iff(p0,p1)))
  cforall.iffcforall(s0, c0, p0, p1)
}
```

```follow
thm cforall.iffcforalligen(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| imp(in(c(s0),c0),iff(p0, p1)) 
} = {
  cforall.iffcforalli(s0, c0, p0, p1)
  cforall.gen(s0, c0, iff(p0,p1))
}
```

```follow
thm cforall.iffcforalligen.a1i(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,p0), cforall(s0,c0,p1))
  -| iff(p0, p1) 
} = {
  cforall.iffcforalligen(s0, c0, p0, p1)
  a1i(in(c(s0),c0), iff(p0,p1))
}
```

```follow
thm cforall.iffcforallid(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, cforall(s0,c0,iff(p0, p1)))
} = {
  syl(p2, iff(cforall(s0,c0,p0),cforall(s0,c0,p1)), cforall(s0,c0,iff(p0,p1)))
  cforall.iffcforall(s0, c0, p0, p1)
}
```

```follow
thm cforall.iffcforallidgen(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, imp(in(c(s0),c0),iff(p0, p1)))
  -| nf(s0, p2)
} = {
  cforall.iffcforallid(s0, c0, p0, p1, p2)
  cforall.gend(s0, c0, iff(p0,p1), p2)
}
```

```follow
thm cforall.iffcforallidgen.a1i(set s0, class c0, prop p0, prop p1, prop p2) {
  |- imp(p2, iff(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  -| imp(p2, iff(p0, p1))
  -| nf(s0, p2)
} = {
  cforall.iffcforallidgen(s0, c0, p0, p1, p2)
  a1id(p2, in(c(s0),c0), iff(p0,p1))
}
```

```follow
thm cforall.andcforall(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,and(p0, p1)), and(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  |- iff(and(cforall(s0,c0,p0), cforall(s0,c0,p1)), cforall(s0,c0,and(p0, p1)))
  |- imp(cforall(s0,c0,and(p0, p1)), and(cforall(s0,c0,p0), cforall(s0,c0,p1)))
  |- imp(and(cforall(s0,c0,p0), cforall(s0,c0,p1)), cforall(s0,c0,and(p0, p1)))
} = {
  iff.split(and(cforall(s0,c0,p0),cforall(s0,c0,p1)), cforall(s0,c0,and(p0,p1)))
  iff.introii.1(cforall(s0,c0,and(p0,p1)), and(cforall(s0,c0,p0),cforall(s0,c0,p1)))
  and.introiid(cforall(s0,c0,and(p0,p1)), cforall(s0,c0,p0), cforall(s0,c0,p1))
  cforall.2cforalligen.a1i(s0, c0, and(p0,p1), p0)
  and.left(p0, p1)
  cforall.2cforalligen.a1i(s0, c0, and(p0,p1), p1)
  and.right(p0, p1)
  and.joini(cforall(s0,c0,p0), cforall(s0,c0,p1), cforall(s0,c0,and(p0,p1)))
  cforall.2cforallid(s0, c0, p1, and(p0,p1), cforall(s0,c0,p0))
  cforall.2cforalligen.a1i(s0, c0, p0, imp(p1,and(p0,p1)))
  and.intro(p0, p1)
}
```

```follow
thm cforall.iff(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,iff(p0, p1)), and(cforall(s0,c0,imp(p0,p1)), cforall(s0,c0,imp(p1,p0))))
  |- iff(and(cforall(s0,c0,imp(p0,p1)), cforall(s0,c0,imp(p1,p0))), cforall(s0,c0,iff(p0, p1)))
  |- imp(cforall(s0,c0,iff(p0, p1)), and(cforall(s0,c0,imp(p0,p1)), cforall(s0,c0,imp(p1,p0))))
  |- imp(and(cforall(s0,c0,imp(p0,p1)), cforall(s0,c0,imp(p1,p0))), cforall(s0,c0,iff(p0, p1)))
} = {
  iff.split(and(cforall(s0,c0,imp(p0,p1)),cforall(s0,c0,imp(p1,p0))), cforall(s0,c0,iff(p0,p1)))
  iff.syl(cforall(s0,c0,iff(p0,p1)), and(cforall(s0,c0,imp(p0,p1)),cforall(s0,c0,imp(p1,p0))), cforall(s0,c0,and(imp(p0,p1),imp(p1,p0))))
  cforall.andcforall(s0, c0, imp(p0,p1), imp(p1,p0))
  cforall.iffcforalligen.a1i(s0, c0, iff(p0,p1), and(imp(p0,p1),imp(p1,p0)))
  iff.def.iff(p0, p1)
}
```

```follow
thm cforall.nf.a5(set s0, class c0, prop p0) {
  |- imp(p0, cforall(s0,c0,p0))
  -| nf(s0, p0)
} = {
  syl(p0, cforall(s0,c0,p0), forall(s0,imp(in(c(s0),c0),p0)))
  cforall.def.ext(s0, c0, p0)
  nf.gend(s0, imp(in(c(s0),c0),p0), p0)
  a1(p0, in(c(s0),c0))
}
```

```follow
thm cforall.a4.nf.imp(set s0, class c0, prop p0, prop p1) {
  |- iff(cforall(s0,c0,imp(p0,p1)), imp(p0, cforall(s0,c0,p1)))
  |- iff(imp(p0, cforall(s0,c0,p1)), cforall(s0,c0,imp(p0,p1)))
  |- imp(cforall(s0,c0,imp(p0,p1)), imp(p0, cforall(s0,c0,p1)))
  |- imp(imp(p0, cforall(s0,c0,p1)), cforall(s0,c0,imp(p0,p1)))
  -| nf(s0, p0)
} = {
  iff.split(imp(p0,cforall(s0,c0,p1)), cforall(s0,c0,imp(p0,p1)))
  iff.syl(cforall(s0,c0,imp(p0,p1)), imp(p0,cforall(s0,c0,p1)), forall(s0,imp(in(c(s0),c0),imp(p0,p1))))
  cforall.def(s0, c0, imp(p0,p1))
  iff.syl(forall(s0,imp(in(c(s0),c0),imp(p0,p1))), imp(p0,cforall(s0,c0,p1)), imp(p0,forall(s0,imp(in(c(s0),c0),p1))))
  imp.iffimpi.4(p0, forall(s0,imp(in(c(s0),c0),p1)), cforall(s0,c0,p1))
  cforall.def.ext(s0, c0, p1)
  iff.syl(forall(s0,imp(in(c(s0),c0),imp(p0,p1))), imp(p0,forall(s0,imp(in(c(s0),c0),p1))), forall(s0,imp(p0,imp(in(c(s0),c0),p1))))
  nf.a4.aea.diff(s0, p0, imp(in(c(s0),c0),p1))
  a4igen.aaa.iff(s0, imp(in(c(s0),c0),imp(p0,p1)), imp(p0,imp(in(c(s0),c0),p1)))
  iff.com12(in(c(s0),c0), p0, p1)
}
```




