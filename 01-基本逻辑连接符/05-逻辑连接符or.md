
# 逻辑连接符 `or`

这一章节我们终于补全了三个符合我们习惯的逻辑操作符：与、或、非。

```follow
term prop or(prop p0, prop p1) { (p0 ∨ p1) }
```

```follow
axiom or.def.1(prop p0, prop p1) {
  |- imp(or(p0,p1), imp(not(p0),p1))
  |- imp(imp(not(p0),p1), or(p0,p1))
}
```

## 真值表 

| 名字 | 第一组值 | 第二组值 | 第三组值 | 第四组值 | 
| :---: | :---: | :---: | :---: | :---: |
| `p0` | 0 | 0 | 1 | 1 | 
| `¬p0` | 1 | 1 | 0 | 0 | 
| `p1` | 0 | 1 | 0 | 1 |
| `¬p0→p1` | 0 | 1 | 1 | 1 | 

## 引入 `Introduction` 

```follow
thm or.left(prop p0, prop p1) {
  |- imp(p0, or(p0, p1))
} = {
  syl(p0, or(p0,p1), imp(not(p0),p1))
  or.def.1(p0, p1)
  cont.1(p0, p1)
}
```

```follow
thm or.lefti(prop p0, prop p1) {
  |- or(p0, p1)
  -| p0
} = {
  mp(or(p0,p1), p0)
  or.left(p0, p1)
}
```

```follow
thm or.leftid(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, p1)
} = {
  syl(p0, or(p1,p2), p1)
  or.left(p1, p2)
}
```

```follow
thm or.right(prop p0, prop p1) {
  |- imp(p0, or(p1, p0))
} = {
  syl(p0, or(p1,p0), imp(not(p1),p0))
  or.def.1(p1, p0)
  a1(p0, not(p1))
}
```

```follow
thm or.righti(prop p0, prop p1) {
  |- or(p0, p1)
  -| p1
} = {
  mp(or(p0,p1), p1)
  or.right(p1, p0)
}
```

```follow
thm or.rightd(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, p2)
} = {
  syl(p0, or(p1,p2), p2)
  or.right(p2, p1)
}
```

## 交换律

```follow
thm or.com(prop p0, prop p1) {
  |- imp(or(p0,p1), or(p1,p0))
} = {
  syl(or(p0,p1), or(p1,p0), imp(not(p0),p1))
  or.def.1(p0, p1)
  syl(imp(not(p0),p1), or(p1,p0), imp(not(p1),p0))
  or.def.1(p1, p0)
  con.1(p0, p1)
}
```

```follow
thm or.comi(prop p0, prop p1) {
  |- or(p0,p1)
  -| or(p1,p0)
} = {
  mp(or(p0,p1), or(p1,p0))
  or.com(p1, p0)
}
```

```follow
thm or.comid(prop p0, prop p1, prop p2) {
  |- imp(p0, or(p1,p2))
  -| imp(p0, or(p2, p1))
} = {
  syl(p0, or(p1,p2), or(p2,p1))
  or.com(p2, p1)
}
```

## 分类讨论 `or.cases`，相当于消去or

```follow
thm or.cases(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p2),imp(imp(p1,p2),imp(or(p0,p1),p2)))
  |- imp(imp(p1,p2),imp(imp(p0,p2),imp(or(p0,p1),p2)))
} = {
  com12i(imp(p1,p2), imp(p0,p2), imp(or(p0,p1),p2))
  and.spliti(imp(p0,p2), imp(p1,p2), imp(or(p0,p1),p2))
  rw2(and(imp(p0,p2),imp(p1,p2)), or(p0,p1), p2, imp(not(p0),p1))
  or.def.1(p0, p1)
  and.joini(imp(p0,p2), imp(p1,p2), imp(imp(not(p0),p1),p2))
  com12i(imp(p0,p2), imp(p1,p2), imp(imp(not(p0),p1),p2))
  com12id(imp(p1,p2), imp(p0,p2), imp(not(p0),p1), p2)
  rw3(imp(p1,p2), imp(not(p0),p1), imp(imp(p0,p2),p2), imp(not(p0),p2))
  trans.1(not(p0), p1, p2)
  cont.4(p0, p2)
}
```

```follow
thm or.casesii(prop p0, prop p1, prop p2) {
  |- imp(or(p0,p1),p2)
  -| imp(p0,p2)
  -| imp(p1,p2)
} = {
  mp(imp(or(p0,p1),p2), imp(p0,p2))
  mp(imp(imp(p0,p2),imp(or(p0,p1),p2)), imp(p1,p2))
  or.cases(p0, p1, p2)
}
```

```follow
thm or.casesiid(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, imp(or(p1,p2),p3))
  -| imp(p0, imp(p1,p3))
  -| imp(p0, imp(p2,p3))
} = {
  mpd(p0, imp(or(p1,p2),p3), imp(p1,p3))
  syl(p0, imp(imp(p1,p3),imp(or(p1,p2),p3)), imp(p2,p3))
  or.cases(p1, p2, p3)
}
```

## 替换某一个命题, `rewrite`

```follow
thm or.rw2(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p3, p2))
  -| imp(p3, p1)
} = {
  syl(p0, or(p1,p2), or(p3,p2))
  or.casesii(p3, p2, or(p1,p2))
  syl(p3, or(p1,p2), p1)
  or.left(p1, p2)
  or.right(p2, p1)
}
```

```follow
thm or.rw3(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p1, p3))
  -| imp(p3, p2)
} = {
  syl(p0, or(p1,p2), or(p1,p3))
  or.casesii(p1, p3, or(p1,p2))
  or.left(p1, p2)
  syl(p3, or(p1,p2), p2)
  or.right(p2, p1)
}
```

```follow
thm or.rw23(prop p0, prop p1, prop p2, prop p3, prop p4) {
  |- imp(p0, or(p1, p2))
  -| imp(p0, or(p3, p4))
  -| imp(p3, p1)
  -| imp(p4, p2)
} = {
  or.rw2(p0, p1, p2, p3)
  or.rw3(p0, p3, p2, p4)
}
```

## 复合关系，`or.2or`

```follow
thm or.2or(prop p0, prop p1, prop p2, prop p3) {
  |- imp(imp(p0,p1),imp(imp(p2,p3), imp(or(p0,p2),or(p1,p3))))
  |- imp(imp(p2,p3),imp(imp(p0,p1), imp(or(p0,p2),or(p1,p3))))
  |- imp(and(imp(p0,p1),imp(p2,p3)), imp(or(p0,p2),or(p1,p3)))
  |- imp(and(imp(p2,p3),imp(p0,p1)), imp(or(p0,p2),or(p1,p3)))
} = {
  and.joini(imp(p0,p1), imp(p2,p3), imp(or(p0,p2),or(p1,p3)))
  and.joini(imp(p2,p3), imp(p0,p1), imp(or(p0,p2),or(p1,p3)))
  com12i(imp(p2,p3), imp(p0,p1), imp(or(p0,p2),or(p1,p3)))
  and.spliti(imp(p0,p1), imp(p2,p3), imp(or(p0,p2),or(p1,p3)))
  rw23(and(imp(p0,p1),imp(p2,p3)), or(p0,p2), or(p1,p3), imp(not(p0),p2), imp(not(p1),p3))
  or.def.1(p0, p2)
  or.def.1(p1, p3)
  and.joini(imp(p0,p1), imp(p2,p3), imp(imp(not(p0),p2),imp(not(p1),p3)))
  syl(imp(p0,p1), imp(imp(p2,p3),imp(imp(not(p0),p2),imp(not(p1),p3))), imp(not(p1),not(p0)))
  trans4.2(not(p1), not(p0), p2, p3)
  con.4(p0, p1)
}
```

```follow
thm or.2orii(prop p0, prop p1, prop p2, prop p3) {
  |- imp(or(p0,p1),or(p2,p3))
  -| imp(p0,p2)
  -| imp(p1,p3)
} = {
  mp(imp(or(p0,p1),or(p2,p3)), imp(p0,p2))
  mp(imp(imp(p0,p2),imp(or(p0,p1),or(p2,p3))), imp(p1,p3))
  or.2or(p0, p2, p1, p3)
}
```

```follow
thm or.2oriid(prop p0, prop p1, prop p2, prop p3, prop p4) {
  |- imp(p0, imp(or(p1,p2),or(p3,p4)))
  -| imp(p0, imp(p1,p3))
  -| imp(p0, imp(p2,p4))
} = {
  mpd(p0, imp(or(p1,p2),or(p3,p4)), imp(p1,p3))
  syl(p0, imp(imp(p1,p3),imp(or(p1,p2),or(p3,p4))), imp(p2,p4))
  or.2or(p1, p3, p2, p4)
}
```

```follow
thm or.2or.2(prop p0, prop p1, prop p2) {
  |- imp(imp(p0,p1),imp(or(p0,p2),or(p1,p2)))
  |- imp(imp(p0,p1),imp(or(p2,p0),or(p2,p1)))
  |- imp(imp(p0,p1),imp(or(p0,p2),or(p2,p1)))
  |- imp(imp(p0,p1),imp(or(p2,p0),or(p1,p2)))
} = {
  rw2(imp(p0,p1), or(p0,p2), or(p2,p1), or(p2,p0))
  or.com(p0, p2)
  rw3(imp(p0,p1), or(p2,p0), or(p1,p2), or(p2,p1))
  or.com(p2, p1)
  mp(imp(imp(p0,p1),imp(or(p0,p2),or(p1,p2))), imp(p2,p2))
  or.2or(p0, p1, p2, p2)
  id(p2)
  mp(imp(imp(p0,p1),imp(or(p2,p0),or(p2,p1))), imp(p2,p2))
  or.2or(p2, p2, p0, p1)
  id(p2)
}
```

```follow
thm or.2ori.2(prop p0, prop p1, prop p2) {
  |- imp(or(p0,p2),or(p1,p2))
  |- imp(or(p2,p0),or(p2,p1))
  |- imp(or(p0,p2),or(p2,p1))
  |- imp(or(p2,p0),or(p1,p2))
  -| imp(p0, p1)
} = {
  mp(imp(or(p0,p2),or(p1,p2)), imp(p0,p1))
  mp(imp(or(p2,p0),or(p2,p1)), imp(p0,p1))
  mp(imp(or(p0,p2),or(p2,p1)), imp(p0,p1))
  mp(imp(or(p2,p0),or(p1,p2)), imp(p0,p1))
  or.2or.2(p0, p1, p2)
}
```

```follow
thm or.2orid.2(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p3, imp(or(p0,p2),or(p1,p2)))
  |- imp(p3, imp(or(p2,p0),or(p2,p1)))
  |- imp(p3, imp(or(p0,p2),or(p2,p1)))
  |- imp(p3, imp(or(p2,p0),or(p1,p2)))
  -| imp(p3, imp(p0, p1))
} = {
  syl(p3, imp(or(p0,p2),or(p1,p2)), imp(p0,p1))
  syl(p3, imp(or(p2,p0),or(p2,p1)), imp(p0,p1))
  syl(p3, imp(or(p0,p2),or(p2,p1)), imp(p0,p1))
  syl(p3, imp(or(p2,p0),or(p1,p2)), imp(p0,p1))
  or.2or.2(p0, p1, p2)
}
```

```follow
thm or.2or.3(prop p0, prop p1, prop p2) {
  |- imp(imp(not(p0),imp(p1,p2)),imp(or(p0,p1),or(p0,p2)))
  |- imp(imp(not(p0),imp(p1,p2)),imp(or(p1,p0),or(p0,p2)))
  |- imp(imp(not(p0),imp(p1,p2)),imp(or(p0,p1),or(p2,p0)))
  |- imp(imp(not(p0),imp(p1,p2)),imp(or(p1,p0),or(p2,p0)))
} = {
  rw2(imp(not(p0),imp(p1,p2)), or(p1,p0), or(p2,p0), or(p0,p1))
  or.com(p1, p0)
  rw3(imp(not(p0),imp(p1,p2)), or(p0,p1), or(p2,p0), or(p0,p2))
  or.com(p0, p2)
  rw2(imp(not(p0),imp(p1,p2)), or(p1,p0), or(p0,p2), or(p0,p1))
  or.com(p1, p0)

  rw23(imp(not(p0),imp(p1,p2)), or(p0,p1), or(p0,p2), imp(not(p0),p1), imp(not(p0),p2))
  or.def.1(p0, p1)
  or.def.1(p0, p2)
  a2(not(p0), p1, p2)
}
```

```follow
thm or.2ori.3(prop p0, prop p1, prop p2) {
  |- imp(or(p0,p1),or(p0,p2))
  |- imp(or(p1,p0),or(p0,p2))
  |- imp(or(p0,p1),or(p2,p0))
  |- imp(or(p1,p0),or(p2,p0))
  -| imp(not(p0),imp(p1,p2))
} = {
  mp(imp(or(p0,p1),or(p0,p2)), imp(not(p0),imp(p1,p2)))
  mp(imp(or(p1,p0),or(p0,p2)), imp(not(p0),imp(p1,p2)))
  mp(imp(or(p0,p1),or(p2,p0)), imp(not(p0),imp(p1,p2)))
  mp(imp(or(p1,p0),or(p2,p0)), imp(not(p0),imp(p1,p2)))
  or.2or.3(p0, p1, p2)
}
```

```follow
thm or.2orid.3(prop p0, prop p1, prop p2, prop p3) {
  |- imp(p3, imp(or(p0,p1),or(p0,p2)))
  |- imp(p3, imp(or(p1,p0),or(p0,p2)))
  |- imp(p3, imp(or(p0,p1),or(p2,p0)))
  |- imp(p3, imp(or(p1,p0),or(p2,p0)))
  -| imp(p3, imp(not(p0),imp(p1,p2)))
} = {
  syl(p3, imp(or(p0,p1),or(p0,p2)), imp(not(p0),imp(p1,p2)))
  syl(p3, imp(or(p1,p0),or(p0,p2)), imp(not(p0),imp(p1,p2)))
  syl(p3, imp(or(p0,p1),or(p2,p0)), imp(not(p0),imp(p1,p2)))
  syl(p3, imp(or(p1,p0),or(p2,p0)), imp(not(p0),imp(p1,p2)))
  or.2or.3(p0, p1, p2)
}
```

```follow
thm or.rw.left(prop p0, prop p1, prop p2) {
  |- or(p0, p1)
  -| or(p2, p1)
  -| imp(p2, p0)
} = {
  mp(or(p0,p1), or(p2,p1))
  or.2orii(p2, p1, p0, p1)
  id(p1)
}
```

```follow
thm or.rw.right(prop p0, prop p1, prop p2) {
  |- or(p0, p1)
  -| or(p0, p2)
  -| imp(p2, p1)
} = {
  mp(or(p0,p1), or(p0,p2))
  or.2orii(p0, p2, p0, p1)
  id(p0)
}
```

## 复合关系，`and.or`

```follow
thm and.or.1(prop p0, prop p1, prop p2) {
  |- imp(or(p0,and(p1,p2)), and(or(p0,p1),or(p0,p2)))
} = {
  and.introiid(or(p0,and(p1,p2)), or(p0,p1), or(p0,p2))
  or.casesii(p0, and(p1,p2), or(p0,p1))
  or.casesii(p0, and(p1,p2), or(p0,p2))
  or.left(p0, p2)
  syl(and(p1,p2), or(p0,p2), p2)
  and.right(p1, p2)
  or.right(p2, p0)
  or.left(p0, p1)
  syl(and(p1,p2), or(p0,p1), p1)
  and.left(p1, p2)
  or.right(p1, p0)
}
```

```follow
thm and.or.2(prop p0, prop p1, prop p2) {
  |- imp(or(and(p0,p1),p2), and(or(p0,p2),or(p1,p2)))
} = {
  and.introiid(or(and(p0,p1),p2), or(p0,p2), or(p1,p2))
  or.casesii(and(p0,p1), p2, or(p0,p2))
  syl(and(p0,p1), or(p0,p2), p0)
  and.left(p0, p1)
  or.left(p0, p2)
  or.right(p2, p0)
  or.casesii(and(p0,p1), p2, or(p1,p2))
  syl(and(p0,p1), or(p1,p2), p1)
  and.right(p0, p1)
  or.left(p1, p2)
  or.right(p2, p1)
}
```

```follow
thm and.or.3(prop p0, prop p1, prop p2, prop p3) {
  |- imp(or(and(p0,p1),and(p2,p3)), and(and(or(p0,p2),or(p0,p3)),and(or(p1,p2),or(p1,p3))))
} = {
  and.rw23(or(and(p0,p1),and(p2,p3)), and(or(p0,p2),or(p0,p3)), and(or(p1,p2),or(p1,p3)), or(p0,and(p2,p3)), or(p1,and(p2,p3)))
  and.or.1(p0, p2, p3)
  and.or.1(p1, p2, p3)
  and.or.2(p0, p1, and(p2,p3))
}
```

```follow
thm and.or.4(prop p0, prop p1, prop p2, prop p3) {
  |- imp(or(and(p0,p1),and(p2,p3)), and(and(or(p0,p2),or(p1,p2)),and(or(p0,p3),or(p1,p3))))
} = {
  and.rw23(or(and(p0,p1),and(p2,p3)), and(or(p0,p2),or(p1,p2)), and(or(p0,p3),or(p1,p3)), or(and(p0,p1),p2), or(and(p0,p1),p3))
  and.or.2(p0, p1, p2)
  and.or.2(p0, p1, p3)
  and.or.1(and(p0,p1), p2, p3)
}
```

## `or.and` 

```follow
thm or.and.1(prop p0, prop p1, prop p2) {
  |- imp(and(p0,or(p1,p2)), or(and(p0,p1),and(p0,p2)))
} = {
  and.joini(p0, or(p1,p2), or(and(p0,p1),and(p0,p2)))
  or.casesiid(p0, p1, p2, or(and(p0,p1),and(p0,p2)))
  rw3(p0, p1, or(and(p0,p1),and(p0,p2)), and(p0,p1))
  and.intro(p0, p1)
  or.left(and(p0,p1), and(p0,p2))
  rw3(p0, p2, or(and(p0,p1),and(p0,p2)), and(p0,p2))
  and.intro(p0, p2)
  or.right(and(p0,p2), and(p0,p1))
}
```

```follow
thm or.and.2(prop p0, prop p1, prop p2) {
  |- imp(and(or(p0,p1),p2),or(and(p0,p2),and(p1,p2)))
} = {
  and.joini(or(p0,p1), p2, or(and(p0,p2),and(p1,p2)))
  or.casesii(p0, p1, imp(p2,or(and(p0,p2),and(p1,p2))))
  rw3(p0, p2, or(and(p0,p2),and(p1,p2)), and(p0,p2))
  and.intro(p0, p2)
  or.left(and(p0,p2), and(p1,p2))
  rw3(p1, p2, or(and(p0,p2),and(p1,p2)), and(p1,p2))
  and.intro(p1, p2)
  or.right(and(p1,p2), and(p0,p2))
}
```

```follow
thm or.and.3(prop p0, prop p1, prop p2, prop p3) {
  |- imp(and(or(p0,p1),or(p2,p3)), or(or(and(p0,p2),and(p0,p3)),or(and(p1,p2),and(p1,p3))))
} = {
  or.rw23(and(or(p0,p1),or(p2,p3)), or(and(p0,p2),and(p0,p3)), or(and(p1,p2),and(p1,p3)), and(p0,or(p2,p3)), and(p1,or(p2,p3)))
  or.and.1(p0, p2, p3)
  or.and.1(p1, p2, p3)
  or.and.2(p0, p1, or(p2,p3))
}
```

```follow
thm or.and.4(prop p0, prop p1, prop p2, prop p3) {
  |- imp(and(or(p0,p1),or(p2,p3)), or(or(and(p0,p2),and(p1,p2)),or(and(p0,p3),and(p1,p3))))
} = {
  or.rw23(and(or(p0,p1),or(p2,p3)), or(and(p0,p2),and(p1,p2)), or(and(p0,p3),and(p1,p3)), and(or(p0,p1),p2), and(or(p0,p1),p3))
  or.and.2(p0, p1, p2)
  or.and.2(p0, p1, p3)
  or.and.1(or(p0,p1), p2, p3)
}
```

### 复合推理拆解 

```follow
thm and.or.split(prop p0, prop p1, prop p2, prop p3, prop p4) {
  |- imp(and(p0,p1),imp(or(p2,p3),p4))
  -| imp(p0, imp(p2, p4))
  -| imp(p1, imp(p3, p4))
} = {
  or.casesiid(and(p0,p1), p2, p3, p4)
  syl(and(p0,p1), imp(p2,p4), p0)
  and.left(p0, p1)
  syl(and(p0,p1), imp(p3,p4), p1)
  and.right(p0, p1)
}
```

```follow
thm or.and.split(prop p0, prop p1, prop p2, prop p3, prop p4) {
  |- imp(or(p0,p1),imp(and(p2,p3),p4))
  -| imp(p0, imp(p2, p4))
  -| imp(p1, imp(p3, p4))
} = {
  or.casesii(p0, p1, imp(and(p2,p3),p4))
  rw2(p0, and(p2,p3), p4, p2)
  and.left(p2, p3)
  rw2(p1, and(p2,p3), p4, p3)
  and.right(p2, p3)
}
```

## 复合关系 `or.not`, 排中律 Excluded Middle 

```follow
thm or.not(prop p0) {
  |- or(p0,not(p0))
  |- or(not(p0),p0)
} = {
  or.comi(not(p0), p0)
  mp(or(p0,not(p0)), imp(not(p0),not(p0)))
  or.def.1(p0, not(p0))
  id(not(p0))
}
```
